; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ Batski control.							   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

CAR_ZF		EQU	40;20

BAT_AIR_DEC	EQU	620;180
BAT_AIR_MIN	EQU	570;100
BAT_AIR_REBOUND	EQU	400;300

; --------------------------------------------------------------------------

CAR_CONTROL	IF	DAMAGE=NO
		CLR.W	OBJ_DTOTAL(A1)
		CLR.W	OBJ_BASH_AMT(A1)
		CLR.W	OBJ_SHOT_AMT(A1)
		ENDIF

		MOVE.W	OBJ_SPD(A1),D0
		MOVE.W	D0,D1
		MULS	#8000,D0
		BPL.S	.slim
		MOVEQ	#0,D0
		MOVEQ	#0,D1
		BRA.S	.sset
.slim		SWAP	D0
		CMP.W	#16,D0
		BCS.S	.sset
		MOVE.W	#16,D0
.sset		MOVE.W	D0,SRAM_DATA+WRAM_SPEED
		MOVE.W	D1,SRAM_DATA+WRAM_SPDNUM

		BCLR	#O_SCRAPE,OBJ_FLAG0(A1)
		MOVE.W	JOY0,D6

; --- Auto ---
		
		MOVE.W	STAGE_AUTO,D0
		BEQ	.update
		
		CLR.W	OBJ_BASH_AMT(A1)
		CLR.W	OBJ_SHOT_AMT(A1)
		AND.W	#%11010000,D6
		TST.W	D0
		BMI	.update

		OR.W	#%00101000,D6
		MOVE.L	OBJ_XPOS(A1),D1
		SUB.L	#200*$100,D1
		BPL.S	.apos
.aneg		CMP.L	#-20*$100,D1
		BPL.S	.adone
		BSET	#0,D6
		CMP.L	#-70*$100,D1
		BPL.S	.adone
		BCLR	#5,D6
		CMP.L	#-110*$100,D1
		BPL.S	.adone
		BCLR	#3,D6
		BRA.S	.adone
.apos		CMP.L	#20*$100,D1
		BMI.S	.adone
		BSET	#1,D6
		CMP.L	#70*$100,D1
		BMI.S	.adone
		BCLR	#5,D6
		CMP.L	#110*$100,D1
		BMI.S	.adone
		BCLR	#3,D6
.adone		CMP.W	#$40,OBJ_SPD(A1)
		BPL.S	.atype
		BSET	#5,D6

.atype		CMP.W	#2,D0
		BNE.S	.update
		AND.W	#%10000011,D6
		OR.W	#%00000100,D6
		MOVE.W	STAGE_GOTO,D2
		SUB.W	OBJ_SPD(A1),D2
		MOVE.W	D2,STAGE_GOTO
		BMI.S	.update
		CMP.W	#$30,OBJ_SPD(A1)
		BPL.S	.update
		AND.W	#%10000011,D6
		CMP.W	#2*$80,D2
		BMI.S	.update
		OR.W	#%00001000,D6

; --- Update ---

.update		MOVE.W	OBJ_SPD(A1),D0
		CMP.W	#-8,D0
		BPL.S	.brake_chk
		BSET	#5,D6
.brake_chk	MOVE.W	D6,D5
		AND.W	#%00101000,D5
		BEQ.S	.joyset
		AND.W	#%11111011,D6
.joyset		MOVE.W	D6,JOY0
	
		BSR	O_WHEEL_UPDATE

.spd_calc	BSR	O_SPEED_UPDATE
.spd_skp	MOVE.W	OBJ_SPD(A1),D0
		SUB.W	#$30,OBJ_ZPOS(A1)
		BSR	O_CALC_MOVEAMTS
		ADD.W	#$30,OBJ_ZPOS(A1)
		MOVE.L	D3,-(SP)
		BSR	O_ROAD_MOVE
		BSR	O_DET_PLIMITS

; --- View angle ---

		MOVEQ	#$20,D1
		MOVE.W	OBJ_WHEEL(A1),D2
		BPL.S	.view_calc
		NEG.W	D1
		NEG.W	D2
.view_calc	MOVE.W	#$20*2,D0
		CMP.W	#$800,D2
		BCS.S	.view_set
		ADD.W	D1,D0
		CMP.W	#$2000,D2
		BCS.S	.view_set
		ADD.W	D1,D0
.view_set	MOVE.W	D0,OBJ_CNT0(A1)	

; --- Off ground ---
      
		MOVE.L	(SP)+,D3
		
		MOVE.W	#3,D7
		BSR	RANDOM_RANGE_U
		ADD.W	#1,D7
		MULS	D7,D3

		MOVE.L	OBJ_ADDR7(A1),D1
		MOVE.L	OBJ_YPOS(A1),D0
		BNE.S	.air_time

		MOVE.L	D1,D2
		SUB.L	D3,D2
		ADD.L	#BAT_AIR_MIN,D2
		BPL	.air_skp
		ASL.L	#8,D1
		DIVS	#BAT_AIR_REBOUND,D1
		EXT.L	D1

.air_time	ADD.L	#BAT_AIR_DEC,D1
		ADD.L	D1,D0
		BPL.S	.land
		MOVE.W	OBJ_ZPOS(A1),D7
		ADD.W	ZVIEW,D7
		LSR.W	#7,D7
		MULU	#RCSTEP,D7
		LEA	ROAD_CORD_DATA-RCSTEP,A5
		MOVE.L	OBJ_YSIZE(A1),D6
		ASR.L	#1,D6
		ADD.L	OBJ_YSIZE(A1),D6
		NEG.L	D6
		ADD.L	D0,D6
		SUB.L	RCROOF(A5,D7.W),D6
		BCC	.land_skp
		SUB.L	D6,D0
		MOVEM.L	D0/D1/A1,-(SP)
		LEA	VIS_ROOF_SPARK0,A0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVE.L	OBJ_YPOS(A1),D2
		SUB.L	OBJ_YSIZE(A1),D2
		BSR	VIS_SET_ZS
		MOVEM.L	(SP)+,D0/D1/A1
		BSET	#O_SCRAPE,OBJ_FLAG0(A1)
		BRA.S	.land_skp
		
.land		CMP.L	#$900,D1
		BMI.S	.land_done

		MOVEM.L	D1/A1,-(SP)
		MOVE.W	OBJ_ZPOS(A1),D0
		ADD.W	OBJ_ZSIZE(A1),D0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVE.L	#0,D2
		MOVE.W	OBJ_SPD(A1),D3

		LEA	VIS_WSPLASH,A0
		BSR	VIS_SET
		MOVE.L	A1,A2
		MOVEM.L	(SP)+,D1/A1
		BNE.S	.land_done
      		
		MOVE.W	D1,D0			;Set size.
		ASR.W	#4,D0
		NEG.W	D0
		ADD.W	#$1D0,D0
		MOVE.W	D0,OBJ_CNT7(A2)

.land_done	MOVEQ	#0,D0
		NEG.L	D1
.land_skp	MOVE.L	D1,D3
		MOVE.L	D0,OBJ_YPOS(A1)
.air_skp	MOVE.L	D3,OBJ_ADDR7(A1)

		BSR	trail

; --- Misc. ---

		MOVE.W	ROAD_CORD_DATA+RCSTEP+RCROOF,TUNNEL_SFX_FLAG

		IF	BATCD_SHELL
		TST.B	shl_sfx
		BEQ.S	.sskp5
		ENDIF

		MOVE.W	JOY0,D0		
   		AND.W	#%00101100,D0
		BTST	#5,D0
		BEQ.S	.skid_chk
		BSET	#3,D0
.skid_chk	BTST	#O_SKID,OBJ_FLAG0(A1)
		BEQ.S	.sskp0
		BSET	#4,D0
.sskp0		TST.L	OBJ_YPOS(A1)
		BPL.S	.sskp1
		BSET	#6,D0
.sskp1		BTST	#O_SCRAPE,OBJ_FLAG0(A1)
		BEQ.S	.sskp2
		BSET	#1,D0
.sskp2		
.sskp3		CMP.W	#4*$FFFF/6,OBJ_DTOTAL(A1)
		BCS.S	.sskp4
		BSET	#7,D0
.sskp4		MOVE.W	OBJ_SPD(A1),D1
		
		MOVE.L	A1,-(SP)
		JSR	CSFX_BATSKI
		MOVE.L	(SP)+,A1
.sskp5
		TST.B	OBJ_FLAG3(A1)
		BEQ.S	.damage_norm
		SUBQ.B	#1,OBJ_FLAG3(A1)
		BSR	DAMAGE_HEAL
		BRA.S	.damage_meter
.damage_norm	BSR	DAMAGE_CTRL
.damage_meter	BSR	PDAMAGE_METER

		BRA	SHOT_SETCHECK

; --------------------------------------------------------------------------

trail
		CMP.W	#$10,OBJ_SPD(A1)
		BMI	.done
		
		BCHG	#O_TIRE,OBJ_FLAG0(A1)
		BEQ.S	.done

		CMP.L	#-16*$100,OBJ_YPOS(A1)
		BMI	.done

		MOVE.L	A1,-(SP)
		LEA	VIS_WAKE_L,A0
		MOVE.W	OBJ_ZPOS(A1),D0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVE.L	#0,D2
		MOVE.W	OBJ_SPD(A1),D3
		MOVEM.L	A1/D0/D1/D2/D3,-(SP)	
	
		SUB.L	#56*$100,D1
		ADD.W	#$60,D0

		BSR	VIS_SET
		
		MOVEM.L	(SP),A1/D0/D1/D2/D3
		LEA	VIS_WAKE_R,A0
		ADD.L	#56*$100,D1
		ADD.W	#$60,D0
		BSR	VIS_SET
		
		MOVEM.L	(SP),A1/D0/D1/D2/D3
		LEA	VIS_WAKE,A0
		SUB.L	#24*$100,D1
		BSR	VIS_SET
		
		MOVEM.L	(SP)+,A1/D0/D1/D2/D3
		LEA	VIS_WAKE,A0
		ADD.L	#24*$100,D1
		BSR	VIS_SET
		
		MOVE.L	(SP)+,A1
.done		
		RTS



VIS_WAKE_L
		DC.W	$0000,$0300,$0300
		DC.W	-$0A,-$400,-$400
		DC.W	$0100,$0400
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0NC
		DC.L	VIS_WAKE_GRS
		DC.W	0

VIS_WAKE_R
		DC.W	$0000,$0300,$0300
		DC.W	-$0A,$400,-$400
		DC.W	$0100,$0400
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0NC
		DC.L	VIS_WAKE_GRS
		DC.W	0

VIS_WAKE
		DC.W	$0000,$0300,$0300
		DC.W	-$0A,0,-$400
		DC.W	$0100,$0300
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0NC
		DC.L	VIS_WAKE_GRS
		DC.W	0

VIS_WAKE_GRS
		XYRATIO	100,190,100,190
		OBJCNTR	35,131,16,2

		OBJSCL	35,131,0,0,16,10,WAKE00
		OBJSCL	32,130,17,0,24,15,WAKE00
		OBJSCL	31,131,42,0,27,18,WAKE00
		OBJSCL	28,133,70,0,33,22,WAKE00
		OBJSCL	27,137,104,0,37,17,WAKE00

; --------------------------------------------------------------------------

SHOT_PTIME	EQU	2
SHOT_FMIN	EQU	5

SHOT_RANGE	EQU	$700
SHOT_ZMOVE	EQU	$60
SHOT_ZINIT	EQU	$198+$38
SHOT_ZOFF	EQU	$C00

SHOT_XMOVE	EQU	15
SHOT_XMAX	EQU	15*5

SHOT_SETCHECK	MOVE.W	OBJ_CNT6(A1),D0
		MOVE.W	#SHOT_XMOVE,D1
		BTST	#0,JOY0+1
		BNE.S	.move
		BTST	#1,JOY0+1
		BEQ.S	.center
		NEG.W	D1
.move		ADD.W	D1,D0
		BPL.S	.iright
.ileft		CMP.W	#-SHOT_XMAX,D0
		BPL.S	.xset
		MOVE.W	#-SHOT_XMAX,D0
		BRA.S	.xset
.iright		CMP.W	#SHOT_XMAX,D0
		BMI.S	.xset
		MOVE.W	#SHOT_XMAX,D0
		BRA.S	.xset
.center		TST.W	D0
		BPL.S	.cplus
.cminus		ADD.W	D1,D0
		BMI.S	.xset
		BRA.S	.exact
.cplus		SUB.W	D1,D0
		BPL.S	.xset
.exact		MOVEQ	#0,D0
.xset		MOVE.W	D0,OBJ_CNT6(A1)

		BSR	MISS_SETCHECK

		MOVE.B	SHOT_FLAG,D0
		MOVE.W	JOY0,D1
		MOVE.B	D1,SHOT_FLAG
		
		TST.W	STAGE_AUTO
		BNE	.end
		
		NOT.B	D0	
		AND.B	D1,D0
		AND.B	#%01000000,D0
		BEQ	.fcheck
		MOVE.W	#SHOT_FMIN,SHOT_FCOUNT
		SUBQ.W	#1,SHOT_PAUSE
		
.fcheck		TST.W	SHOT_PAUSE
		BGT	.wait
		AND.B	#%01000000,D1
		BEQ	.wait
		TST.W	SHOT_FCOUNT
		BEQ	.end
		SUBQ.W	#1,SHOT_FCOUNT

		BSR	OBJECT_ALLOC
		BNE	.wait
		
		LEA	OBJECT_DATA,A0
		MOVE.W	#2,OBJ_CNT1(A0)
		MOVE.W	#SHOT_PTIME,SHOT_PAUSE

		LEA	SHOT_CAR_GTAB,A3
		MOVE.W	#4*2,D5
		MOVE.W	#4,D6
		MOVE.W	OBJ_CNT6(A0),D7
		BPL.S	.gcalc
		NEG.W	D6
		NEG.W	D7
.gcalc		CMP.W	#20,D7
		BCS.S	.gset
		ADD.W	D6,D5
		CMP.W	#40,D7
		BCS.S	.gset
		ADD.W	D6,D5
.gset		ADD.W	D5,A3

		LEA	SHOT_CAR_XPOS,A4
		MOVE.W	OBJ_CNT0(A0),D7
		ASR.W	#2,D7
		ADD.W	D7,A4

		BSR.S	.set

		BSR	OBJECT_ALLOC
		BNE	.end
		BSR.S	.set
		
		MOVE.W	#SFX_BSFIRE_A,D0
		JMP	CSFX_ONESHOT_V

.set		MOVE.W	#$10,OBJ_ZSIZE(A1)
		MOVE.L	#$10*$100,OBJ_XSIZE(A1)
		MOVE.L	#$10*$100,OBJ_YSIZE(A1)
		
		MOVE.W	OBJ_ZPOS(A0),D0
		ADD.W	OBJ_ZSIZE(A0),D0
		MOVE.W	D0,OBJ_ZPOS(A1)
		
		MOVE.L	OBJ_YPOS(A0),D0
		SUB.L	#$100*30,D0
		MOVE.L	D0,OBJ_YPOS(A1)
		
		MOVE.W	#ID_SHOT,OBJ_ID(A1)
		MOVE.W	#SHOT_ZMOVE,OBJ_CNT0(A1)
		MOVE.L	#SHOT_MOVE,OBJ_MOVE_CTRL(A1)
		MOVE.L	#SHOT_PRINT,OBJ_PRN_CTRL(A1)

		MOVE.W	OBJ_CNT6(A0),D0
		SUB.W	D0,OBJ_CNT1(A1)
		
		MOVE.L	OBJ_XPOS(A0),D0
		ADD.L	(A4)+,D0
		MOVE.L	D0,OBJ_XPOS(A1)
		
		MOVE.L	(A3),OBJ_ADDR0(A1)
		RTS

.wait		SUBQ.W	#1,SHOT_PAUSE
		BPL.S	.end
		CLR.W	SHOT_PAUSE
.end		RTS

SHOT_CAR_XPOS	DC.L	-40*$100,8*$100
		DC.L	-34*$100,14*$100
		DC.L	-24*$100,24*$100
		DC.L	-14*$100,34*$100
		DC.L	-8*$100,40*$100

SHOT_CAR_GTAB	DC.L	OBJ_BSMISS_0
		DC.L	OBJ_BSMISS_1
		DC.L	OBJ_BSMISS_2
		DC.L	OBJ_BSMISS_3
		DC.L	OBJ_BSMISS_4

; --------------------------------------------------------------------------

SHOT_MOVE	MOVE.W	OBJ_ZPOS(A1),D0
		BMI	O_CLEAR_S
		CMP.W	#SHOT_RANGE,D0
		BPL	O_CLEAR_S

.mover		BSR	O_CALC_PATCH		
		NEG.W	D4
		ADD.W	#$80,D4

		MOVEQ	#0,D0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVE.W	ZMOVE,D3
		BMI.S	.mgo

.zloop		SUB.W	D4,D3
		BPL.S	.zcalc
		ADD.W	D3,D4
		BLE.S	.mset
.zcalc		ADD.W	D4,D0			;Zmove update.
		MOVE.W	RCXFRACT(A5),D6
		MULS	D6,D4
		SUB.L	D4,D1
		LEA	RCSTEP(A5),A5
		MOVE.W	#$80,D4
		BRA.S	.zloop

.mset		NEG.W	D4
.mgo		MOVE.W	OBJ_CNT0(A1),D3

.mloop		SUB.W	D4,D3
		BPL.S	.mcalc
		ADD.W	D3,D4
		BLE.S	.mdone
.mcalc		ADD.W	D4,D0			;Zmove update.
		MOVE.W	OBJ_CNT1(A1),D6
		ADD.W	RCXFRACT(A5),D6
		MULS	D6,D4
		SUB.L	D4,D1
		LEA	RCSTEP(A5),A5
		MOVE.W	#$80,D4
		BRA.S	.mloop

.mdone		SUB.L	OBJ_XPOS(A1),D1		;Xmove

		LEA	SIGN_DET_ZXKILL,A5	;Sign hit ajust routine.
		MOVE.L	OBJECT_DET_LIST+((OBJECT_MAX-1)*4),-(SP)
		CLR.L	OBJECT_DET_LIST+((OBJECT_MAX-1)*4)
		BSR	DETECT_ZXY
		MOVE.L	(SP)+,OBJECT_DET_LIST+((OBJECT_MAX-1)*4)
		ADD.W	D0,OBJ_ZPOS(A1)
		ADD.L	D1,OBJ_XPOS(A1)
		MOVE.W	DET_HIT_FLAG,D0
		BNE.S	.detect_hit
		RTS

.detect_hit	CMP.W	#OBJ_HIT,D0
		BCC	.object_hit

.sign_hit	MOVE.W	#SFX_BSHIT_B,D0
		JSR	CSFX_ONESHOT_V

		LEA	VIS_SPARK_0,A0
		MOVEQ	#0,D3

.set_vis	MOVE.L	A1,-(SP)
		BSR	VIS_SET_YZX
		MOVE.L	(SP)+,A1
		BRA	O_CLEAR_S

.object_hit	MOVE.W	#SFX_BSHIT_A,D0
		JSR	CSFX_ONESHOT_V

		MOVE.L	DET_HIT_DATA1,A2
		CMP.W	#ID_CAR,OBJ_ID(A2)
		BNE.S	.skp
		ADDQ.W	#2,OBJ_SHOT_AMT(A2)
.skp		MOVE.W	OBJ_SPD(A2),D3
		LEA	VIS_WHACKS_0,A0

		MOVEM.L	A1/A2,-(SP)
		BSR	VIS_SET_YZX
		MOVE.L	A1,A3
		MOVEM.L	(SP)+,A1/A2
		BNE	O_CLEAR_S
		CMP.L	A1,A2
		BCS	O_CLEAR_S
		MOVE.W	OBJ_SPD(A2),D0
		ADD.W	D0,OBJ_ZPOS(A3)
		BRA	O_CLEAR_S

; --------------------------------------------------------------------------

SHOT_PRINT	BSR	O_PR_ALLOCX_XIT
		BSR	O_CALC_PATCHZ
		MOVE.L	OBJ_ADDR0(A1),A0
		BSR	O_CALC_VIEW
		MOVE.L	OBJ_ADDR1(A1),A2
		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		BRA	O_PR_SET0

; --------------------------------------------------------------------------

		XYRATIO	100,160,100,160
		OBJCNTR	0,0,10,6

OBJ_BSMISS_0	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCLM	-2,0,22,0,12,11,BSMISS
		OBJSCLM	-2,0,22,0,12,11,BSMISS
		OBJSCLM	-2,0,22,0,12,11,BSMISS
		OBJSCLM	-1,0,11,0,10,12,BSMISS
		OBJSCL	0,0,0,0,10,13,BSMISS

OBJ_BSMISS_1	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCLM	-2,0,22,0,12,11,BSMISS
		OBJSCLM	-2,0,22,0,12,11,BSMISS
		OBJSCLM	-1,0,11,0,10,12,BSMISS
		OBJSCL	0,0,0,0,10,13,BSMISS
		OBJSCL	-1,0,11,0,10,12,BSMISS

OBJ_BSMISS_2	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCLM	-2,0,22,0,12,11,BSMISS
		OBJSCLM	-1,0,11,0,10,12,BSMISS
		OBJSCL	0,0,0,0,10,13,BSMISS
		OBJSCL	-1,0,11,0,10,12,BSMISS
		OBJSCL	-2,0,22,0,12,11,BSMISS

OBJ_BSMISS_3	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCLM	-1,0,11,0,10,12,BSMISS
		OBJSCL	0,0,0,0,10,13,BSMISS
		OBJSCL	-1,0,11,0,10,12,BSMISS
		OBJSCL	-2,0,22,0,12,11,BSMISS
		OBJSCL	-2,0,22,0,12,11,BSMISS

OBJ_BSMISS_4	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCL	0,0,0,0,10,13,BSMISS
		OBJSCL	-1,0,11,0,10,12,BSMISS
		OBJSCL	-2,0,22,0,12,11,BSMISS
		OBJSCL	-2,0,22,0,12,11,BSMISS
		OBJSCL	-2,0,22,0,12,11,BSMISS

; --------------------------------------------------------------------------

MISS_PTIME	EQU	10
MISS_ZMOVE	EQU	$60
MISS_ZINIT	EQU	$80
MISS_DAMAGE	EQU	10

MISS_SETCHECK	MOVE.B	MISS_FLAG,D0
		MOVE.W	JOY0,D1
		MOVE.B	D1,MISS_FLAG
		NOT.B	D0	
		AND.B	D1,D0
		AND.B	#%00010000,D0
		BEQ	.wait
		TST.W	MISSTOT
		BEQ	.end
		MOVE.W	MISS_PAUSE,D0
		OR.W	STAGE_AUTO,D0
		BNE.S	.wait

		LEA	OBJ_BTMISS,A6
		BSR	O_OBJECT_SETUP
		BNE	.end
		
		LEA	OBJECT_DATA,A0
		MOVE.W	OBJ_ZPOS(A0),D0
		ADD.W 	#MISS_ZINIT,D0
		MOVE.W	D0,OBJ_ZPOS(A1)
		MOVE.L	OBJ_XPOS(A0),OBJ_XPOS(A1)
		MOVE.L	OBJ_YPOS(A0),D0
		SUB.L	#$1000,D0
		MOVE.L	D0,OBJ_YPOS(A1)

		MOVE.W	OBJ_WHEEL(A0),D0
		EXT.L	D0
		MOVE.L	D0,OBJ_CNT0(A1)

		MOVE.L	#MISS_HIT_OBJECT,OBJ_ADDR7(A1)
		SUBQ.W	#1,MISSTOT
		MOVE.W	#MISS_PTIME,MISS_PAUSE
		
		MOVE.W	#SFX_BRFIRE_A,D0
		JSR	CSFX_ONESHOT_V

		RTS

.wait		SUBQ.W	#1,MISS_PAUSE
		BPL.S	.end
		CLR.W	MISS_PAUSE
.end		RTS

; --------------------------------------------------------------------------

MISS_FIND	MOVEQ	#0,D3
		MOVE.W	#(VIEW_PATCHES+1)*$80,D1
		MOVE.W	OBJ_ZPOS(A1),D2
		LEA	OBJECT_DATA,A2
		MOVEQ	#OBJECT_MAX-2,D7
		
.loop		LEA	OBJ_DLEN(A2),A2
		TST.B	OBJ_FLAG0(A2)
		BEQ.S	.next	
		CMP.W	#ID_CAR,OBJ_ID(A2)
		BNE.S	.next
		MOVE.W	OBJ_ZPOS(A2),D0
		ADD.W	OBJ_ZSIZE(A2),D0
		CMP.W	D2,D0
		BMI.S	.next
		SUB.W	OBJ_ZSIZE(A2),D0
		CMP.W	D1,D0
		BPL.S	.next
		MOVE.L	A2,D3
		MOVE.W	D0,D1
.next		DBRA	D7,.loop
		RTS

; --------------------------------------------------------------------------

MISS_MOVE	
		BSR	O_RANGE_CHK 	
		BRA.L	MISS_KILL
		BRA.L	MISS_KILL

		BSR	MISS_FIND
		
		MOVE.W	#MISS_ZMOVE,D0
		ADD.W	ZMOVE,D0
		MOVEQ	#0,D1
		MOVEQ	#0,D2
		
		TST.L	D3
		BEQ.S	.xrange
		MOVE.L	D3,A2
		MOVE.W	OBJ_ZPOS(A2),D0
		SUB.W	OBJ_ZPOS(A1),D0
		ASR.W	#3,D0
		ADD.W	#$10,D0
		ADD.W	OBJ_SPD(A2),D0
		CMP.W	#$180,D0
		BMI.S	.xmove
		MOVE.W	#$180,D0
.xmove		MOVE.L	#$800,D1
		MOVE.L	OBJ_XPOS(A1),D4
		SUB.L	OBJ_XPOS(A2),D4
		BMI.S	.ymove
		NEG.L	D1
.ymove		MOVE.L	#$400,D2
		MOVE.L	OBJ_YPOS(A2),D4
		SUB.L	OBJ_YPOS(A1),D4
		ASL.L	D4
		SUB.L	OBJ_YSIZE(A2),D4
		ADD.L	OBJ_YSIZE(A1),D4
		BPL.S	.xrange
		NEG.L	D2

.xrange		ADD.L	OBJ_CNT0(A1),D1
		BPL.S	.xpos
		CMP.L	#-$1400,D1
		BPL.S	.xset
		MOVE.L	#-$1400,D1
		BRA.S	.xset
.xpos		CMP.L	#$1400,D1
		BMI.S	.xset
		MOVE.L	#$1400,D1
.xset		MOVE.L	D1,OBJ_CNT0(A1)

.yrange		ADD.L	OBJ_CNT2(A1),D2
		BPL.S	.ypos
		CMP.L	#-$E00,D2
		BPL.S	.yset
		MOVE.L	#-$E00,D2
		BRA.S	.yset
.ypos		CMP.L	#$E00,D2
		BMI.S	.yset
		MOVE.L	#$E00,D2
.yset		MOVE.L	D2,OBJ_CNT2(A1)
		ADD.L	D2,OBJ_YPOS(A1)

		LEA	SIGN_DET_ZXKILL,A5	;Sign hit adjust routine.
		CLR.L	OBJECT_DET_LIST
		BSR	DETECT_ZXY
		MOVE.L	#OBJECT_DATA,OBJECT_DET_LIST
		ADD.W	D0,OBJ_ZPOS(A1)
		ADD.L	D1,OBJ_XPOS(A1)

		MOVE.W	DET_HIT_FLAG,D0
		BNE.S	.hit
		LEA	VIS_SMOKE_MISS,A0
		BRA	VIS_SET_XYZS

.hit		CMP.W	#OBJ_HIT,D0
		BCS.S	MISS_HIT_SIGN
		MOVE.L	DET_HIT_DATA1,A2
		BRA.S	MISS_HIT_OBJECT
.end		RTS

MISS_KILL	
;		CLR.W	MISS_PAUSE
		BRA.L	O_CLEAR_S

MISS_HIT_OBJECT	CMP.W	#ID_MAIN,OBJ_ID(A2)
		BNE.S	.cont
		RTS
.cont		CMP.W	#ID_CAR,OBJ_ID(A2)
		BNE.S	.skp
		ADD.W	#MISS_DAMAGE,OBJ_SHOT_AMT(A2)
		ADDQ.B	#6,OBJ_FLASH1(A2)
.skp		MOVE.W	OBJ_SPD(A2),OBJ_SPD(A1)

MISS_HIT_SIGN
		BSR	MISS_KILL
		
		LEA	VIS_EXPLD_A,A0
		BRA	VIS_SET_XYZS

MISS_PRINT	MOVE.L	OBJ_ADDR1(A1),A2
		BSR	O_PR_ALLOCX
		BRA.L	.deall
		BRA.L	.deall
		BRA.L	.deall

		BSR	O_CALC_PATCHZ
		MOVE.L	OBJ_ADDR0(A1),A3
		MOVE.L	OBJGR_ANM0(A3),A0
		BSR	O_CALC_VIEW

		MOVEQ	#0,D1
		MOVE.W	#$20,D2
		MOVE.W	OBJ_CNT1(A1),D3
		BPL.S	.xfrmp
		NEG.W	D2
		NEG.W	D3
.xfrmp		CMP.W	#$600,D3
		BMI.S	.yfrm
		ADD.W	D2,D1
		CMP.W	#$C00,D3
		BMI.S	.yfrm
		ADD.W	D2,D1
.yfrm		MOVE.W	#9*$20,D2
		MOVE.W	OBJ_CNT3(A1),D3
		BPL.S	.yfrmp
		NEG.W	D2
		NEG.W	D3
.yfrmp		CMP.W	#$600,D3
		BMI.S	.frmset
		ADD.W	D2,D1
.frmset		LEA	(A0,D1.W),A0

		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		BRA	O_PR_SET0

.deall		CLR.L	OBJ_ADDR1(A1)
		BRA	O_DEALL_S

; --------------------------------------------------------------------------
	
CAR_PRINT	
		BSR	O_CALC_PATCH
		MOVE.W	OBJ_CNT0(A1),D3

		TST.W	OBJ_CNT5(A1)
		BEQ.S	.skp
		SUBQ.W	#1,OBJ_CNT5(A1)
		BRA	PBR	
	
.skp		CLR.W	OBJ_CNT4(A1)

		BSR	PR_BATFLAME
		BRA	PR_BATMOBILE

PBR	       
		LEA	OBJ_MOBILE_SPN0,A0
  		BTST	#5,JOY0+1
		BEQ.S	.skp
		LEA	OBJ_MOBILE_SPN1,A0
.skp
		BSR	O_PR_ALLOC_XIT
		BSR	O_CALC_PATCHZ
		MOVE.L	OBJ_ADDR1(A1),A2
		BSR	O_FLASH_CHECK_S
		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		
		MOVE.W	OBJ_CNT4(A1),D2
		ADD.W	OBJ_CNT2(A1),D2
		MOVE.W	D2,OBJ_CNT4(A1)
		AND.W	#$3FE,D2
		BRA	O_PR_ROTATE


PR_BATMOBILE	BSR	O_PR_ALLOC_XIT
		
		LEA	SKI_TAB,A0
		MOVE.L	OBJ_YPOS(A1),D1
		BMI.S	.pcalc
		MOVEQ	#0,D1
		BRA.S	.pset
.pcalc		NEG.W	D1
		MULU	#$155,D1
		SWAP	D1
		AND.W	#%11111100,D1
		CMP.W	#5*4,D1
		BLE.S	.pset
		MOVE.W	#5*4,D1
.pset		MOVE.L	(A0,D1.W),A0

		MOVE.L	OBJ_ADDR1(A1),A2
		LEA	8(A0,D3.W),A0
		BSR	O_FLASH_CHECK_S
		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		
		TST.L	OBJ_YPOS(A1)
		BNE	O_PR_SET0
		BRA	O_PR_SET0
		BRA	O_PR_DIFF0
		
PR_BATFLAME	MOVE.W	OBJ_CNT7(A1),D1
		ADDQ.W	#4,D1
		CMP.W	#3*4,D1
		BCS.S	.askp
		MOVEQ	#0,D1
.askp		MOVE.W	D1,OBJ_CNT7(A1)
		
	  	LEA	FLAME_TAB,A0
  		BTST	#5,JOY0+1
	  	BEQ.S	.fskp
		LEA	FLAME_TAB+12,A0
.fskp		MOVE.L	(A0,D1.W),D1
		BNE.S	.fprint
		RTS
		
.fprint		MOVE.L	D1,A0
		BSR	O_PZ_ALLOC_XIT
		MOVE.L	OBJ_ADDR3(A1),A2
		LEA	(A0,D3.W),A0
		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR3(A1)
		BRA	O_PR_SET0
		BRA	O_PR_DIFF0

; --------------------------------------------------------------------------

FLAME_TAB	DC.L	0,OBJ_FLAME_NA1,OBJ_FLAME_NA2
		DC.L	OBJ_TURBO_NA0,OBJ_TURBO_NA1,OBJ_TURBO_NA2

SKI_TAB		DC.L	OBJ_MOBILE_ANM0
		DC.L	OBJ_MOBILE_ANM1
		DC.L	OBJ_MOBILE_ANM2
		DC.L	OBJ_MOBILE_ANM3
		DC.L	OBJ_MOBILE_ANM4
		DC.L	OBJ_MOBILE_ANM5

		XYRATIO	$100,($100+CAR_ZF),$100,($100+CAR_ZF)
		OBJCNTR	120,78,82,49

OBJ_MOBILE	DC.L	OBJ_MOBILE_SET
		DC.L	OBJ_MOBILE_ANM0
		DC.L	OBJ_MOBILE_ANM0
		DC.L	0
		DC.L	OBJ_MOBILE_DAM

BAT_SMK_POS	XSIZE	36			
		XSIZE	-12			
		XSIZE	30			
		XSIZE	-18			
		XSIZE	24
		XSIZE	-24			
		XSIZE	18			
		XSIZE	-30			
		XSIZE	12			
		XSIZE	-36			
		
OBJ_MOB_XVIEW	DC.W	2*$20,$20
		DC.L	$3000,$7800,-1
OBJ_MOB_YVIEW	DC.W	5*$20
		DC.W	-1
OBJ_MOB_JUMP	DC.W	5*$20
		DC.W	-1

OBJ_MOBILE_ANM0	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCLM	124,73,0,0,80,60,BTSKI3
		OBJSCLM	122,75,0,0,80,55,BTSKI2
		OBJSCL	120,78,0,0,82,49,BTSKI1
		OBJSCL	122,75,0,0,80,55,BTSKI2
		OBJSCL	124,73,0,0,80,60,BTSKI3

OBJ_MOBILE_ANM1	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCLM	124,73,80,0,80,63,BTSKI3
		OBJSCLM	122,75,80,0,80,58,BTSKI2
		OBJSCL	120,78,96,0,82,52,BTSKI1
		OBJSCL	122,75,80,0,80,58,BTSKI2
		OBJSCL	124,73,80,0,80,63,BTSKI3

OBJ_MOBILE_ANM2	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCLM	124,73,0,64,80,66,BTSKI3
		OBJSCLM	122,75,0,64,80,61,BTSKI2
		OBJSCL	120,78,0,64,82,55,BTSKI1
		OBJSCL	122,75,0,64,80,61,BTSKI2
		OBJSCL	124,73,0,64,80,66,BTSKI3

OBJ_MOBILE_ANM3	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCLM	124,73,80,64,80,69,BTSKI3
		OBJSCLM	122,75,80,64,80,64,BTSKI2
		OBJSCL	120,78,96,64,82,58,BTSKI1
		OBJSCL	122,75,80,64,80,64,BTSKI2
		OBJSCL	124,73,80,64,80,69,BTSKI3

OBJ_MOBILE_ANM4	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCLM	124,73,0,144,80,72,BTSKI3
		OBJSCLM	122,75,0,128,80,66,BTSKI2
		OBJSCL	120,78,0,128,82,61,BTSKI1
		OBJSCL	122,75,0,128,80,66,BTSKI2
		OBJSCL	124,73,0,144,80,72,BTSKI3

OBJ_MOBILE_ANM5	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCLM	124,73,80,144,80,77,BTSKI3
		OBJSCLM	122,75,80,128,80,73,BTSKI2
		OBJSCL	120,78,96,128,82,67,BTSKI1
		OBJSCL	122,75,80,128,80,73,BTSKI2
		OBJSCL	124,73,80,144,80,77,BTSKI3

OBJ_MOBILE_SPN0
		OBJSCL	(121+39),(90+26-8-4),73,58,82,82,BTSKIS

OBJ_MOBILE_SPN1
		OBJSCL	(121+39),(90+26-8-4),185,58,82,82,BTSKIS

OBJ_MOBILE_SET	DC.W	ID_MAIN		;OBJ_ID
		DC.L	0		;OBJ_MOVE_CTRL
		DC.L	CAR_PRINT	;OBJ_PRN_CTRL
		DC.W	$50		;OBJ_ZSIZE
		XYSIZE	40,48
		DC.W	$5050		;OBJ_MASS
		DC.L	HROUTINE_CAR	;OBJ_HIT_GOTO
		DC.W	$100+CAR_ZF	;OBJ_ZPOS
		DC.L	$100*200	;OBJ_XPOS
		DC.L	$100*0		;OBJ_YPOS
		DC.W	$50		;OBJ_SPD_MAXA
		DC.W	$88		;OBJ_SPD_MAXB
		DC.W	50		;OBJ_SPD_INCFA
		DC.W	40		;OBJ_SPD_INCFB
		DC.W	80		;OBJ_SPD_DECF
		DC.W	8		;OBJ_SPD_BRAKE
		DC.W	$050A		;OBJ_TRACTION

; --------------------------------------------------------------------------

OBJ_MOBILE_DAM	DC.W	($FFFF/$240)/15,$FFFF/30

		DC.W	$FFFF
		DC.L	0,KILL_SET3

; --------------------------------------------------------------------------

		OBJCNTR	(120+2),(78+12),80,49

OBJ_FLAME_NA1	
		OBJSCLM	142,128,42,0,10,10,SKFLAM
		OBJSCLM	150,128,22,0,9,10,SKFLAM
		OBJSCL	157,128,0,0,10,11,SKFLAM
		OBJSCL	150,128,22,0,9,10,SKFLAM
		OBJSCL	142,128,42,0,10,10,SKFLAM
		
OBJ_FLAME_NA2
		OBJSCLM	142,128,53,0,10,10,SKFLAM
		OBJSCLM	150,128,32,0,9,10,SKFLAM
		OBJSCL	157,128,11,0,10,11,SKFLAM
		OBJSCL	150,128,32,0,9,10,SKFLAM
		OBJSCL	142,128,53,0,10,10,SKFLAM

OBJ_TURBO_NA0	
		OBJSCLM	141,128,63,12,11,12,SKFLAM
		OBJSCLM	150,128,33,12,9,12,SKFLAM
		OBJSCL	157,128,0,12,10,13,SKFLAM
		OBJSCL	150,128,33,12,9,12,SKFLAM
		OBJSCL	141,128,63,12,11,12,SKFLAM

OBJ_TURBO_NA1	
		OBJSCLM	141,128,75,12,11,11,SKFLAM
		OBJSCLM	150,128,43,12,9,12,SKFLAM
		OBJSCL	157,128,11,12,10,13,SKFLAM
		OBJSCL	150,128,43,12,9,12,SKFLAM
		OBJSCL	141,128,75,12,11,11,SKFLAM

OBJ_TURBO_NA2	
		OBJSCLM	141,128,87,12,11,12,SKFLAM
		OBJSCLM	150,128,53,12,9,12,SKFLAM
		OBJSCL	157,128,22,12,10,13,SKFLAM
		OBJSCL	150,128,53,12,9,12,SKFLAM
		OBJSCL	141,128,87,12,11,12,SKFLAM

; --------------------------------------------------------------------------

		XYRATIO	100,220,100,220
		OBJCNTR	12,7,14,12
OBJ_BTMISS
		DC.L	OBJ_BTMISS_SET
		DC.L	OBJ_BTMISS_ANM0


OBJ_BMISS_XVIEW	DC.W	22*$20,$20
		DC.L	$3000,$7800,-1
OBJ_BMISS_YVIEW	DC.W	9*$20
		DC.W	$16,-1

OBJ_BTMISS_ANM0	DC.L	OBJ_BMISS_XVIEW,OBJ_BMISS_YVIEW
		
		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,4,44,0,14,24,BTMISS
		OBJSCL	12,3,0,0,14,26,BTMISS
		OBJSCL	12,4,44,0,14,24,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS

		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,4,44,0,14,24,BTMISS
		OBJSCL	12,3,0,0,14,26,BTMISS
		OBJSCL	12,4,44,0,14,24,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS

		OBJSCLM	12,8,103,0,15,15,BTMISS
		OBJSCLM	12,8,103,0,15,15,BTMISS
		OBJSCLM	12,8,103,0,15,15,BTMISS
		OBJSCLM	12,7,59,0,13,16,BTMISS
		OBJSCL	12,7,15,0,14,17,BTMISS
		OBJSCL	12,7,59,0,13,16,BTMISS
		OBJSCL	12,8,103,0,15,15,BTMISS
		OBJSCL	12,8,103,0,15,15,BTMISS
		OBJSCL	12,8,103,0,15,15,BTMISS

		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,73,0,13,14,BTMISS
		OBJSCL	12,9,30,0,13,15,BTMISS
		OBJSCL	12,9,73,0,13,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS

		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,73,0,13,14,BTMISS
		OBJSCL	12,9,30,0,13,15,BTMISS
		OBJSCL	12,9,73,0,13,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS


OBJ_BTMISS_SET	DC.W	ID_MISS		;OBJ_ID
		DC.L	MISS_MOVE	;OBJ_MOVE_CTRL
		DC.L	MISS_PRINT	;OBJ_PRN_CTRL
		DC.W	$20		;OBJ_ZSIZE
		XYSIZE	14,12
		DC.W	0		;OBJ_MASS
		DC.L	O_T2M_HIT_EN	;OBJ_HIT_GOTO

VIS_SMOKE_MISS
		DC.W	$0000,$400,$400
		DC.W	-$1C,0,0
		DC.W	$0100,$0A00
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN1
		DC.L	VIS_SMOKE2_GR2A
		DC.W	0		;SFX

; --------------------------------------------------------------------------

VIS_WSPLASH
		DC.W	$0000,$0000,$0000
		DC.W	0,0,0
		DC.W	$0100,$0800
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0NC
		DC.L	VIS_WSPLASH_GRS
		DC.W	SFX_SPLASH_A	;SFX

VIS_WSPLASH_GRS

		XYRATIO	100,140,100,140
		OBJCNTR	106,62,76,25

		OBJSCL	106,62,0,0,76,51,WSPLS1

		XYRATIO	100,160,100,160
		OBJCNTR	106,62,76,25

		OBJSCL	106,62,0,0,76,51,WSPLS1
		OBJSCL	97,53,0,0,93,64,WSPLS2
		OBJSCL	83,46,0,0,117,76,WSPLS3
		OBJSCL	68,37,0,0,146,93,WSPLS4
		OBJSCL	62,33,0,0,158,103,WSPLS5
		OBJSCL	49,30,0,0,175,113,WSPLS6
		OBJSCL	49,30,0,0,175,113,WSPLS6

; --------------------------------------------------------------------------

VIS_WBLAST
		DC.W	$0000,$0000,$0000
		DC.W	0,0,0
		DC.W	$080,$0600
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0NC
		DC.L	VIS_WBLAST_GRS
		DC.W	0

VIS_WBLAST_GRS

		XYRATIO	100,60,100,100
		OBJCNTR	113,77,52,20

		OBJSCL	113,77,0,0,52,20,WBLAST
		OBJSCL	108,61,53,0,62,36,WBLAST
		OBJSCL	101,45,116,0,76,52,WBLAST
		OBJSCL	92,38,0,37,92,48,WBLAST
		OBJSCL	88,32,93,53,104,53,WBLAST
		OBJSCL	83,29,0,107,117,54,WBLAST

; --------------------------------------------------------------------------



