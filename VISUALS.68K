; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ VISUALS - SPARKS							   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

;*D7=Random number. (Range 0 to 65535)

RANDOM_NO	MOVE.L	A0,-(SP)
		LEA	SEEDS,A0
		ADDQ.W	#3,(A0)
		MOVE.W	(A0)+,D7
		SUB.W	#141,(A0)
		ADD.W	(A0)+,D7
		ROL.W	D7
		ROL.W	(A0)
		ADD.W	(A0),D7
		MOVE.W	D7,(A0)
		MOVE.L	(SP)+,A0
		TST.W	D7
		RTS

;D7=Max value.
;*D7=Random number. (Range 0 to n)

RANDOM_RANGE_S	MOVE.W	D7,-(SP)
		BSR	RANDOM_NO
		BPL.S	RRU_POS
		BSR	RANDOM_NO
		MULU	(SP)+,D7
		SWAP	D7
		AND.L	#$FFFF,D7
		NEG.L	D7
		RTS
		
RANDOM_RANGE_U	MOVE.W	D7,-(SP)
RRU_POS		BSR	RANDOM_NO
		MULU	(SP)+,D7
		SWAP	D7
		AND.L	#$FFFF,D7
		RTS

; --------------------------------------------------------------------------

VIS_SET_SYZX	MOVE.W	OBJ_SPD(A1),D3
VIS_SET_YZX	MOVE.L	OBJ_YPOS(A1),D2
VIS_SET_ZX	MOVE.W	OBJ_ZPOS(A1),D0
VIS_SET_X	MOVE.L	OBJ_XPOS(A1),D1
		BRA.S	VIS_SET

VIS_SET_XYZS	MOVE.L	OBJ_XPOS(A1),D1
VIS_SET_YZS	MOVE.L	OBJ_YPOS(A1),D2
VIS_SET_ZS	MOVE.W	OBJ_ZPOS(A1),D0
VIS_SET_S	MOVE.W	OBJ_SPD(A1),D3

VIS_SET		BSR	OBJECT_ALLOC
		BNE.S	.end
		MOVE.W	(A0)+,D7
		BSR	RANDOM_RANGE_S
		ADD.W	D7,D0
		MOVE.W	D0,OBJ_ZPOS(A1)
		MOVE.W	(A0)+,D7
		BSR	RANDOM_RANGE_S
		ADD.L	D7,D1
		MOVE.L	D1,OBJ_XPOS(A1)
		MOVE.W	(A0)+,D7
		BSR	RANDOM_RANGE_S
		ADD.L	D7,D2
		MOVE.L	D2,OBJ_YPOS(A1)
		ADD.W	(A0)+,D3
		MOVE.W	D3,OBJ_CNT3(A1)		;Set Zupdate. 
		MOVE.W	(A0)+,OBJ_CNT4(A1)	;Set Xupdate. 
		MOVE.W	(A0)+,OBJ_CNT5(A1)	;Set Yupdate. 
		MOVE.W	(A0)+,OBJ_CNT1(A1)	;Set Step. 
		MOVE.W	(A0)+,OBJ_CNT2(A1)	;Set Termination. 
		MOVE.L	(A0)+,OBJ_MOVE_CTRL(A1)
		MOVE.L	(A0)+,OBJ_PRN_CTRL(A1)
		MOVE.L	(A0)+,OBJ_ADDR0(A1)
		MOVE.W	(A0)+,D0
		BEQ.S	.end
		BSR	CSFX_ONESHOT_V
		MOVEQ	#0,D0
.end		RTS

VIS_MOVE0_RCHK	BSR	O_RANGE_CHK		;Range animation.
		BRA.L	VIS_DEALLOCATE
		BRA.L	VIS_DEALLOCATE
VIS_MOVE0	MOVE.W	OBJ_CNT0(A1),D0
		ADD.W	OBJ_CNT1(A1),D0
		CMP.W	OBJ_CNT2(A1),D0
		BPL.S	VIS_DEALLOCATE
VIS_MCONT	MOVE.W	D0,OBJ_CNT0(A1)
		MOVE.W	OBJ_CNT3(A1),D0
		ADD.W	D0,OBJ_ZPOS(A1)
		MOVE.W	OBJ_CNT4(A1),D0
		EXT.L	D0
		ADD.L	D0,OBJ_XPOS(A1)
		MOVE.W	OBJ_CNT5(A1),D0
		EXT.L	D0
		ADD.L	D0,OBJ_YPOS(A1)
		RTS

VIS_MOVE1	BSR	O_RANGE_CHK 		;Loop animation.
		BRA.L	VIS_DEALLOCATE
		BRA.L	VIS_DEALLOCATE
		MOVE.W	OBJ_CNT0(A1),D0
		ADD.W	OBJ_CNT1(A1),D0
		AND.W	OBJ_CNT2(A1),D0
		BRA.S	VIS_MCONT

VIS_DEALLOCATE	CLR.B	OBJ_FLAG0(A1)
		MOVE.L	OBJ_ADDR1(A1),A2
		BRA	O_DEALL_S

VIS_PRN1	ADD.W	ZMOVE,D0
		MOVE.W	D0,OBJ_ZPOS(A1)
VIS_PRN0	CMP.W	#$80,D0
		BPL.S	.norm
		MOVE.W	#$88,D0
.norm		BSR	O_PR_ALLOC_XIT
		BSR	VIS_PCALC
		MOVE.W	D0,D1
		ADD.W	OBJ_CNT7(A1),D1
		BRA	O_PR_SET0_RS

VIS_PRN0NC	CMP.W	#$80,D0
		BPL.S	.norm
		MOVE.W	#$88,D0
.norm		BSR	O_PR_ALLOC_XIT
		BSR	VIS_PCALC
		MOVE.W	D0,D1
		ADD.W	OBJ_CNT7(A1),D1
		BSR	O_PR_SET0_RS
		MOVE.L	#$01000000,(A6)
		MOVE.L	#$00C00000,20(A6)
		RTS

VIS_PRN0W	CMP.W	#$80,D0
		BPL.S	.norm
		MOVE.W	#$88,D0
.norm		BSR	O_PR_ALLOC_XIT
		BSR	VIS_PCALC
		BRA	O_PR_SET0_W

VIS_PRN1X	ADD.W	ZMOVE,D0
		MOVE.W	D0,OBJ_ZPOS(A1)
VIS_PRN0X	CMP.W	#$80,D0
		BPL.S	.norm
		MOVE.W	#$88,D0
.norm		BSR	O_PR_ALLOCX_XIT
		BSR	VIS_PCALC
		MOVE.W	D0,D1
		ADD.W	OBJ_CNT7(A1),D1
		BRA	O_PR_SET0_RS

VIS_PRN_ROTATEX	BSR	O_PR_ALLOCX_XIT
		BRA.S	VPR_CONT

VIS_PRN_ROTATE	BSR	O_PR_ALLOC_XIT
VPR_CONT	BSR	VIS_PCALC
		MOVE.W	OBJ_CNT7(A1),D2
		ADD.W	OBJ_CNT6(A1),D2
		MOVE.W	D2,OBJ_CNT7(A1)
		AND.W	#$3FE,D2
		MOVE.W	D2,OBJ_CNT7(A1)
		BRA	O_PR_ROTATE

VIS_PCALC	MOVE.L	OBJ_ADDR0(A1),A0
		MOVE.L	OBJ_ADDR1(A1),A2
		MOVE.W	OBJ_CNT0(A1),D7
		ASR.W	#3,D7
		AND.W	#$FFE0,D7
		LEA	(A0,D7.W),A0
		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		BRA	O_CALC_PATCHZ

; --------------------------------------------------------------------------

VIS_RTOBJ_MOVER	BSR	O_RANGE_CHK
		BRA.L	VIS_DEALLOCATE
		BRA.L	VIS_DEALLOCATE

		MOVE.W	OBJ_CNT7(A1),D5		;Current rotation.
		MOVE.W	OBJ_CNT6(A1),D4		;Current rotation dir.
		MOVE.W	OBJ_CNT4(A1),D3		;Current Xstep.
		MOVE.L	OBJ_ADDR7(A1),D2	
		MOVE.L	OBJ_YPOS(A1),D1
		BMI.S	.air_time
		MOVE.W	D4,D7
		ASR.W 	#3,D7
		SUB.W	D7,D4
		NEG.W	D4
		ASR.W	D3
		MOVE.W	#2,D7
		BSR	RANDOM_RANGE_U
		ADDQ.W	#1,D7
		MOVE.L	D2,D6
		ASR.L	D7,D6
		SUB.L	D6,D2
		CMP.L	OBJ_ADDR4(A1),D2
		BPL.S 	.air_rebound
		MOVEQ	#0,D3
		MOVEQ	#0,D4
		MOVE.W	D5,D7
		AND.W	#$3FE,D7
		MOVE.L	OBJ_ADDR2(A1),D5
		CMP.W	OBJ_ADDR3(A1),D7
		BPL	.set
		SWAP	D5
		CMP.W	OBJ_ADDR3+2(A1),D7
		BPL	.set
		SWAP	D5
		BRA	.set

.air_rebound	NEG.L	D2
.air_time	ADD.L	OBJ_ADDR4(A1),D2
		ADD.L	D2,D1
		BMI	.set
		NEG.W	OBJ_CNT6(A1)
		MOVEQ	#0,D1

		IF	LEVEL>3
		MOVE.L	A1,-(SP)

		CMP.W	#$E00,OBJ_ZPOS(A1)
		BPL	.wblast
		MOVE.W	#4,D7
		BSR	RANDOM_RANGE_U
		LSL.W	D7
		MOVE.W	.wblast_list(PC,D7.W),D0
		JSR	CSFX_ONESHOT_V
.wblast		LEA	VIS_WBLAST,A0
		CLR.L	OBJ_YPOS(A1)
		BSR	VIS_SET_XYZS
		MOVE.L	(SP)+,A1
		JMP	O_CLEAR_S
.wblast_list	DC.W	SFX_WATERPLOP_A		
		DC.W	SFX_WATERPLOP_B		
		DC.W	SFX_WATERPLOP_C		
		DC.W	SFX_WATERPLOP_D		
		ENDIF

		CMP.L	#$800,D2
		BMI.S	.set
		CMP.W	#$C00,OBJ_ZPOS(A1)
		BPL.S	.set
		CMP.W	#$80,OBJ_ZPOS(A1)
		BMI.S	.set
		MOVE.L	OBJECT_DATA+OBJ_XPOS,D0
		SUB.L	OBJ_XPOS(A1),D0
		BPL.S	.chk
		NEG.L	D0
.chk		CMP.L	#200*$100,D0
		BPL.S	.set
		MOVE.L	OBJ_DAMAGE_DATA(A1),D0
		BEQ.S	.set
		MOVE.L	D0,A6
		MOVE.W	(A6)+,D7
		BSR	RANDOM_RANGE_U
		LSL.W	D7
		MOVE.W	(A6,D7.W),D0
		BSR	CSFX_ONESHOT_V

.set		MOVE.L	D1,OBJ_YPOS(A1)		;Set YpoS.
		MOVE.L	D2,OBJ_ADDR7(A1)
		MOVE.W	D3,OBJ_CNT4(A1)
		EXT.L	D3
		ADD.L	D3,OBJ_XPOS(A1)		;Set Xpos.
		MOVE.W	D4,OBJ_CNT6(A1)
		MOVE.W	D5,OBJ_CNT7(A1)

		MOVE.W	OBJ_CNT3(A1),D1		;Calc Z decay.
		BPL.S	.pos
		ADD.W	OBJ_ADDR5+2(A1),D1
		BMI.S	.setz
		BRA.S	.zero
.pos		SUB.W	OBJ_ADDR5+2(A1),D1
		BPL.S	.setz
.zero		MOVEQ	#0,D1
.setz		MOVE.W	D1,OBJ_CNT3(A1)
		ADD.W	D1,OBJ_ZPOS(A1)		;Set Zpos.
		
		MOVE.W	OBJ_CNT0(A1),D0		;Animation.
		ADD.W	OBJ_CNT1(A1),D0
		AND.W	OBJ_CNT2(A1),D0
		MOVE.W	D0,OBJ_CNT0(A1)

		CMP.W	#1,BG_TYPE		;Check if inside tunnel.
		BNE	.end

; --- Tunnel ---

		MOVE.L	OBJ_XPOS(A1),D0
		BPL.S	.poschk
		MOVEQ	#0,D0
		BRA.S	.wallhit
.poschk		CMP.L	#400*$100,D0
		BMI.S	.xset
		MOVE.L	#400*$100,D0
.wallhit	ASR.W	OBJ_CNT4(A1)
		NEG.W	OBJ_CNT4(A1)
		NEG.W	OBJ_CNT6(A1)
.xset		MOVE.L	D0,OBJ_XPOS(A1)

		MOVE.W	OBJ_ZPOS(A1),D7
		ADD.W	ZVIEW,D7
		LSR.W	#7,D7
		MULU	#RCSTEP,D7
		LEA	ROAD_CORD_DATA-RCSTEP,A5
		MOVE.L	OBJ_YSIZE(A1),D6
		ASR.L	#1,D6
		ADD.L	OBJ_YSIZE(A1),D6
		NEG.L	D6
		ADD.L	OBJ_YPOS(A1),D6
		CMP.L	RCROOF(A5,D7.W),D6
		BPL.S	.end
		MOVE.L	OBJ_ADDR7(A1),D0
		ASR.L	D0
		MOVE.L	D0,OBJ_ADDR7(A1)
		MOVE.L	RCROOF(A5,D7.W),OBJ_YPOS(A1)
.end		RTS


VIS_RTOBJ_SET	BSR	VIS_SET
		BNE	.end
		MOVE.L	(A0)+,OBJ_ADDR2(A1)	;Set lie angles.
		MOVE.L	(A0)+,OBJ_ADDR3(A1)	;Set lie range.
		MOVE.W	(A0)+,OBJ_ADDR4+2(A1)	;Set Y-decay.
		MOVE.W	(A0)+,OBJ_ADDR5+2(A1)	;Set Z-decay.
		BSR	RANDOM_NO
		MOVE.W	D7,OBJ_CNT7(A1)		;Set first rotation.
		MOVE.W	#$20,D7
		BSR	RANDOM_RANGE_S
		MOVE.W	(A0)+,D1
		LSL.W	D1
		ASR.W	D1
		BPL.S	.svary
		ADD.W	D7,D1
		BRA.S	.sset
.svary		ADD.W	D7,D1
		BSR	RANDOM_NO
		BPL.S	.sset
		NEG.W	D1
.sset		MOVE.W	D1,OBJ_CNT6(A1)		;Set rotation dir.
		MOVE.W	(A0)+,D7
		BPL.S	.dvary
		LSL.W	D7
		ASR.W	D7
		BPL.S	.dpos
.dneg		NEG.W	D7
		BSR	RANDOM_RANGE_U
		NEG.W	D7
		BRA.S	.dset
.dpos		BSR	RANDOM_RANGE_U
		BRA.S	.dset
.dvary		BSR	RANDOM_RANGE_S
.dset		MOVE.W	D7,OBJ_CNT4(A1)		;Set random xstep.
		EXT.L	D3
		BMI.S	.skp1
		NEG.L	D3
.skp1		ASL.L	#4,D3
		SUB.L	#$600,D3
		MOVE.L	D3,OBJ_ADDR7(A1)
		ADD.L	D3,OBJ_YPOS(A1)
		
		MOVE.W	(A0)+,D7
		BSR	RANDOM_RANGE_U
		EXT.L	D7
		SUB.L	D7,OBJ_ADDR7(A1)

		MOVE.L	(A0)+,OBJ_DAMAGE_DATA(A1)

		MOVEQ	#0,D0
.end		RTS

; --------------------------------------------------------------------------

VIS_SPARK_0
		DC.W	0,$200,$200
		DC.W	-$08,0,-$800
		DC.W	$0080,$0500
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_SPARK0_GRS
		DC.W	0			;SFX

VIS_SPARK_1
		DC.W	0,0,0
		DC.W	-$04,0,-$200
		DC.W	$0080,$0500
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_SPARK0_GRS
		DC.W	0			;SFX

VIS_SMOKE_0
		DC.W	$0000,$0500,$0500
		DC.W	-$18,0,-$800
		DC.W	$0100,$0B00
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN1
		DC.L	VIS_SMOKE_GRS
		DC.W	0			;SFX

VIS_SMOKE_1
		DC.W	$0040,$2000,$0000
		DC.W	-$08,0,-$800
		DC.W	$0100,$0800
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN1
		DC.L	VIS_SMOKE_GRS+$20*3
		DC.W	0			;SFX

VIS_SMOKE_2
		DC.W	$0000,$800,$0000
		DC.W	-$30,0,-$400
		DC.W	$0100,$0B00
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN1
		DC.L	VIS_SMOKE_GRS2
		DC.W	0			;SFX

VIS_ESKIDSMOKE	DC.W	$0000,$0400,$0000
		DC.W	0,0,0
		DC.W	$0100,$0B00
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_SMOKE_GRS1
		DC.W	0			;SFX

VIS_BSKIDSMOKE	DC.W	$0000,$0500,$0000
		DC.W	-$C,0,-$380
		DC.W	$0100,$0B00
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN1
		DC.L	VIS_SMOKE_GRS
		DC.W	0			;SFX

VIS_WHACKS_0	DC.W	0,0,0
		DC.W	0,0,0
		DC.W	$0100,$0300
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_WHACKS_GRS
		DC.W	0			;SFX

VIS_WHACKS_1	DC.W	0,0,0
		DC.W	0,0,0
		DC.W	$0100,$0300
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN1
		DC.L	VIS_WHACKS_GRS
		DC.W	0			;SFX

VIS_CAR_BASHF	DC.W	0,0,0
		DC.W	0,0,0
		DC.W	$0100,$0200
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_BASHF_GRS
		DC.W	0			;SFX

VIS_CAR_BASHR	DC.W	0,0,0
		DC.W	0,0,0
		DC.W	$0100,$0200
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_BASHR_GRS
		DC.W	0			;SFX

VIS_CAR_BASHL	DC.W	0,0,0
		DC.W	0,0,0
		DC.W	$0100,$0200
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_BASHL_GRS
		DC.W	0			;SFX

VIS_CAR_SPARK0	DC.W	0,$200,$200
		DC.W	-$0C,0,-$800
		DC.W	$0080,$0500
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_SPARK0_GRS
		DC.W	0			;SFX

VIS_TUNL_SPARK0	DC.W	$10,$200,$200
		DC.W	-$08,$400,-$400
		DC.W	$0080,$0500
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_SPARK0_GRS
		DC.W	0			;SFX

VIS_TUNR_SPARK0	DC.W	$10,$200,$200
		DC.W	-$08,-$400,-$400
		DC.W	$0080,$0500
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_SPARK0_GRS
		DC.W	0			;SFX

VIS_ROOF_SPARK0	DC.W	$10,$2000,0
		DC.W	-$08,0,$400
		DC.W	$0080,$0500
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_SPARK0_GRS
		DC.W	0			;SFX

VIS_LAND_SPARKL	DC.W	8,$800,$200
		DC.W	-16,-$480,-$200
		DC.W	$0080,$0500
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_SPARK0_GRS
		DC.W	0			;SFX

VIS_LAND_SPARKR	DC.W	8,$800,$200
		DC.W	-12,$480,-$200
		DC.W	$0080,$0500
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_SPARK0_GRS
		DC.W	0			;SFX

; --------------------------------------------------------------------------

		XYRATIO	100,130,100,130
		OBJCNTR	4,8,10,11

VIS_SPARK0_GRS	OBJSCL	4,8,0,0,10,17,SPARKY
		OBJSCL	1,1,11,0,17,31,SPARKY
		OBJSCL	1,0,29,0,17,34,SPARKY
		OBJSCL	1,5,47,0,17,34,SPARKY
		OBJSCL	0,16,65,0,18,28,SPARKY


		XYRATIO	100,250,100,250
		OBJCNTR	4,8,10,11

VIS_SPARK1_GRS	OBJSCL	4,8,0,0,10,17,SPARKY
		OBJSCL	1,1,11,0,17,31,SPARKY
		OBJSCL	1,0,29,0,17,34,SPARKY
		OBJSCL	1,5,47,0,17,34,SPARKY
		OBJSCL	0,16,65,0,18,28,SPARKY

; --------------------------------------------------------------------------

		XYRATIO	100,280,100,300
		OBJCNTR	14,142,21,10
VIS_BASHF_GRS
		OBJSCL	14,142,0,0,21,21,BASHSP
		OBJSCL	9,137,21,0,31,31,BASHSP

VIS_BASHR_GRS
		OBJSCL	18,147,52,0,14,23,BASHSP
		OBJSCL	16,137,66,0,18,31,BASHSP

VIS_BASHL_GRS
		OBJSCLM	18,147,52,0,14,23,BASHSP
		OBJSCLM	16,137,66,0,18,31,BASHSP

		XYRATIO	100,480,100,500
		OBJCNTR	14,142,21,10
VIS_BASHFX_GRS
		OBJSCL	14,142,0,0,21,21,BASHSP
		OBJSCL	9,137,21,0,31,31,BASHSP

; --------------------------------------------------------------------------

VIS_WHACKS_GRS
		XYRATIO	100,90,100,90
		OBJCNTR	0,0,31,15
		OBJSCL	0,0,0,0,31,31,WHACKS
		OBJSCL	0,0,31,0,1,1,WHACKS
		OBJSCL	0,0,0,0,31,31,WHACKS

; --------------------------------------------------------------------------

VIS_SMOKE_GRS
		XYRATIO	100,125,100,125
		OBJCNTR	6,6,5,3

		OBJSCL	6,6,0,0,5,6,SSMOKE
		OBJSCL	4,6,6,0,8,7,SSMOKE
		OBJSCL	2,4,0,8,12,11,SSMOKE
		OBJSCL	0,2,15,0,17,14,SSMOKE
		OBJSCL	1,3,33,0,16,13,SSMOKE
		OBJSCL	2,2,50,0,15,15,SSMOKE
		OBJSCL	1,1,16,15,16,16,SSMOKE
		OBJSCL	1,2,33,14,15,16,SSMOKE
		OBJSCL	1,2,49,16,15,16,SSMOKE
		OBJSCL	1,3,66,0,15,15,SSMOKE
		OBJSCL	2,3,67,18,13,13,SSMOKE

VIS_SMOKE_GRS1
		XYRATIO	100,140,100,140
		OBJCNTR	6,6,5,3

		OBJSCL	6,6,0,0,5,6,SSMOKE
VIS_SMOKE_GRS1A	OBJSCL	4,6,6,0,8,7,SSMOKE
		OBJSCL	2,4,0,8,12,11,SSMOKE
		OBJSCL	0,2,15,0,17,14,SSMOKE
		OBJSCL	1,3,33,0,16,13,SSMOKE
		OBJSCL	2,2,50,0,15,15,SSMOKE
		OBJSCL	1,1,16,15,16,16,SSMOKE
		OBJSCL	1,2,33,14,15,16,SSMOKE
		OBJSCL	1,2,49,16,15,16,SSMOKE
		OBJSCL	1,3,66,0,15,15,SSMOKE
		OBJSCL	2,3,67,18,13,13,SSMOKE

VIS_SMOKE_GRS2
		XYRATIO	100,180,100,180
		OBJCNTR	6,6,5,3

		OBJSCL	6,6,0,0,5,6,SSMOKE
VIS_SMOKE_GRS2A	OBJSCL	4,6,6,0,8,7,SSMOKE
		OBJSCL	2,4,0,8,12,11,SSMOKE
		OBJSCL	0,2,15,0,17,14,SSMOKE
		OBJSCL	1,3,33,0,16,13,SSMOKE
		OBJSCL	2,2,50,0,15,15,SSMOKE
		OBJSCL	1,1,16,15,16,16,SSMOKE
		OBJSCL	1,2,33,14,15,16,SSMOKE
		OBJSCL	1,2,49,16,15,16,SSMOKE
		OBJSCL	1,3,66,0,15,15,SSMOKE
		OBJSCL	2,3,67,18,13,13,SSMOKE

VIS_SMOKE_GRS3
		XYRATIO	100,220,100,220
		OBJCNTR	6,6,5,3

		OBJSCL	6,6,0,0,5,6,SSMOKE
VIS_SMOKE_GRS3A	OBJSCL	4,6,6,0,8,7,SSMOKE
		OBJSCL	2,4,0,8,12,11,SSMOKE
		OBJSCL	0,2,15,0,17,14,SSMOKE
		OBJSCL	1,3,33,0,16,13,SSMOKE
		OBJSCL	2,2,50,0,15,15,SSMOKE
		OBJSCL	1,1,16,15,16,16,SSMOKE
		OBJSCL	1,2,33,14,15,16,SSMOKE
		OBJSCL	1,2,49,16,15,16,SSMOKE
		OBJSCL	1,3,66,0,15,15,SSMOKE
		OBJSCL	2,3,67,18,13,13,SSMOKE

; --------------------------------------------------------------------------

VIS_SMOKE2_GRS
		XYRATIO	100,125,100,125
		OBJCNTR	6,6,5,3

		OBJSCL	6,6,0,0,5,6,SMOKE2
		OBJSCL	4,6,6,0,8,7,SMOKE2
		OBJSCL	2,4,0,8,12,11,SMOKE2
		OBJSCL	0,2,15,0,17,14,SMOKE2
		OBJSCL	1,3,33,0,16,13,SMOKE2
		OBJSCL	2,2,50,0,15,15,SMOKE2
		OBJSCL	1,1,16,15,16,16,SMOKE2
		OBJSCL	1,2,33,14,15,16,SMOKE2
		OBJSCL	1,2,49,16,15,16,SMOKE2
		OBJSCL	1,3,66,0,15,15,SMOKE2
		OBJSCL	2,3,67,18,13,13,SMOKE2

VIS_SMOKE2_GR2
		XYRATIO	100,180,100,180
		OBJCNTR	6,6,5,3

		OBJSCL	6,6,0,0,5,6,SMOKE2
VIS_SMOKE2_GR2A	OBJSCL	4,6,6,0,8,7,SMOKE2
		OBJSCL	2,4,0,8,12,11,SMOKE2
		OBJSCL	0,2,15,0,17,14,SMOKE2
		OBJSCL	1,3,33,0,16,13,SMOKE2
		OBJSCL	2,2,50,0,15,15,SMOKE2
		OBJSCL	1,1,16,15,16,16,SMOKE2
		OBJSCL	1,2,33,14,15,16,SMOKE2
		OBJSCL	1,2,49,16,15,16,SMOKE2
		OBJSCL	1,3,66,0,15,15,SMOKE2
		OBJSCL	2,3,67,18,13,13,SMOKE2

VIS_SMOKE2_GR3
		XYRATIO	100,220,100,220
		OBJCNTR	6,6,5,3

		OBJSCL	6,6,0,0,5,6,SMOKE2
VIS_SMOKE2_GR3A	OBJSCL	4,6,6,0,8,7,SMOKE2
		OBJSCL	2,4,0,8,12,11,SMOKE2
		OBJSCL	0,2,15,0,17,14,SMOKE2
		OBJSCL	1,3,33,0,16,13,SMOKE2
		OBJSCL	2,2,50,0,15,15,SMOKE2
		OBJSCL	1,1,16,15,16,16,SMOKE2
		OBJSCL	1,2,33,14,15,16,SMOKE2
		OBJSCL	1,2,49,16,15,16,SMOKE2
		OBJSCL	1,3,66,0,15,15,SMOKE2
		OBJSCL	2,3,67,18,13,13,SMOKE2

; --------------------------------------------------------------------------
