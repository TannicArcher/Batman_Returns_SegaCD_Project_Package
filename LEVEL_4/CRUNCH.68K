; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ BATMAN RETURNS CD-ROM						   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

; ROAD OBJECT - LEVEL 4 - "Cruncher"

; --------------------------------------------------------------------------

CRUNCH_SPD	=	$340
CRUNCH_SPDT	=	$1000
CRUNCH_SPDH	=	$200
CRUNCH_SPDO	=	$300
CRUNCH_SPDC	=	$180
CRUNCH_RMIN	=	$600
CRUNCH_SPARK	=	$800
CRUNCH_DELAY	=	20

; --------------------------------------------------------------------------

CRSETUP_UDLR	BSR	CRSETUP_UD
		BNE	.end

		MOVE.L	A1,A3
		LEA	OBJ_CRUNCH_L,A6
		BSR	O_OBJECT_SETUP
		BNE	.end
		MOVE.L	A1,OBJ_ADDR5(A3)
		MOVE.W	D2,OBJ_ZPOS(A1)
		MOVE.W	#CRUNCH_SPD,OBJ_CNT1(A1)
		MOVE.W	#-CRUNCH_SPDH,OBJ_CNT5(A1)
		MOVE.L	#-$3000,OBJ_ADDR2(A1)
		MOVE.L	#$7000,OBJ_ADDR3(A1)
		MOVE.W	#6,OBJ_CNT3(A1)
		MOVE.W	OBJ_CNT6(A3),OBJ_CNT6(A1)
		MOVE.W	OBJ_FLAG2(A3),OBJ_FLAG2(A1)
		MOVE.W	RDDATA0(A2),D0
		BNE.S	.set
		MOVE.W	#-$40,D0
.set		MOVE.W	D0,OBJ_FLAG2(A1)

		MOVE.L	A1,A3
		LEA	OBJ_CRUNCH_R,A6
		BSR	O_OBJECT_SETUP
		BNE.S	.end
		MOVE.L	A3,OBJ_ADDR7(A1)
		MOVE.L	A1,OBJ_ADDR7(A3)
		MOVE.W	OBJ_ZPOS(A3),OBJ_ZPOS(A1)
		MOVE.W	#-CRUNCH_SPD,OBJ_CNT1(A1)
		MOVE.W	#CRUNCH_SPDH,OBJ_CNT5(A1)
		MOVE.L	#$12000,OBJ_ADDR2(A1)
		MOVE.L	#$1C000,OBJ_ADDR3(A1)
		MOVE.L	#$1C000,OBJ_XPOS(A1)
		MOVE.W	OBJ_CNT6(A3),OBJ_CNT6(A1)
		MOVE.W	OBJ_FLAG2(A3),OBJ_FLAG2(A1)

.end		RTS

; --------------------------------------------------------------------------

CRSETUP_UD	LEA	OBJ_CRUNCH_U,A6
		BSR	O_MTR_SETUP

		MOVE.W	D2,OBJ_ZPOS(A1)
		MOVE.W	#CRUNCH_SPD,OBJ_CNT1(A1)
		MOVE.W	#-CRUNCH_SPDH,OBJ_CNT5(A1)
		MOVE.L	#-$A000,OBJ_ADDR2(A1)
		MOVE.W	#7,OBJ_CNT3(A1)
		MOVE.W	RDDATA0(A2),D0
		BNE.S	.set
		MOVE.W	#-$40,D0
.set		MOVE.W	D0,OBJ_FLAG2(A1)

		MOVE.W	#CRUNCH_DELAY,D7
		BSR	RANDOM_RANGE_U
		MOVE.W	D7,OBJ_CNT6(A1)
		MOVE.W	#8,OBJ_CNT2(A1)

		MOVE.L	A1,A3
		LEA	OBJ_CRUNCH_D,A6
		BSR	O_OBJECT_SETUP
		BNE.S	.end
		MOVE.L	A3,OBJ_ADDR7(A1)
		MOVE.L	A1,OBJ_ADDR7(A3)
		MOVE.W	OBJ_ZPOS(A3),OBJ_ZPOS(A1)

		MOVE.W	#-CRUNCH_SPD,OBJ_CNT1(A1)
		MOVE.W	#CRUNCH_SPDH,OBJ_CNT5(A1)
		MOVE.L	#$B000,OBJ_ADDR2(A1)
		MOVE.L	#$15000,OBJ_ADDR3(A1)
		MOVE.L	#$15000,OBJ_YPOS(A1)
		
		MOVE.W	OBJ_CNT6(A3),OBJ_CNT6(A1)
		MOVE.W	#8,OBJ_CNT2(A1)
		MOVE.W	OBJ_FLAG2(A3),OBJ_FLAG2(A1)

		MOVEQ	#0,D0
.end		RTS

; --------------------------------------------------------------------------

CRSETUP_LR	LEA	OBJ_CRUNCH_L,A6
		BSR	O_MTR_SETUP

		MOVE.W	D2,OBJ_ZPOS(A1)
		MOVE.W	#CRUNCH_SPD,OBJ_CNT1(A1)
		MOVE.W	#-CRUNCH_SPDH,OBJ_CNT5(A1)
		MOVE.L	#-$3000,OBJ_ADDR2(A1)
		MOVE.L	#$7000,OBJ_ADDR3(A1)
		MOVE.W	#6,OBJ_CNT3(A1)
		MOVE.W	RDDATA0(A2),D0
		BNE.S	.set
		MOVE.W	#-$40,D0
.set		MOVE.W	D0,OBJ_FLAG2(A1)

		MOVE.W	#CRUNCH_DELAY,D7
		BSR	RANDOM_RANGE_U
		MOVE.W	D7,OBJ_CNT6(A1)

		MOVE.L	A1,A3
		LEA	OBJ_CRUNCH_R,A6
		BSR	O_OBJECT_SETUP
		BNE.S	.end
		MOVE.L	A3,OBJ_ADDR7(A1)
		MOVE.L	A1,OBJ_ADDR7(A3)
		MOVE.W	OBJ_ZPOS(A3),OBJ_ZPOS(A1)

		MOVE.W	#-CRUNCH_SPD,OBJ_CNT1(A1)
		MOVE.W	#CRUNCH_SPDH,OBJ_CNT5(A1)
		MOVE.L	#$12000,OBJ_ADDR2(A1)
		MOVE.L	#$1C000,OBJ_ADDR3(A1)
		MOVE.L	#$1C000,OBJ_XPOS(A1)

		MOVE.W	OBJ_CNT6(A3),OBJ_CNT6(A1)
		MOVE.W	OBJ_FLAG2(A3),OBJ_FLAG2(A1)

.end		RTS

; --------------------------------------------------------------------------

CRSETUP_L	LEA	OBJ_CRUNCH_L,A6
		BSR	O_MTR_SETUP
		MOVE.W	D2,OBJ_ZPOS(A1)
		MOVE.W	#CRUNCH_SPD,OBJ_CNT1(A1)
		MOVE.W	#-CRUNCH_SPDH,OBJ_CNT5(A1)
		MOVE.L	#-$3000,OBJ_ADDR2(A1)
		MOVE.L	#$7000,OBJ_ADDR3(A1)
		
		MOVE.W	#CRUNCH_DELAY,D7
		BSR	RANDOM_RANGE_U
		MOVE.W	D7,OBJ_CNT6(A1)

		MOVE.W	RDDATA0(A2),D0
		BNE.S	.set
		MOVE.W	#-$40,D0
.set		MOVE.W	D0,OBJ_FLAG2(A1)
		RTS

CRSETUP_R	LEA	OBJ_CRUNCH_R,A6
		BSR	O_MTR_SETUP
		MOVE.W	D2,OBJ_ZPOS(A1)
		MOVE.W	#-CRUNCH_SPD,OBJ_CNT1(A1)
		MOVE.W	#CRUNCH_SPDH,OBJ_CNT5(A1)
		MOVE.L	#$12000,OBJ_ADDR2(A1)
		MOVE.L	#$1C000,OBJ_ADDR3(A1)
		MOVE.L	#$1C000,OBJ_XPOS(A1)
		
		MOVE.W	#CRUNCH_DELAY,D7
		BSR	RANDOM_RANGE_U
		MOVE.W	D7,OBJ_CNT6(A1)

		MOVE.W	RDDATA0(A2),D0
		BNE.S	.set
		MOVE.W	#-$40,D0
.set		MOVE.W	D0,OBJ_FLAG2(A1)
.end		RTS

; --------------------------------------------------------------------------

CRSETUP_TL	LEA	OBJ_CRUNCH_L,A6
		BSR	O_MTR_SETUP
		MOVE.W	D2,OBJ_ZPOS(A1)
		MOVE.W	#CRUNCH_SPDT,OBJ_CNT1(A1)
		MOVE.W	#-CRUNCH_SPDH,OBJ_CNT5(A1)
		MOVE.L	#-$3000,OBJ_ADDR2(A1)
		MOVE.L	#$7000,OBJ_ADDR3(A1)
		MOVE.L	#CRUNCH_MOVE_LRT,OBJ_MOVE_CTRL(A1)
		
		MOVE.W	RDDATA0(A2),D0
		BNE.S	.set
		MOVE.W	#-$40,D0
.set		MOVE.W	D0,OBJ_FLAG2(A1)
		RTS

CRSETUP_TR	LEA	OBJ_CRUNCH_R,A6
		BSR	O_MTR_SETUP
		MOVE.W	D2,OBJ_ZPOS(A1)
		MOVE.W	#-CRUNCH_SPDT,OBJ_CNT1(A1)
		MOVE.W	#CRUNCH_SPDH,OBJ_CNT5(A1)
		MOVE.L	#$12000,OBJ_ADDR2(A1)
		MOVE.L	#$1C000,OBJ_ADDR3(A1)
		MOVE.L	#$1C000,OBJ_XPOS(A1)
		MOVE.L	#CRUNCH_MOVE_LRT,OBJ_MOVE_CTRL(A1)
		
		MOVE.W	RDDATA0(A2),D0
		BNE.S	.set
		MOVE.W	#-$40,D0
.set		MOVE.W	D0,OBJ_FLAG2(A1)
.end		RTS

; --------------------------------------------------------------------------

CRDOOR1_OPX	LEA	OBJ_CRUNCH_DL1,A6
		LEA	OBJ_CRUNCH_DR1,A4
		BRA	CRSETUP_OPX
CRDOOR1_OP	LEA	OBJ_CRUNCH_DL1,A6
		LEA	OBJ_CRUNCH_DR1,A4
		BRA	CRSETUP_OP

CRDOOR2_OPX	LEA	OBJ_CRUNCH_DL2,A6
		LEA	OBJ_CRUNCH_DR2,A4
		BRA	CRSETUP_OPX
CRDOOR2_OP	LEA	OBJ_CRUNCH_DL2,A6
		LEA	OBJ_CRUNCH_DR2,A4
		BRA	CRSETUP_OP

CRDOOR3_OPX	LEA	OBJ_CRUNCH_DL3,A6
		LEA	OBJ_CRUNCH_DR3,A4
		BRA.S	CRSETUP_OPX
CRDOOR3_OP	LEA	OBJ_CRUNCH_DL3,A6
		LEA	OBJ_CRUNCH_DR3,A4
		BRA.S	CRSETUP_OP

CRDOOR4_OPX	LEA	OBJ_CRUNCH_DL4,A6
		LEA	OBJ_CRUNCH_DR4,A4
		BRA.S	CRSETUP_OPX
CRDOOR4_OP	LEA	OBJ_CRUNCH_DL4,A6
		LEA	OBJ_CRUNCH_DR4,A4
		BRA.S	CRSETUP_OP

CRDOOR5_OPX	LEA	OBJ_CRUNCH_DL5,A6
		LEA	OBJ_CRUNCH_DR5,A4
		BRA.S	CRSETUP_OPX
CRDOOR5_OP	LEA	OBJ_CRUNCH_DL5,A6
		LEA	OBJ_CRUNCH_DR5,A4
		BRA.S	CRSETUP_OP

CRDOOR6_OPX	LEA	OBJ_CRUNCH_DL6,A6
		LEA	OBJ_CRUNCH_DR6,A4
		BRA.S	CRSETUP_OPX
CRDOOR6_OP	LEA	OBJ_CRUNCH_DL6,A6
		LEA	OBJ_CRUNCH_DR6,A4
		BRA.S	CRSETUP_OP

CRSETUP_OPX	BSR	CRSETUP_OP
		BNE.S	.end
		MOVE.L	#CRUNCH_MOVE_LRX,OBJ_MOVE_CTRL(A3)
.end		RTS

CRSETUP_OP	BSR	O_MTR_SETUP

		MOVE.W	D2,OBJ_ZPOS(A1)
		MOVE.W	#CRUNCH_SPDC,OBJ_CNT5(A1)
		MOVE.L	#-$3000,OBJ_ADDR2(A1)
		MOVE.L	#$7400,OBJ_ADDR3(A1)
		MOVE.L	#$7400,OBJ_XPOS(A1)
		MOVE.W	#6,OBJ_CNT3(A1)
		MOVE.L	#CRUNCH_MOVE_LRO,OBJ_MOVE_CTRL(A1)
		MOVE.L	#CRUNCH_PRINT_OP,OBJ_PRN_CTRL(A1)

		MOVE.W	RDDATA0(A2),D0
		BNE.S	.set
		MOVE.W	#-$50,D0
.set		MOVE.W	D0,OBJ_FLAG2(A1)

		MOVE.L	A1,A3
		MOVE.L	A4,A6
		BSR	O_OBJECT_SETUP
		BNE.S	.end
		MOVE.L	A3,OBJ_ADDR7(A1)
		MOVE.L	A1,OBJ_ADDR7(A3)
		MOVE.W	OBJ_ZPOS(A3),OBJ_ZPOS(A1)

		MOVE.W	#-CRUNCH_SPDC,OBJ_CNT5(A1)
		MOVE.L	#$11C00,OBJ_ADDR2(A1)
		MOVE.L	#$11C00,OBJ_XPOS(A1)
		MOVE.L	#$1C000,OBJ_ADDR3(A1)
		MOVE.L	#CRUNCH_MOVE_LRO,OBJ_MOVE_CTRL(A1)
		MOVE.L	#CRUNCH_PRINT_OP,OBJ_PRN_CTRL(A1)

		MOVE.W	OBJ_FLAG2(A3),OBJ_FLAG2(A1)
.end		MOVEQ	#0,D0
		RTS

; --------------------------------------------------------------------------

CRUNCH_DEALL	BSR	O_MTR_DEALL
		BEQ.S	.end
		MOVE.L	OBJ_ADDR7(A1),D0
		BEQ.S	.end
		MOVE.L	D0,A1
		BSR	O_CLEAR_S
		MOVE.L	OBJ_ADDR5(A1),D0
		BEQ.S	.end
		MOVE.L	D0,A1
		BSR	O_CLEAR_S
		MOVE.L	OBJ_ADDR7(A1),D0
		BEQ.S	.end
		MOVE.L	D0,A1
		BRA	O_CLEAR_S
.end		RTS

; --------------------------------------------------------------------------

CRUNCH_MOVE_LRX	CMP.W	#$100,OBJ_ZPOS(A1)
		BPL.S	CRUNCH_MOVE_LRO
		TAS.B	OBJ_FLAG1(A1)
		BNE.S	CRUNCH_MOVE_LRO
		ADDQ.W	#1,ECL_PAUSE

CRUNCH_MOVE_LRO
		MOVE.W	#-1,OBJ_CNT4(A1)
		MOVE.W	OBJ_CNT5(A1),OBJ_CNT1(A1)
		TST.W	OBJ_CNT7(A1)
		BEQ.S	CRUNCH_MOVE_LR
		SUBQ.W	#2,OBJ_CNT7(A1)
		BPL.S	.skp
		CLR.W	OBJ_CNT7(A1)
.skp		CLR.W	OBJ_CNT4(A1)
		NEG.W	OBJ_CNT1(A1)
		BPL.S	.pchk
.nchk		MOVE.W	#-CRUNCH_SPDO,OBJ_CNT1(A1)
		TST.W	OBJ_CNT0(A1)	
		BMI.S	CRUNCH_MOVE_LR
		CLR.W	OBJ_CNT0(A1)	
		BRA.S	CRUNCH_MOVE_LR
.pchk		MOVE.W	#CRUNCH_SPDO,OBJ_CNT1(A1)
		TST.W	OBJ_CNT0(A1)	
		BPL.S	CRUNCH_MOVE_LR
		CLR.W	OBJ_CNT0(A1)	
		BRA.S	CRUNCH_MOVE_LR

CRUNCH_MOVE_LRT	CMP.W	#$500,OBJ_ZPOS(A1)
		BMI.S	.move
		RTS
.move		MOVE.L	#CRUNCH_MOVE_LR,OBJ_MOVE_CTRL(A1)
		NOT.W	OBJ_CNT4(A1)

CRUNCH_MOVE_LR	LEA	VIS_CRSPARK_LR,A0
		MOVE.L	#-$6000,D6
		MOVE.L	OBJ_XPOS(A1),D1
		BSR	CRUNCH_MOVER
		MOVE.L	D1,OBJ_XPOS(A1)
		RTS

CRUNCH_MOVE_UD	LEA	VIS_CRSPARK_UD,A0
		MOVE.L	#-$2000,D6
		MOVE.L	OBJ_YPOS(A1),D1
		BSR	CRUNCH_MOVER
		MOVE.L	D1,OBJ_YPOS(A1)
		RTS

CRUNCH_MOVER	TST.W	OBJ_CNT6(A1)		;Control delay.
		BEQ.S	.cont
		SUBQ.W	#1,OBJ_CNT6(A1)
		RTS

.cont		MOVE.L	OBJ_ADDR2(A1),D3	;Xfmax.
		MOVE.L	OBJ_ADDR3(A1),D4	;Xbmax.

		MOVE.W	OBJ_CNT0(A1),D0		;Xmove.
		MOVE.W	OBJ_CNT1(A1),D2		;Xstep.
		BPL.S	.forward

.backward	ADD.W	D2,D0
		EXT.L	D0
		BMI.S	.bchk
		ADD.L	D0,D1
		CMP.L	D4,D1
		BLE	.set
		MOVE.L	D4,D1
		ASR.W	D0
		NEG.W	D0
		BRA	.set
.bchk		ADD.L	D0,D1
		CMP.L	D3,D1
		BGT.S	.set
		MOVE.L	D3,D1
		ASR.W	D0
		NEG.W	D0
		CMP.W	#CRUNCH_RMIN,D0
		BPL.S	.set
.change		TST.W	OBJ_CNT4(A1)
		BEQ.S	.change_ok
		MOVEQ	#0,D0
		MOVEQ	#0,D2
.change_ok	NEG.W	D2
		BRA.S	.set

.forward	ADD.W	D2,D0
		EXT.L	D0
		BPL.S	.fchk
		ADD.L	D0,D1
		CMP.L	D3,D1
		BGT.S	.set
		MOVE.L	D3,D1
		ASR.W	D0
		NEG.W	D0
		BRA.S	.set
.fchk		ADD.L	D0,D1
		CMP.L	D4,D1
		BLT.S	.set
		MOVE.L	D4,D1
		ASR.W	D0
		NEG.W	D0
		CMP.W	#-CRUNCH_RMIN,D0
		BPL.S	.change
		
		CMP.W	#-CRUNCH_SPARK,D0
		BPL.S	.set
		CMP.W	#$80,OBJ_ZPOS(A1)
		BMI.S	.set
		MOVE.W	OBJ_CNT3(A1),D7
		SUBQ.W	#1,D7
		BMI.S	.set
		
		MOVE.W	D0,-(SP)
		MOVE.W	#SFX_DOORCLANG_A,D0
		JSR	CSFX_ONESHOT_V
		MOVE.W	(SP)+,D0

.spark_lp	MOVEM.L	D0-D2/D6/D7/A0/A1,-(SP)
		MOVE.L	#$C800,D1
		MOVE.L	D6,D2
		JSR	VIS_SET_ZS
		MOVEM.L	(SP)+,D0-D2/D6/D7/A0/A1
		DBRA	D7,.spark_lp

.set		MOVE.W	D0,OBJ_CNT0(A1)
		MOVE.W	D2,OBJ_CNT1(A1)
		RTS

; --------------------------------------------------------------------------

CRUNCH_PRINT_OP	MOVE.L	OBJ_ADDR7(A1),D7
		BEQ.S	CRUNCH_PRINT
		MOVE.L	D7,A2
		MOVE.W	OBJ_SHOT_AMT(A1),D7
		ADD.W	OBJ_SHOT_AMT(A2),D7
		BEQ.S	CRUNCH_PRINT
		CLR.W	OBJ_SHOT_AMT(A1)
		CLR.W	OBJ_SHOT_AMT(A2)
		ADD.W	D7,OBJ_CNT7(A1)
		MOVE.W	OBJ_CNT7(A1),OBJ_CNT7(A2)
		ADDQ.B	#2,OBJ_FLASH1(A1)
		ADDQ.B	#2,OBJ_FLASH1(A2)
		CMP.W	#12,D7
		BMI.S	CRUNCH_PRINT
		ADDQ.B	#6,OBJ_FLASH1(A1)
		ADDQ.B	#6,OBJ_FLASH1(A2)

CRUNCH_PRINT	ADDQ.W	#1,D0
		MOVE.W	D0,D1
		CMP.W	#$160,D1
		BMI.S	.alloc
		ADD.W	#$200,D1
.alloc		ADD.W	OBJ_CNT2(A1),D1
		BSR	O_PR_A_XIT
		
		BSR	O_CALC_PATCHZ
		MOVE.L	OBJ_ADDR0(A1),A0
		MOVE.L	OBJGR_ANM0(A0),A0
		MOVE.L	OBJ_ADDR1(A1),A2
		BSR	O_FLASH_CHECK_S
		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		BSR	O_PR_SET0_W
		MOVEM.W	(A6),D0/D1
		CMP.W	#$100,D0
		BEQ.S	.yclip
		SUB.W	#2,D0
		BMI.S	.yclip
		MOVE.W	D0,(A6)
.yclip		TST.W	D1
		BEQ.S	.end
		ADDQ.W	#2,D1
		CMP.W	#$100,D1
		BPL.S	.end
		MOVE.W	D1,2(A6)
.end		RTS		

; --------------------------------------------------------------------------

CRUNCH_HIT	CMP.W	#IDRANGE_CAR,OBJ_ID(A1)
		BCS.S	.car_hit
		CMP.W	#ID_SHOT,OBJ_ID(A1)
		BEQ.S	.shot
		CMP.W	#ID_MISS,OBJ_ID(A1)
		BNE	OBJ_DET_NEXT
		CMP.L	#CRUNCH_PRINT_OP,OBJ_PRN_CTRL(A2)
		BNE	OBJ_DET_ZXKILL
		ADD.W	#16,OBJ_SHOT_AMT(A2)
.shot		ADD.W	#2,OBJ_SHOT_AMT(A2)
		BRA	OBJ_DET_ZXKILL

.car_hit	MOVEM.L	A0/A1/A2/D7,-(SP)
		EXG	A1,A2

		MOVE.L	OBJ_SPD(A2),D7
		BPL.S	.hit_calc
		NEG.L	D7
.hit_calc	SWAP	D7
		ROL.L	#3,D7
		ADD.W	D7,OBJ_BASH_AMT(A2)

		MOVE.L	#200*$100,D7
		SUB.L	OBJ_XPOS(A2),D7
		ASL.L	#5,D7
		MOVE.L	D7,OBJ_XINERTIA(A2)
		
		ADDQ.B	#4,OBJ_FLASH1(A2)	;Rebound batski.
		MOVE.W	OBJ_SPD(A2),D7
		ASR.W	D7
		NEG.W	D7
		BMI.S	.neg
		ADD.W	#$10,D7
		BRA.S	.set
.neg		CMP.W	OBJ_FLAG2(A1),D7
		BMI.S	.set
		MOVE.W	OBJ_FLAG2(A1),D7
.set		MOVE.W	D7,OBJ_SPD(A2)
		
		CMP.L	#CRUNCH_PRINT_OP,OBJ_PRN_CTRL(A1)
		BEQ.S	.sfx

		BSR	DETECT_DEALL
		MOVE.W	OBJ_CNT5(A1),OBJ_CNT1(A1)
		MOVE.W	#-1,OBJ_CNT4(A1)
		MOVE.B	#6,OBJ_FLASH0(A1)
		CLR.B	OBJ_FLASH1(A1)
		CMP.L	#CRUNCH_MOVE_LRO,OBJ_MOVE_CTRL(A1)
		BNE.S	.double_chk
		MOVE.L	#CRUNCH_MOVE_LR,OBJ_MOVE_CTRL(A1)
		NEG.W	OBJ_CNT1(A1)
.double_chk	MOVE.L	OBJ_ADDR7(A1),D7
		BEQ.S	.sfx
		MOVE.L	D7,A1
		BSR	DETECT_DEALL
		MOVE.W	OBJ_CNT5(A1),OBJ_CNT1(A1)
		MOVE.W	#-1,OBJ_CNT4(A1)
		MOVE.B	#6,OBJ_FLASH0(A1)
		CLR.B	OBJ_FLASH1(A1)
		CMP.L	#CRUNCH_MOVE_LRO,OBJ_MOVE_CTRL(A1)
		BNE.S	.sfx
		MOVE.L	#CRUNCH_MOVE_LR,OBJ_MOVE_CTRL(A1)
		NEG.W	OBJ_CNT1(A1)

.sfx		MOVE.W	#SFX_METCRUNCH_A,D0
		JSR	CSFX_ONESHOT_V

.done		MOVEM.L	(SP)+,A0/A1/A2/D7
		JMP	OBJ_DET_ZXKILL

; --------------------------------------------------------------------------

VIS_CRSPARK_LR
		DC.W	0,$1800,$6000
		DC.W	-$08,0,$600
		DC.W	$0080,$0500
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0W
		DC.L	VIS_SPARK1_GRS
		DC.W	0			;SFX

VIS_CRSPARK_UD
		DC.W	0,$C800,$1800
		DC.W	-$08,0,$600
		DC.W	$0080,$0500
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0W
		DC.L	VIS_SPARK1_GRS
		DC.W	0			;SFX

; --------------------------------------------------------------------------

		XYRATIO	112,400,100,370
		OBJCNTR	0,0,64,110

OBJ_CRUNCH_L	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_L
OBJ_CRANM_L	OBJSCL	0,0,0,0,64,110,CRUNHL

OBJ_CRUNCH_R	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_R
OBJ_CRANM_R	OBJSCL	0,0,0,0,64,110,CRUNHR

OBJ_CRUNCH_DL1	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DL1
OBJ_CRANM_DL1	OBJSCL	0,0,0,0,64,110,DOORL1

OBJ_CRUNCH_DR1	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DR1
OBJ_CRANM_DR1	OBJSCL	0,0,0,0,64,110,DOORR1

OBJ_CRUNCH_DL2	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DL2
OBJ_CRANM_DL2	OBJSCL	0,0,0,0,64,110,DOORL2

OBJ_CRUNCH_DR2	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DR2
OBJ_CRANM_DR2	OBJSCL	0,0,0,0,64,110,DOORR2

OBJ_CRUNCH_DL3	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DL3
OBJ_CRANM_DL3	OBJSCL	0,0,0,0,64,110,DOORL3

OBJ_CRUNCH_DR3	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DR3
OBJ_CRANM_DR3	OBJSCL	0,0,0,0,64,110,DOORR3

OBJ_CRUNCH_DL4	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DL4
OBJ_CRANM_DL4	OBJSCL	0,0,0,0,64,110,DOORL4

OBJ_CRUNCH_DR4	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DR4
OBJ_CRANM_DR4	OBJSCL	0,0,0,0,64,110,DOORR4

OBJ_CRUNCH_DL5	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DL5
OBJ_CRANM_DL5	OBJSCL	0,0,0,0,64,110,DOORL5

OBJ_CRUNCH_DR5	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DR5
OBJ_CRANM_DR5	OBJSCL	0,0,0,0,64,110,DOORR5

OBJ_CRUNCH_DL6	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DL6
OBJ_CRANM_DL6	OBJSCL	0,0,0,0,64,110,DOORL6

OBJ_CRUNCH_DR6	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_DR6
OBJ_CRANM_DR6	OBJSCL	0,0,0,0,64,110,DOORR6

OBJ_CRUNCH_OL	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_OL
OBJ_CRANM_OL	OBJSCL	0,0,0,0,64,110,CRUNOL

OBJ_CRUNCH_OR	DC.L	OBJ_CRSET_LR
		DC.L	OBJ_CRANM_OR
OBJ_CRANM_OR	OBJSCL	0,0,0,0,64,110,CRUNOR

OBJ_CRSET_LR	DC.W	ID_ROADOBJ	;OBJ_ID
		DC.L	CRUNCH_MOVE_LR	;OBJ_MOVE_CTRL
		DC.L	CRUNCH_PRINT	;OBJ_PRN_CTRL
		DC.W	$08		;OBJ_ZSIZE
		XYSIZE	50,110
		DC.W	0		;OBJ_MASS
		DC.L	CRUNCH_HIT	;OBJ_HIT_GOTO
		DC.W	0		;OBJ_ZPOS
		DC.L	-$3000		;OBJ_XPOS
		YSIZE	48		;OBJ_YPOS

		XYRATIO	110,400,100,370
		OBJCNTR	0,0,110,64

OBJ_CRUNCH_U	DC.L	OBJ_CRSET_UD
		DC.L	OBJ_CRANM_U
OBJ_CRANM_U	OBJSCL	0,0,0,0,110,64,CRUNHU

OBJ_CRUNCH_D	DC.L	OBJ_CRSET_UD
		DC.L	OBJ_CRANM_D
OBJ_CRANM_D	OBJSCL	0,0,0,0,110,64,CRUNHD

OBJ_CRSET_UD	DC.W	ID_ROADOBJ	;OBJ_ID
		DC.L	CRUNCH_MOVE_UD	;OBJ_MOVE_CTRL
		DC.L	CRUNCH_PRINT	;OBJ_PRN_CTRL
		DC.W	$08		;OBJ_ZSIZE
		XYSIZE	110,50
		DC.W	0		;OBJ_MASS
		DC.L	CRUNCH_HIT	;OBJ_HIT_GOTO
		DC.W	0		;OBJ_ZPOS
		DC.L	200*$100	;OBJ_XPOS
		DC.L	-$A000		;OBJ_YPOS

; --------------------------------------------------------------------------
