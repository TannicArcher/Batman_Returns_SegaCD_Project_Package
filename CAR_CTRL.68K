; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ Batmobile control.							   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

CAR_ZF		EQU	20

BAT_AIR_DEC	EQU	620;490;520;512
BAT_AIR_MIN	EQU	570;640
BAT_AIR_REBOUND	EQU	400;450;512

; --------------------------------------------------------------------------

CAR_CONTROL	IF	DAMAGE=NO
		CLR.W	OBJ_BASH_AMT(A1)
		CLR.W	OBJ_SHOT_AMT(A1)
		CLR.W	OBJ_DTOTAL(A1)
		ENDIF

		BCLR	#O_SCRAPE,OBJ_FLAG0(A1)

		MOVE.W	JOY0,D6
		MOVE.W	OBJ_SPD(A1),D0
		CMP.W	#-40,D0
		BPL.S	.brake_chk
		BSET	#5,D6
.brake_chk	MOVE.W	D6,D5
		AND.W	#%00101000,D5
		BEQ.S	.pset
		AND.W	#%11111011,D6

.pset		MOVE.W	D0,D1
		MULS	#6600,D0
		BPL.S	.slim
		MOVEQ	#0,D0
		MOVEQ	#0,D1
		BRA.S	.sset
.slim		SWAP	D0
		CMP.W	#16,D0
		BCS.S	.sset
		MOVE.W	#16,D0
.sset		MOVE.W	D0,SRAM_DATA+WRAM_SPEED
		MOVE.W	D1,SRAM_DATA+WRAM_SPDNUM

; --- Auto ---
		
		MOVE.W	STAGE_AUTO,D0
		BEQ	.update
		
		CLR.W	OBJ_BASH_AMT(A1)
		CLR.W	OBJ_SHOT_AMT(A1)
		AND.W	#%11010000,D6
		TST.W	D0
		BMI	.update
		
		OR.W	#%11101000,D6
		MOVE.L	OBJ_XPOS(A1),D0
		SUB.L	#200*$100,D0
		BPL.S	.apos
.aneg		CMP.L	#-20*$100,D0
		BPL.S	.adone
		BSET	#0,D6
		CMP.L	#-70*$100,D0
		BPL.S	.adone
		BCLR	#5,D6
		CMP.L	#-110*$100,D0
		BPL.S	.adone
		BCLR	#3,D6
		BRA.S	.adone
.apos		CMP.L	#20*$100,D0
		BMI.S	.adone
		BSET	#1,D6
		CMP.L	#70*$100,D0
		BMI.S	.adone
		BCLR	#5,D6
		CMP.L	#110*$100,D0
		BMI.S	.adone
		BCLR	#3,D6
.adone		CMP.W	#$40,OBJ_SPD(A1)
		BPL.S	.atype
		BSET	#5,D6

.atype		CMP.W	#2,D0
		BNE.S	.update
		AND.W	#%10000011,D6
		OR.W	#%00000100,D6
		MOVE.W	STAGE_GOTO,D2
		SUB.W	OBJ_SPD(A1),D2
		MOVE.W	D2,STAGE_GOTO
		BMI.S	.update
		CMP.W	#$30,OBJ_SPD(A1)
		BPL.S	.update
		AND.W	#%10000011,D6
		CMP.W	#2*$80,D2
		BMI.S	.update
		OR.W	#%00001000,D6

; --- Update ---

.update		MOVE.W	D6,JOY0
		BSR	O_WHEEL_UPDATE
		TST.L	OBJ_YPOS(A1)
		BEQ.S	.spd_calc

  		BTST	#5,D6
		BEQ.S	.spd_skp
		MOVE.L	OBJ_SPD(A1),D0
		MOVEQ	#0,D1
		MOVE.W	OBJ_SPD_MAXB(A1),D1
		SWAP	D1
		MOVE.W	OBJ_SPD_INCFB(A1),D2
		ASL.W	D2
		BSR	O_CALC_INERTIA
		MOVE.L	D0,OBJ_SPD(A1)
		BRA.S	.spd_skp

.spd_calc	BSR	O_SPEED_UPHILL
.spd_skp	MOVE.W	OBJ_SPD(A1),D0
		SUB.W	#$30,OBJ_ZPOS(A1)
		BSR	O_CALC_MOVEAMTS
		ADD.W	#$30,OBJ_ZPOS(A1)
		MOVE.L	D3,-(SP)
		BSR	O_ROAD_MOVE
		
		BSR	O_DET_PLIMITS
		BPL.S	.view
		TST.L	OBJ_XINERTIA(A1)
		BNE.S	.view
	       	CMP.W	#8,OBJ_SPD(A1)
		BPL.S	.view
		MOVE.L	#50*$100,D3
		MOVE.L	OBJ_XPOS(A1),D0
		SUB.L	#200*$100,D0
		BMI.S	.xpush
		MOVE.L	#350*$100,D3
.xpush		BSR	HITX_GOTO
		ADDQ.B	#2,OBJ_FLASH0(A1)

; --- View angle ---

.view		MOVEQ	#$20,D1
		MOVE.W	OBJ_WHEEL(A1),D2
		BPL.S	.view_calc
		NEG.W	D1
		NEG.W	D2
.view_calc	MOVE.W	#$20*7,D0
		CMP.W	#WHEEL_STEP_N,D2
		BCS.S	.view_set
		ADD.W	D1,D0
		CMP.W	#WHEEL_MAX,D2
		BCS.S	.view_set
		ADD.W	D1,D0
.view_set	MOVE.W	D0,OBJ_CNT0(A1)	

; --- Off ground ---
		
		MOVE.L	(SP)+,D3

		MOVE.L	OBJ_ADDR7(A1),D1
		MOVE.L	OBJ_YPOS(A1),D0
		BNE.S	.air_time

		MOVE.L	D1,D2
		SUB.L	D3,D2
		ADD.L	#BAT_AIR_MIN,D2
		BPL	.air_skp
		ASL.L	#8,D1
		DIVS	#BAT_AIR_REBOUND,D1
		EXT.L	D1

.air_time	ADD.L	#BAT_AIR_DEC,D1
		ADD.L	D1,D0
		BPL.S	.land
		MOVE.W	OBJ_ZPOS(A1),D7
		ADD.W	ZVIEW,D7
		LSR.W	#7,D7
		MULU	#RCSTEP,D7
		LEA	ROAD_CORD_DATA-RCSTEP,A5
		MOVE.L	OBJ_YSIZE(A1),D6
		ASR.L	#1,D6
		ADD.L	OBJ_YSIZE(A1),D6
		NEG.L	D6
		ADD.L	D0,D6
		SUB.L	RCROOF(A5,D7.W),D6
		BCC	.land_skp
		SUB.L	D6,D0
		MOVEM.L	D0/D1/A1,-(SP)
		LEA	VIS_ROOF_SPARK0,A0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVE.L	OBJ_YPOS(A1),D2
		SUB.L	OBJ_YSIZE(A1),D2
		BSR	VIS_SET_ZS
		MOVEM.L	(SP)+,D0/D1/A1
		MOVE.L	OBJ_SPD(A1),D7
		ASR.L	#4,D7
		SUB.L	D7,OBJ_SPD(A1)
		BSET	#O_SCRAPE,OBJ_FLAG0(A1)
		BRA.S	.land_skp
		
.land		CMP.L	#$300,D1
		BMI.S	.land_done

		CMP.L	#$500,D1
		BMI.S	.sfx_skp
		MOVE.W	#SFX_BMLAND_A,D0
		JSR	CSFX_ONESHOT_V
.sfx_skp
		MOVEM.L	D1/A1,-(SP)
		MOVE.W	OBJ_ZPOS(A1),D0
		ADD.W	OBJ_ZSIZE(A1),D0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVE.L	OBJ_YPOS(A1),D2
		MOVE.W	OBJ_SPD(A1),D3
		MOVEM.L	D0-D3,-(SP)
		LEA	VIS_LAND_SPARKL,A0
		ADD.L	#-20*$100,D1
		BSR	VIS_SET
		MOVEM.L	(SP)+,D0-D3
		LEA	VIS_LAND_SPARKR,A0
		ADD.L	#20*$100,D1
		BSR	VIS_SET
		MOVEM.L	(SP)+,D1/A1

.land_done	MOVEQ	#0,D0
		NEG.L	D1
.land_skp	MOVE.L	D1,D3
		MOVE.L	D0,OBJ_YPOS(A1)
.air_skp	MOVE.L	D3,OBJ_ADDR7(A1)

; --- Skid visuals ---

   		BTST	#O_SKID,OBJ_FLAG0(A1)
		BEQ.S	.misc
		LEA	VIS_BSKIDSMOKE,A0
		LEA	BAT_SMK_POS-(8*5),A2
		MOVE.W	OBJ_CNT0(A1),D0
		ASR.W	#2,D0
		LEA	(A2,D0.W),A2
		MOVE.L	OBJ_XPOS(A1),D1
		ADD.L	(A2)+,D1
		MOVEQ	#0,D3
		MOVE.W	OBJ_ZPOS(A1),D0
		SUB.W	OBJ_SPD(A1),D0
		MOVE.L	OBJ_YPOS(A1),D2
		MOVEM.L	A0/A1/A2/D0/D2/D3,-(SP)
		BSR	VIS_SET
		MOVEM.L	(SP),A0/A1/A2/D0/D2/D3
		MOVE.L	OBJ_XPOS(A1),D1
		ADD.L	(A2)+,D1
		BSR	VIS_SET
		MOVEM.L	(SP)+,A0/A1/A2/D0/D2/D3

; --- Misc. ---

.misc		
		MOVE.W	ROAD_CORD_DATA+RCSTEP+RCROOF,TUNNEL_SFX_FLAG

		IF	BATCD_SHELL
		TST.B	shl_sfx
		BEQ.S	.sskp5
		ENDIF

		MOVE.W	JOY0,D0		
   		AND.W	#%00101100,D0
		BTST	#5,D0
		BEQ.S	.skid_chk
		BSET	#3,D0
.skid_chk	BTST	#O_SKID,OBJ_FLAG0(A1)
		BEQ.S	.sskp0
		BSET	#4,D0
.sskp0		TST.L	OBJ_YPOS(A1)
		BPL.S	.sskp1
		BSET	#6,D0
.sskp1		BTST	#O_SCRAPE,OBJ_FLAG0(A1)
		BEQ.S	.sskp2
		BSET	#1,D0
.sskp2		
.sskp3		CMP.W	#4*$FFFF/6,OBJ_DTOTAL(A1)
		BCS.S	.sskp4
		BSET	#7,D0
.sskp4		MOVE.W	OBJ_SPD(A1),D1
		
		MOVE.L	A1,-(SP)
		JSR	CSFX_BATMOBILE
		MOVE.L	(SP)+,A1
.sskp5
		TST.B	OBJ_FLAG3(A1)
		BEQ.S	.damage_norm
		SUBQ.B	#1,OBJ_FLAG3(A1)
		BSR	DAMAGE_HEAL
		BRA.S	.damage_meter
.damage_norm	BSR	DAMAGE_CTRL
.damage_meter	BSR	PDAMAGE_METER
		
		BRA	SHOT_SETCHECK

; --------------------------------------------------------------------------

SHOT_PTIME	EQU	2
SHOT_FMIN	EQU	5

SHOT_RANGE	EQU	$700
SHOT_ZMOVE	EQU	$60
SHOT_ZINIT	EQU	$198+$38
SHOT_ZOFF	EQU	$C00

SHOT_SETCHECK	BSR	MISS_SETCHECK

		MOVE.B	SHOT_FLAG,D0
		MOVE.W	JOY0,D1
		MOVE.B	D1,SHOT_FLAG
		
		TST.W	STAGE_AUTO
		BNE	.end
		
		NOT.B	D0	
		AND.B	D1,D0
		AND.B	#%01000000,D0
		BEQ	.fcheck
		MOVE.W	#SHOT_FMIN,SHOT_FCOUNT
		SUBQ.W	#1,SHOT_PAUSE
		
.fcheck		TST.W	SHOT_PAUSE
		BGT	.wait
		AND.B	#%01000000,D1
		BEQ	.wait
		TST.W	SHOT_FCOUNT
		BEQ	.end
		SUBQ.W	#1,SHOT_FCOUNT

		BSR	OBJECT_ALLOC
		BNE	.wait
		
		LEA	OBJECT_DATA,A0
		MOVE.W	#2,OBJ_CNT1(A0)
		MOVE.W	#SHOT_PTIME,SHOT_PAUSE

		LEA	SHOT_CAR_XPOS-8*5,A4
		MOVE.W	OBJ_CNT0(A0),D7
		ASR.W	#2,D7
		LEA	(A4,D7.W),A4
		LEA	ROAD_CORD_DATA-RCSTEP,A5
		MOVE.W	OBJ_ZPOS(A0),D7	
		ADD.W	#$40,D7
		ADD.W	ZVIEW,D7
		LSR.W	#7,D7
		MULU	#RCSTEP,D7
		LEA	RCYFRACT(A5,D7.W),A5

		BSR.S	.set
		MOVE.L	OBJ_XPOS(A0),D0
		ADD.L	(A4)+,D0
		MOVE.L	D0,OBJ_XPOS(A1)

		BSR	OBJECT_ALLOC
		BNE	.end
		BSR.S	.set
		MOVE.L	OBJ_XPOS(A0),D0
		ADD.L	(A4),D0
		MOVE.L	D0,OBJ_XPOS(A1)
		
		MOVE.W	#SFX_BDFIRE_A,D0
		JSR	CSFX_ONESHOT_V

		RTS

.set		MOVE.W	#$10,OBJ_ZSIZE(A1)
		MOVE.L	#$10*$100,OBJ_XSIZE(A1)
		MOVE.L	#$10*$100,OBJ_YSIZE(A1)
		
		MOVE.W	OBJ_ZPOS(A0),D0
		ADD.W	OBJ_ZSIZE(A0),D0
		MOVE.W	D0,OBJ_ZPOS(A1)
		
		MOVE.L	OBJ_YPOS(A0),D0
		SUB.L	#$100*20,D0
		MOVE.L	D0,OBJ_YPOS(A1)
		
		MOVE.W	#ID_SHOT,OBJ_ID(A1)
		MOVE.W	#SHOT_ZMOVE,OBJ_CNT0(A1)
		MOVE.L	#SHOT_MOVE,OBJ_MOVE_CTRL(A1)
		MOVE.L	#SHOT_PRINT,OBJ_PRN_CTRL(A1)

		MOVE.W	OBJ_WHEEL(A0),D0
		EXT.L	D0
		DIVS	#95,D0
		NEG.W	D0
		MOVE.W	D0,OBJ_CNT1(A1)

		MOVE.W	(A5),D0
		NEG.W	D0
		MOVE.W	D0,OBJ_CNT2(A1)
		RTS

.wait		SUBQ.W	#1,SHOT_PAUSE
		BPL.S	.end
		CLR.W	SHOT_PAUSE
.end		RTS

SHOT_CAR_XPOS	DC.L	-40*$100,8*$100
		DC.L	-34*$100,14*$100
		DC.L	-24*$100,24*$100
		DC.L	-14*$100,34*$100
		DC.L	-8*$100,40*$100

; --------------------------------------------------------------------------

SHOT_MOVE	MOVE.W	OBJ_ZPOS(A1),D0
		BMI	O_CLEAR_S
		CMP.W	#SHOT_RANGE,D0
		BPL	O_CLEAR_S

		BSR	O_CALC_PATCH		
		NEG.W	D4
		ADD.W	#$80,D4

		MOVEQ	#0,D0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVE.L	OBJ_YPOS(A1),D2
		MOVE.W	ZMOVE,D3
		BMI.S	.mgo

.zloop		SUB.W	D4,D3
		BPL.S	.zcalc
		ADD.W	D3,D4
		BLE.S	.mset
.zcalc		ADD.W	D4,D0			;Zmove update.
		MOVE.W	D4,D5
		MOVE.W	RCXFRACT(A5),D6
		MOVE.W	OBJ_CNT2(A1),D7
		ADD.W	RCYFRACT(A5),D7
		MULS	D6,D4
		MULS	D7,D5
		SUB.L	D4,D1
		SUB.L	D5,D2
		BPL.S	.mhit
		LEA	RCSTEP(A5),A5
		MOVE.W	#$80,D4
		BRA.S	.zloop

.mset		NEG.W	D4
.mgo		MOVE.W	OBJ_CNT0(A1),D3

.mloop		SUB.W	D4,D3
		BPL.S	.mcalc
		ADD.W	D3,D4
		BLE.S	.mdone
.mcalc		ADD.W	D4,D0			;Zmove update.
		MOVE.W	D4,D5
		MOVE.W	OBJ_CNT1(A1),D6
		MOVE.W	OBJ_CNT2(A1),D7
		ADD.W	RCXFRACT(A5),D6
		ADD.W	RCYFRACT(A5),D7
		MULS	D6,D4
		MULS	D7,D5
		SUB.L	D4,D1
		SUB.L	D5,D2
		BPL.S	.mhit
		LEA	RCSTEP(A5),A5
		MOVE.W	#$80,D4
		BRA.S	.mloop

.mhit		DIVS	D7,D2			;Recalc move amts.
		ADD.W	D2,D0			;Zmove to hit.
		MULS	D6,D2
		SUB.L	D2,D1			;Xpos at hit.
		MOVEQ	#0,D2			;Ypos at hit.
		NOT.B	OBJ_FLAG1(A1)
.mdone		MOVE.L	D2,OBJ_YPOS(A1)		;Set Ypos.
		SUB.L	OBJ_XPOS(A1),D1		;Xmove

		LEA	SIGN_DET_ZXKILL,A5	;Sign hit ajust routine.
		MOVE.L	OBJECT_DET_LIST+((OBJECT_MAX-1)*4),-(SP)
		CLR.L	OBJECT_DET_LIST+((OBJECT_MAX-1)*4)
		BSR	DETECT_ZXY
		MOVE.L	(SP)+,OBJECT_DET_LIST+((OBJECT_MAX-1)*4)
		ADD.W	D0,OBJ_ZPOS(A1)
		ADD.L	D1,OBJ_XPOS(A1)
		
		MOVE.W	DET_HIT_FLAG,D0
		BNE.S	.detect_hit
		TST.B	OBJ_FLAG1(A1)
		BNE.S	.ground_hit
		RTS

.ground_hit	MOVE.W	#SFX_BDHIT_B,D0
		JSR	CSFX_ONESHOT_V

		LEA	VIS_SPARK_0,A0
		MOVE.W	ZMOVE,D3
		BRA.S	.set_vis

.detect_hit	CMP.W	#OBJ_HIT,D0
		BCC	.object_hit

.sign_hit	MOVE.W	#SFX_BDHIT_B,D0
		JSR	CSFX_ONESHOT_V

		LEA	VIS_SPARK_0,A0
		MOVEQ	#0,D3

.set_vis	MOVE.L	A1,-(SP)
		BSR	VIS_SET_YZX
		MOVE.L	(SP)+,A1
		BRA	O_CLEAR_S

.object_hit	MOVE.W	#SFX_BDHIT_A,D0
		JSR	CSFX_ONESHOT_V

		MOVE.L	DET_HIT_DATA1,A2
		CMP.W	#ID_CAR,OBJ_ID(A2)
		BNE.S	.skp
		ADDQ.W	#2,OBJ_SHOT_AMT(A2)
		IF	BATCD_SHELL
		TST.B	shl_difficulty
		BNE.S	.skp
		ADDQ.W	#2,OBJ_SHOT_AMT(A2)
		ENDIF
.skp		MOVE.W	OBJ_SPD(A2),D3
		LEA	VIS_WHACKS_0,A0

		MOVEM.L	A1/A2,-(SP)
		BSR	VIS_SET_YZX
		MOVE.L	A1,A3
		MOVEM.L	(SP)+,A1/A2
		BNE	O_CLEAR_S
		CMP.L	A1,A2
		BCS	O_CLEAR_S
		MOVE.W	OBJ_SPD(A2),D0
		ADD.W	D0,OBJ_ZPOS(A3)
		BRA	O_CLEAR_S

; --------------------------------------------------------------------------

SHOT_PRINT	BSR	O_PR_ALLOCX_XIT	
		BSR	O_CALC_PATCH
		LEA	OBJ_DISC,A0
		BRA	O_PR_SET0
	
; --------------------------------------------------------------------------

MISS_PTIME	EQU	6
MISS_ZMOVE	EQU	$60
MISS_ZINIT	EQU	$80
MISS_DAMAGE	EQU	10

MISS_SETCHECK	MOVE.B	MISS_FLAG,D0
		MOVE.W	JOY0,D1
		MOVE.B	D1,MISS_FLAG
		NOT.B	D0	
		AND.B	D1,D0
		AND.B	#%00010000,D0
		BEQ	.wait
		TST.W	MISSTOT
		BEQ	.end
		MOVE.W	MISS_PAUSE,D0
		OR.W	STAGE_AUTO,D0
		BNE.S	.wait

		LEA	OBJ_BTMISS,A6
		BSR	O_OBJECT_SETUP
		BNE	.end
		
		LEA	OBJECT_DATA,A0
		MOVE.W	OBJ_ZPOS(A0),D0
		ADD.W 	#MISS_ZINIT,D0
		MOVE.W	D0,OBJ_ZPOS(A1)
		MOVE.L	OBJ_XPOS(A0),OBJ_XPOS(A1)
		MOVE.L	OBJ_YPOS(A0),D0
		SUB.L	#$1000,D0
		MOVE.L	D0,OBJ_YPOS(A1)

		MOVE.W	OBJ_WHEEL(A0),D7
		EXT.L	D7
		MOVE.L	D7,OBJ_CNT0(A1)

		MOVE.L	#MISS_HIT_OBJECT,OBJ_ADDR7(A1)
		SUBQ.W	#1,MISSTOT
		MOVE.W	#MISS_PTIME,MISS_PAUSE
		
		MOVE.W	#SFX_BRFIRE_A,D0
		JSR	CSFX_ONESHOT_V

		RTS

.wait		SUBQ.W	#1,MISS_PAUSE
		BPL.S	.end
		CLR.W	MISS_PAUSE
.end		RTS

; --------------------------------------------------------------------------

MISS_FIND	MOVEQ	#0,D3
		MOVE.W	#$1000,D1
		MOVE.W	OBJ_ZPOS(A1),D2
		LEA	OBJECT_DATA,A2
		MOVEQ	#OBJECT_MAX-2,D7
		
.loop		LEA	OBJ_DLEN(A2),A2
		TST.B	OBJ_FLAG0(A2)
		BEQ.S	.next	
		MOVE.W	OBJ_ID(A2),D0
		BLE.S	.next
		CMP.W	#IDRANGE_CAR,D0
		BPL.S	.next
		MOVE.W	OBJ_ZPOS(A2),D0
		ADD.W	OBJ_ZSIZE(A2),D0
		CMP.W	D2,D0
		BMI.S	.next
		SUB.W	OBJ_ZSIZE(A2),D0
		CMP.W	D1,D0
		BPL.S	.next
		MOVE.L	A2,D3
		MOVE.W	D0,D1
.next		DBRA	D7,.loop
		RTS

; --------------------------------------------------------------------------

MISS_MOVE	
		BSR	O_RANGE_CHK 	
		BRA.L	MISS_KILL
		BRA.L	MISS_KILL

		BSR	MISS_FIND
		
		MOVE.W	#MISS_ZMOVE,D0
		ADD.W	ZMOVE,D0
		MOVEQ	#0,D1
		MOVEQ	#0,D2
		
		TST.L	D3
		BEQ.S	.xrange
		MOVE.L	D3,A2
		MOVE.W	OBJ_ZPOS(A2),D0
		SUB.W	OBJ_ZPOS(A1),D0
		ASR.W	#3,D0
		ADD.W	#$10,D0
		ADD.W	OBJ_SPD(A2),D0
		CMP.W	#$180,D0
		BMI.S	.xmove
		MOVE.W	#$180,D0
.xmove		MOVE.L	#$800,D1
		MOVE.L	OBJ_XPOS(A1),D4
		SUB.L	OBJ_XPOS(A2),D4
		BMI.S	.ymove
		NEG.L	D1
.ymove		MOVE.L	#$400,D2
		MOVE.L	OBJ_YPOS(A2),D4
		SUB.L	OBJ_YPOS(A1),D4
		ASL.L	D4
		SUB.L	OBJ_YSIZE(A2),D4
		ADD.L	OBJ_YSIZE(A1),D4
		BPL.S	.xrange
		NEG.L	D2

.xrange		ADD.L	OBJ_CNT0(A1),D1
		BPL.S	.xpos
		CMP.L	#-$1400,D1
		BPL.S	.xset
		MOVE.L	#-$1400,D1
		BRA.S	.xset
.xpos		CMP.L	#$1400,D1
		BMI.S	.xset
		MOVE.L	#$1400,D1
.xset		MOVE.L	D1,OBJ_CNT0(A1)

.yrange		ADD.L	OBJ_CNT2(A1),D2
		BPL.S	.ypos
		CMP.L	#-$E00,D2
		BPL.S	.yset
		MOVE.L	#-$E00,D2
		BRA.S	.yset
.ypos		CMP.L	#$E00,D2
		BMI.S	.yset
		MOVE.L	#$E00,D2
.yset		MOVE.L	D2,OBJ_CNT2(A1)
		ADD.L	D2,OBJ_YPOS(A1)

		LEA	SIGN_DET_ZXKILL,A5	;Sign hit adjust routine.
		CLR.L	OBJECT_DET_LIST
		BSR	DETECT_ZXY
		MOVE.L	#OBJECT_DATA,OBJECT_DET_LIST
		ADD.W	D0,OBJ_ZPOS(A1)
		ADD.L	D1,OBJ_XPOS(A1)

		MOVE.W	DET_HIT_FLAG,D0
		BNE.S	.hit
		LEA	VIS_SMOKE_MISS,A0
		BRA	VIS_SET_XYZS

.hit		CMP.W	#OBJ_HIT,D0
		BCS.S	MISS_HIT_SIGN
		MOVE.L	DET_HIT_DATA1,A2
		BRA.S	MISS_HIT_OBJECT
.end		RTS

MISS_KILL	
;		CLR.W	MISS_PAUSE
		BRA.L	O_CLEAR_S

MISS_HIT_OBJECT	CMP.W	#ID_MAIN,OBJ_ID(A2)
		BNE.S	.cont
		RTS
.cont		CMP.W	#ID_CAR,OBJ_ID(A2)
		BNE.S	.skp
		ADD.W	#MISS_DAMAGE,OBJ_SHOT_AMT(A2)
		ADDQ.B	#6,OBJ_FLASH1(A2)
.skp		MOVE.W	OBJ_SPD(A2),OBJ_SPD(A1)

MISS_HIT_SIGN
		BSR	MISS_KILL
		
		LEA	VIS_EXPLD_A,A0
		BRA	VIS_SET_XYZS

MISS_PRINT	MOVE.L	OBJ_ADDR1(A1),A2
		BSR	O_PR_ALLOCX
		BRA.L	.deall
		BRA.L	.deall
		BRA.L	.deall

		BSR	O_CALC_PATCHZ
		MOVE.L	OBJ_ADDR0(A1),A3
		MOVE.L	OBJGR_ANM0(A3),A0
		BSR	O_CALC_VIEW

		MOVEQ	#0,D1
		MOVE.W	#$20,D2
		MOVE.W	OBJ_CNT1(A1),D3
		BPL.S	.xfrmp
		NEG.W	D2
		NEG.W	D3
.xfrmp		CMP.W	#$600,D3
		BMI.S	.yfrm
		ADD.W	D2,D1
		CMP.W	#$C00,D3
		BMI.S	.yfrm
		ADD.W	D2,D1
.yfrm		MOVE.W	#9*$20,D2
		MOVE.W	OBJ_CNT3(A1),D3
		BPL.S	.yfrmp
		NEG.W	D2
		NEG.W	D3
.yfrmp		CMP.W	#$600,D3
		BMI.S	.frmset
		ADD.W	D2,D1
.frmset		LEA	(A0,D1.W),A0

		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		BRA	O_PR_SET0

.deall		CLR.L	OBJ_ADDR1(A1)
		BRA	O_DEALL_S

; --------------------------------------------------------------------------

		XYRATIO	100,220,100,220
		OBJCNTR	12,7,14,12
OBJ_BTMISS
		DC.L	OBJ_BTMISS_SET
		DC.L	OBJ_BTMISS_ANM0


OBJ_BMISS_XVIEW	DC.W	22*$20,$20
		DC.L	$3000,$7800,-1
OBJ_BMISS_YVIEW	DC.W	9*$20
		DC.W	$16,-1

OBJ_BTMISS_ANM0	DC.L	OBJ_BMISS_XVIEW,OBJ_BMISS_YVIEW
		
		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,4,44,0,14,24,BTMISS
		OBJSCL	12,3,0,0,14,26,BTMISS
		OBJSCL	12,4,44,0,14,24,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS

		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,5,87,0,15,23,BTMISS
		OBJSCLM	12,4,44,0,14,24,BTMISS
		OBJSCL	12,3,0,0,14,26,BTMISS
		OBJSCL	12,4,44,0,14,24,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS
		OBJSCL	12,5,87,0,15,23,BTMISS

		OBJSCLM	12,8,103,0,15,15,BTMISS
		OBJSCLM	12,8,103,0,15,15,BTMISS
		OBJSCLM	12,8,103,0,15,15,BTMISS
		OBJSCLM	12,7,59,0,13,16,BTMISS
		OBJSCL	12,7,15,0,14,17,BTMISS
		OBJSCL	12,7,59,0,13,16,BTMISS
		OBJSCL	12,8,103,0,15,15,BTMISS
		OBJSCL	12,8,103,0,15,15,BTMISS
		OBJSCL	12,8,103,0,15,15,BTMISS

		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,73,0,13,14,BTMISS
		OBJSCL	12,9,30,0,13,15,BTMISS
		OBJSCL	12,9,73,0,13,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS

		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,119,0,15,14,BTMISS
		OBJSCLM	12,9,73,0,13,14,BTMISS
		OBJSCL	12,9,30,0,13,15,BTMISS
		OBJSCL	12,9,73,0,13,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS
		OBJSCL	12,9,119,0,15,14,BTMISS


OBJ_BTMISS_SET	DC.W	ID_MISS		;OBJ_ID
		DC.L	MISS_MOVE	;OBJ_MOVE_CTRL
		DC.L	MISS_PRINT	;OBJ_PRN_CTRL
		DC.W	$20		;OBJ_ZSIZE
		XYSIZE	14,12
		DC.W	0		;OBJ_MASS
		DC.L	O_T2M_HIT_EN	;OBJ_HIT_GOTO

VIS_SMOKE_MISS
		DC.W	$0000,$400,$400
		DC.W	-$1C,0,0
		DC.W	$0100,$0A00
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN1
		DC.L	VIS_SMOKE2_GR2A
		DC.W	0		;SFX

; --------------------------------------------------------------------------

CAR_PRINT	;RTS

		BSR	O_CALC_PATCH

		MULS	RCXFRACT(A5),D4
		ADD.L	RCXPOS(A5),D4
		ADD.L	OBJ_XPOS(A1),D4
		MOVE.L	CAMERA_X,D7
		OR.L	CAMERA_VIBE,D7
		BNE.S	.skp
		MOVEQ	#0,D4
.skp		
		MULS	RCYFRACT(A5),D5
		ADD.L	RCYPOS(A5),D5
		MOVE.L	OBJ_YPOS(A1),D1
		BEQ.S	.on_ground
		ADD.L	D1,D5
		
		MOVE.L	OBJ_ADDR7(A1),D1
		LEA	OBJ_MOB_JUMP,A3
		BRA.S	.jump

.on_ground	MOVE.W	RCYFRACT(A5),D1
		LEA	OBJ_MOB_YVIEW,A3
.jump		MOVE.W	(A3)+,D2
		MOVE.W	OBJ_CNT0(A1),D3
		TST.W	D1
		BPL.S	.yangle_find
		NEG.W	D1
		NEG.W	D2
.yangle_find	CMP.W	(A3)+,D1
		BCS.S	.print
    		ADD.W	D2,D3
		BRA.S	.yangle_find

.print		BSR	PR_BATFLAME
		BSR	PR_BATMOBILE
		BSR	PR_BATDBLAST

PR_BATDAMAGE	MOVE.L	OBJ_DAMAGE_DATA(A1),D2
		BEQ.S	.end
		MOVE.L	D2,A0
		LEA	DAT_ADDR00(A0),A3
		LEA	DAT_CNT44(A0),A4
		MOVE.W	(A4)+,D7
		LSR.W	#2,D3
		MOVE.W	D3,D2
		LSR.W	#2,D2
		ADD.W	D2,D3
		BSR	O_CALC_PATCH
		BRA.S	.first
.loop		MOVE.W	D7,-(SP)
		MOVE.L	4(A3),A0
		MOVE.L	8(A3),A2
		LEA	8(A0,D3.W),A0
		BSR	SD_PR
		LEA	12(A3),A3
		LEA	2(A4),A4
		MOVE.W	(SP)+,D7
.first		DBRA	D7,.loop
.end		RTS

PR_BATMOBILE	BSR	O_PR_ALLOC_XIT
		LEA	OBJ_MOBILE_ANM0+8,A0
		MOVE.L	OBJ_ADDR1(A1),A2
		LEA	(A0,D3.W),A0
		
		BSR	O_FLASH_CHECK_S
		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		BSR	O_PR_SET1
		
		BSR	O_PZ_ALLOC_XIT
		LEA	OBJ_SHADOW,A0
		MOVE.L	OBJ_ADDR2(A1),A2
		SUB.L	OBJ_YPOS(A1),D5
		LEA	(A0,D3.W),A0

		BSR	O_FCS_CHK
		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR2(A1)
		BSR	O_PR_SET1
		ADD.L	OBJ_YPOS(A1),D5
		RTS

PR_BATFLAME	MOVE.W	OBJ_CNT7(A1),D1
		ADDQ.W	#4,D1
		CMP.W	#3*4,D1
		BCS.S	.askp
		MOVEQ	#0,D1
.askp		MOVE.W	D1,OBJ_CNT7(A1)
		
	  	LEA	FLAME_TAB,A0
  		BTST	#5,JOY0+1
	  	BEQ.S	.fskp
		LEA	FLAME_TAB+12,A0
.fskp		MOVE.L	(A0,D1.W),D1
		BNE.S	.fprint
		RTS
		
.fprint		MOVE.L	D1,A0
		BSR	O_PZ_ALLOC_XIT
		MOVE.L	OBJ_ADDR3(A1),A2
		LEA	(A0,D3.W),A0
		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR3(A1)
		BRA	O_PR_SET1

PR_BATDBLAST	MOVE.W	OBJ_CNT1(A1),D1
		BNE.S	.pr_blast
		RTS

.pr_blast	SUBQ.W	#1,OBJ_CNT1(A1)
		LEA	OBJ_DBLAST_LB,A0
		LSR.W	D1
		BCS.S	.go
		LEA	OBJ_DBLAST_LA,A0
.go		BSR	O_PZ_ALLOC_XIT
		MOVE.L	OBJ_ADDR4(A1),A2
		MOVE.W	D3,D1
		LSR.W	#2,D1
		MOVE.W	D1,D2
		LSR.W	#2,D1
		ADD.W	D2,D1
		LEA	(A0,D1.W),A0
		BSR	O_ALLOC_DEALL_P
		MOVE.L	A0,OBJ_ADDR4(A1)
		BSR	O_PR_SET1P
		LEA	15*10(A0),A0
		BSR	O_PZ_ALLOC_XIT
		BRA	O_PR_SET1P

; --------------------------------------------------------------------------

FLAME_TAB	DC.L	0,OBJ_FLAME_NA1,OBJ_FLAME_NA2
		DC.L	OBJ_TURBO_NA0,OBJ_TURBO_NA1,OBJ_TURBO_NA2

		XYRATIO	$100,$114,$100,$114
		OBJCNTR	129,62,60,46

OBJ_MOBILE	DC.L	OBJ_MOBILE_SET
		DC.L	OBJ_MOBILE_ANM0
		DC.L	OBJ_MOBILE_ANM0
		DC.L	0
		DC.L	OBJ_MOBILE_DAM

BAT_SMK_POS	XSIZE	36			
		XSIZE	-12			
		XSIZE	30			
		XSIZE	-18			
		XSIZE	24
		XSIZE	-24			
		XSIZE	18			
		XSIZE	-30			
		XSIZE	12			
		XSIZE	-36			
		
OBJ_MOB_XVIEW	DC.W	7*$20,$20
		DC.L	$3000,$7800,-1
OBJ_MOB_YVIEW	DC.W	5*$20
		DC.W	$16,-1
OBJ_MOB_JUMP	DC.W	5*$20
		DC.W	$400,-1

OBJ_MOBILE_ANM0	DC.L	OBJ_MOB_XVIEW,OBJ_MOB_YVIEW

		OBJSCLM	114,58,0,0,76,54,BATMOC
		OBJSCLM	122,58,0,0,64,54,BATMOB
		OBJSCL	129,58,0,0,60,53,BATMOA
		OBJSCL	122,58,0,0,64,54,BATMOB
		OBJSCL	114,58,0,0,76,54,BATMOC
		
		OBJSCLM	114,62,0,0,76,49,BATMOF
		OBJSCLM	122,62,0,0,65,48,BATMOE
		OBJSCL	129,62,0,0,60,46,BATMOD
		OBJSCL	122,62,0,0,65,48,BATMOE
		OBJSCL	114,62,0,0,76,49,BATMOF
		
		OBJSCLM	114,66,0,0,76,44,BATMOI
		OBJSCLM	122,66,0,0,64,44,BATMOH
		OBJSCL	129,66,0,0,60,43,BATMOG
		OBJSCL	122,66,0,0,64,44,BATMOH
		OBJSCL	114,66,0,0,76,44,BATMOI

OBJ_SHADOW	OBJSCLM	120,73,0,0,70,42,BSHADC
		OBJSCLM	125,72,0,0,61,43,BSHADB
		OBJSCL	130,71,0,0,58,43,BSHADA
		OBJSCL	125,72,0,0,61,43,BSHADB
		OBJSCL	120,73,0,0,70,42,BSHADC

		OBJSCLM	120,74,0,0,70,40,BSHADF
		OBJSCLM	125,72,0,0,62,41,BSHADE
		OBJSCL	129,74,0,0,60,37,BSHADD
		OBJSCL	125,72,0,0,62,41,BSHADE
		OBJSCL	120,74,0,0,70,40,BSHADF

		OBJSCLM	119,88,0,0,71,25,BSHADI
		OBJSCLM	124,87,0,0,62,26,BSHADH
		OBJSCL	129,85,0,0,60,27,BSHADG
		OBJSCL	124,87,0,0,62,26,BSHADH
		OBJSCL	119,88,0,0,71,25,BSHADI

OBJ_MOBILE_SET	DC.W	ID_MAIN		;OBJ_ID
		DC.L	SIGN_NEXT	;OBJ_MOVE_CTRL
		DC.L	CAR_PRINT	;OBJ_PRN_CTRL
		DC.W	$50		;OBJ_ZSIZE
		RATIO	$100,$114,60	;OBJ_XSIZE
		RATIO	$100,$114,46	;OBJ_YSIZE
		DC.W	$5050		;OBJ_MASS
		DC.L	HROUTINE_CAR	;OBJ_HIT_GOTO
		DC.W	$100+CAR_ZF	;OBJ_ZPOS
		DC.L	$100*200	;OBJ_XPOS
		DC.L	$100*0		;OBJ_YPOS
		DC.W	$60		;OBJ_SPD_MAXA
		DC.W	$A0		;OBJ_SPD_MAXB
		DC.W	30		;OBJ_SPD_INCFA
		DC.W	20		;OBJ_SPD_INCFB
		DC.W	50		;OBJ_SPD_DECF
		DC.W	10		;OBJ_SPD_BRAKE
		DC.W	$050A		;OBJ_TRACTION

; --------------------------------------------------------------------------

OBJ_MOBILE_DAM	DC.W	($FFFF/$240)/10,$FFFF/30

		DC.W	3*$FFFF/6
		DC.L	OBJ_BAT_DPL,DAM_SET0
		DC.W	4*$FFFF/6
		DC.L	OBJ_BAT_DPL,DAM_SET0
		DC.W	5*$FFFF/6
		DC.L	OBJ_BAT_DPL,DAM_SET0
		
		DC.W	$FFFF
		DC.L	0,KILL_SET3

OBJ_BAT_DPL	DC.W	5
		DC.L	OBJ_BAT_DP1
		DC.L	OBJ_BAT_DP2
		DC.L	OBJ_BAT_DP3
		DC.L	OBJ_BAT_DP4
		DC.L	OBJ_BAT_DP5
		
OBJ_BAT_DP1	DC.L	VIS_FLAME3,VIS_EXPCTRL_LF
		DC.L	OBJ_BTDAM_XVIEW,OBJ_BTDAM_YVIEW
		DAMPTS	$11,145,76,$11,142,76,$11,138,76
		DAMPTS	$11,144,75,$11,142,75,$11,138,75
		DAMPTS	$11,145,78,$11,142,78,$11,138,78

OBJ_BAT_DP2	DC.L	VIS_FLAME2M,VIS_EXPCTRL_RF
		DC.L	OBJ_BTDAM_XVIEW,OBJ_BTDAM_YVIEW
		DAMPTS	$11,181,99,$11,173,100,$11,166,100
		DAMPTS	$11,182,96,$11,174,98,$11,166,99
		DAMPTS	$11,182,96,$11,174,97,$11,166,97

OBJ_BAT_DP3	DC.L	VIS_FLAME4M,VIS_EXPCTRL_RF
		DC.L	OBJ_BTDAM_XVIEW,OBJ_BTDAM_YVIEW
		DAMPTS	$11,183,64,$01,185,65,$01,187,66
		DAMPTS	$11,182,70,$01,185,71,$01,187,71
		DAMPTS	$11,182,79,$01,185,79,$01,187,79

OBJ_BAT_DP4	DC.L	VIS_FLAME2,VIS_EXPCTRL_LF
		DC.L	OBJ_BTDAM_XVIEW,OBJ_BTDAM_YVIEW
		DAMPTSM	$11,181,99,$11,173,100,$11,166,100
		DAMPTSM	$11,182,96,$11,174,98,$11,166,99
		DAMPTSM	$11,182,96,$11,174,97,$11,166,97

OBJ_BAT_DP5	DC.L	VIS_FLAME4,VIS_EXPCTRL_LF
		DC.L	OBJ_BTDAM_XVIEW,OBJ_BTDAM_YVIEW
		DAMPTSM	$11,183,64,$01,185,65,$01,187,66
		DAMPTSM	$11,182,70,$01,185,71,$01,187,71
		DAMPTSM	$11,182,79,$01,185,79,$01,187,79

OBJ_BTDAM_XVIEW	DC.W	7*10,10
		DC.L	$3000,$7800,-1
OBJ_BTDAM_YVIEW	DC.W	5*10
		DC.W	$16,-1

; --------------------------------------------------------------------------

OBJ_FLAME_NA1	
		OBJSCLM	141,97,40,0,9,10,BTFLAM
		OBJSCLM	146,97,18,0,10,10,BTFLAM
		OBJSCL	155,96,0,0,8,11,BTFLAM
		OBJSCL	146,97,18,0,10,10,BTFLAM
		OBJSCL	141,97,40,0,9,10,BTFLAM

		OBJSCLM	140,95,102,4,10,10,BTFLAM
		OBJSCLM	147,95,82,3,9,10,BTFLAM
	    	OBJSCL	154,94,60,3,10,11,BTFLAM
		OBJSCL	147,95,82,3,9,10,BTFLAM
		OBJSCL	140,95,102,4,10,10,BTFLAM

		OBJSCLM	140,96,166,4,10,9,BTFLAM
		OBJSCLM	147,95,146,4,9,9,BTFLAM
		OBJSCL	154,95,124,4,10,10,BTFLAM
		OBJSCL	147,95,146,4,9,9,BTFLAM
		OBJSCL	140,96,166,4,10,9,BTFLAM

OBJ_FLAME_NA2	
		OBJSCLM	141,97,50,0,9,10,BTFLAM
		OBJSCLM	146,97,29,0,10,10,BTFLAM
		OBJSCL	155,96,9,0,8,11,BTFLAM
		OBJSCL	146,97,29,0,10,10,BTFLAM
		OBJSCL	141,97,50,0,9,10,BTFLAM
		
		OBJSCLM	140,95,113,4,10,10,BTFLAM
		OBJSCLM	147,95,92,3,9,10,BTFLAM
		OBJSCL	154,94,71,3,10,11,BTFLAM
		OBJSCL	147,95,92,3,9,10,BTFLAM
		OBJSCL	140,95,113,4,10,10,BTFLAM
		
		OBJSCLM	140,96,177,5,10,9,BTFLAM
		OBJSCLM	147,95,156,4,9,9,BTFLAM
		OBJSCL	154,95,135,4,10,10,BTFLAM
		OBJSCL	147,95,156,4,9,9,BTFLAM
		OBJSCL	140,96,177,5,10,9,BTFLAM
		
OBJ_TURBO_NA0	
		OBJSCLM	139,97,63,15,11,12,BTFLAM
		OBJSCLM	147,97,33,12,9,13,BTFLAM
		OBJSCL	154,96,0,12,10,15,BTFLAM
		OBJSCL	147,97,33,12,9,13,BTFLAM
		OBJSCL	139,97,63,15,11,12,BTFLAM

		OBJSCLM	139,95,162,15,11,12,BTFLAM
		OBJSCLM	147,95,132,15,9,12,BTFLAM
		OBJSCL	154,94,99,15,10,13,BTFLAM
		OBJSCL	147,95,132,15,9,12,BTFLAM
		OBJSCL	139,95,162,15,11,12,BTFLAM

		OBJSCLM	139,96,206,16,11,9,BTFLAM
		OBJSCLM	147,95,233,5,9,10,BTFLAM
		OBJSCL	154,95,200,5,10,10,BTFLAM
		OBJSCL	147,95,233,5,9,10,BTFLAM
		OBJSCL	139,96,206,16,11,9,BTFLAM

OBJ_TURBO_NA1	
		OBJSCLM	139,97,75,15,11,12,BTFLAM
		OBJSCLM	147,97,43,12,9,13,BTFLAM
		OBJSCL	154,96,11,12,10,15,BTFLAM
		OBJSCL	147,97,43,12,9,13,BTFLAM
		OBJSCL	139,97,75,15,11,12,BTFLAM

		OBJSCLM	139,95,174,15,11,11,BTFLAM
		OBJSCLM	147,95,142,15,9,12,BTFLAM
		OBJSCL	154,94,110,15,10,13,BTFLAM
		OBJSCL	147,95,142,15,9,12,BTFLAM
		OBJSCL	139,95,174,15,11,11,BTFLAM

		OBJSCLM	139,96,218,16,11,9,BTFLAM
		OBJSCLM	147,95,186,18,9,10,BTFLAM
		OBJSCL	154,95,211,5,10,10,BTFLAM
		OBJSCL	147,95,186,18,9,10,BTFLAM
		OBJSCL	139,96,218,16,11,9,BTFLAM

OBJ_TURBO_NA2	
		OBJSCLM	139,97,87,15,11,12,BTFLAM
		OBJSCLM	147,97,53,15,9,13,BTFLAM
		OBJSCL	154,96,22,12,10,14,BTFLAM
		OBJSCL	147,97,53,15,9,13,BTFLAM
		OBJSCL	139,97,87,15,11,12,BTFLAM
		
		OBJSCLM	139,95,188,5,11,12,BTFLAM
		OBJSCLM	147,95,152,15,9,12,BTFLAM
		OBJSCL	154,94,121,15,10,13,BTFLAM
		OBJSCL	147,95,152,15,9,12,BTFLAM
		OBJSCL	139,95,188,5,11,12,BTFLAM
		
		OBJSCLM	139,96,230,16,11,9,BTFLAM
		OBJSCLM	147,95,196,18,9,10,BTFLAM
		OBJSCL	154,95,222,5,10,10,BTFLAM
		OBJSCL	147,95,196,18,9,10,BTFLAM
		OBJSCL	139,96,230,16,11,9,BTFLAM

; --------------------------------------------------------------------------

OBJ_DBLAST_LA	OBJPOSM	172,48,32,OBJ_DBLAST_AM
	      	OBJPOSM	166,47,32,OBJ_DBLAST_AM
	      	OBJPOS	127,46,OBJ_DBLAST_AM
	      	OBJPOS	132,46,OBJ_DBLAST_AM
	      	OBJPOS	139,46,OBJ_DBLAST_AM

	      	OBJPOSM	172,55,32,OBJ_DBLAST_AM
	      	OBJPOSM	167,55,32,OBJ_DBLAST_AM
		OBJPOS	126,54,OBJ_DBLAST_AM
	      	OBJPOS	133,54,OBJ_DBLAST_AM
	      	OBJPOS	140,54,OBJ_DBLAST_AM

	      	OBJPOSM	172,66,32,OBJ_DBLAST_AM
	      	OBJPOSM	166,66,32,OBJ_DBLAST_AM
	      	OBJPOS	127,65,OBJ_DBLAST_AM
	      	OBJPOS	134,65,OBJ_DBLAST_AM
	      	OBJPOS	141,65,OBJ_DBLAST_AM

OBJ_DBLAST_RA	OBJPOSM	139,46,32,OBJ_DBLAST_A
		OBJPOSM	132,46,32,OBJ_DBLAST_A
		OBJPOS	160,46,OBJ_DBLAST_A
	      	OBJPOS	166,47,OBJ_DBLAST_A
	      	OBJPOS	172,48,OBJ_DBLAST_A

	      	OBJPOSM	140,55,32,OBJ_DBLAST_A
	      	OBJPOSM	133,55,32,OBJ_DBLAST_A
		OBJPOS	160,54,OBJ_DBLAST_A
	      	OBJPOS	167,55,OBJ_DBLAST_A
	      	OBJPOS	172,55,OBJ_DBLAST_A

	      	OBJPOSM	141,65,32,OBJ_DBLAST_A
	      	OBJPOSM	134,65,32,OBJ_DBLAST_A
	      	OBJPOS	160,66,OBJ_DBLAST_A
	      	OBJPOS	166,66,OBJ_DBLAST_A
	      	OBJPOS	172,66,OBJ_DBLAST_A

		OBJCNTR	(129-3),(62-4),60,46

OBJ_DBLAST_LB	OBJPOSM	172,48,22,OBJ_DBLAST_BM
	      	OBJPOSM	166,47,22,OBJ_DBLAST_BM
	      	OBJPOS	127,46,OBJ_DBLAST_BM
	      	OBJPOS	132,46,OBJ_DBLAST_BM
	      	OBJPOS	139,46,OBJ_DBLAST_BM

	      	OBJPOSM	172,55,22,OBJ_DBLAST_BM
	      	OBJPOSM	167,55,22,OBJ_DBLAST_BM
		OBJPOS	126,54,OBJ_DBLAST_BM
	      	OBJPOS	133,54,OBJ_DBLAST_BM
	      	OBJPOS	140,54,OBJ_DBLAST_BM

	      	OBJPOSM	172,66,22,OBJ_DBLAST_BM
	      	OBJPOSM	166,66,22,OBJ_DBLAST_BM
	      	OBJPOS	127,65,OBJ_DBLAST_BM
	      	OBJPOS	134,65,OBJ_DBLAST_BM
	      	OBJPOS	141,65,OBJ_DBLAST_BM

OBJ_DBLAST_RB	OBJPOSM	139,46,22,OBJ_DBLAST_B
	      	OBJPOSM	132,46,22,OBJ_DBLAST_B
	      	OBJPOS	160,46,OBJ_DBLAST_B
	      	OBJPOS	166,47,OBJ_DBLAST_B
	      	OBJPOS	172,48,OBJ_DBLAST_B

	      	OBJPOSM	140,55,22,OBJ_DBLAST_B
	      	OBJPOSM	133,55,22,OBJ_DBLAST_B
		OBJPOS	160,54,OBJ_DBLAST_B
	      	OBJPOS	167,55,OBJ_DBLAST_B
	      	OBJPOS	172,55,OBJ_DBLAST_B

	      	OBJPOSM	141,65,22,OBJ_DBLAST_B
	      	OBJPOSM	134,65,22,OBJ_DBLAST_B
	      	OBJPOS	160,66,OBJ_DBLAST_B
	      	OBJPOS	166,66,OBJ_DBLAST_B
	      	OBJPOS	172,66,OBJ_DBLAST_B

OBJ_DBLAST_A	XYSIZE	32,22
		SCALER	23,0,32,22,BSHOOT
OBJ_DBLAST_AM	XYSIZE	32,22
		SCALERM	23,0,32,22,BSHOOT
OBJ_DBLAST_B	XYSIZE	22,18
		SCALER	0,0,22,18,BSHOOT
OBJ_DBLAST_BM	XYSIZE	22,18
		SCALER	0,0,22,18,BSHOOT
	
; --------------------------------------------------------------------------

		XYRATIO	100,180,100,180
		OBJCNTR	10,16,11,3
OBJ_DISC
		OBJSCL	9,15,0,19,11,6,BSHOOT

; --------------------------------------------------------------------------


