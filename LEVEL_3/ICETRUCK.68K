; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ BATMAN RETURNS CD-ROM						   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

; MAIN BOSS - LEVEL 3 - "Penguin ice truck"

; --------------------------------------------------------------------------

ICE_SETUP	LEA	OBJ_ICE,A6
		JSR	O_OBJECT_SETUP
		BNE.S	.end

		MOVE.L	#LOGIC_ICE,OBJ_LOGIC_DATA(A1)
		MOVE.W	#$50,OBJ_LMSPD(A1)
		MOVE.L	#17*3303,OBJ_LXMAX(A1)
		MOVE.L	#15*3303,OBJ_LXMIN(A1)
		MOVE.W	#16*$80,OBJ_LZMIN(A1)
		MOVE.W	#18*$80,OBJ_LZMAX(A1)

		MOVE.L	#DROP_DATA,OBJ_ADDR6(A1)

		MOVE.W	#SFX_EICETRUCK_A,OBJ_FX0_SFXNUM(A1)

		MOVEQ	#0,D0
.end		RTS

; --------------------------------------------------------------------------

ICE_CLEAR	
		MOVE.L	OBJ_ADDR2(A1),A2
		JSR	O_DEALL_S
		MOVE.L	OBJ_ADDR3(A1),A2
		JSR	O_DEALL_S
		MOVE.L	OBJ_ADDR4(A1),A2
		JSR	O_DEALL_S
		MOVE.L	OBJ_ADDR5(A1),A2
		JSR	O_DEALL_S
		JSR	O_CLEAR_S
		JSR	CLEAR_DAMAGE
		MOVE.L	OBJ_ADDR7(A1),D0
		BEQ.S	.skp
		MOVE.L	D0,A1
		CLR.B	OBJ_FLAG0(A1)
		JSR	DETECT_DEALL
.skp		
		JSR	SFX3D_KILL
		JSR	SCADD_MAINBOSS
		JMP	ECL_KILL

; --------------------------------------------------------------------------

ICE_MOVE	BTST	#O_CLEAR,OBJ_FLAG0(A1)
		BNE.S	ICE_CLEAR
		CLR.W	OBJ_XFORCE(A1)
		CLR.L	OBJ_XINERTIA(A1)
		
		JSR	LOGIC_CTRL
		JSR	SFX3D_SPEED
		JSR	DAMAGE_CTRL
		JSR	EDAMAGE_METER
		
		BSR	TARGET_MOVE

		MOVE.W	#-1,D2
		MOVE.W	OBJ_CNT7(A1),D0
		BPL.S	.aspin
		MOVE.W	#2,D2
		MOVE.W	OBJ_CNT6(A1),D1
		ADD.W	#$30,D1
		ADD.W	D1,D0
		BMI.S	.pset
		MOVEQ	#0,D0
		ASR.W	D1
		CMP.W	#$10,D1
		BMI.S	.pset
		SUBQ.W	#1,D0
		NEG.W	D1
.pset		MOVE.W	D1,OBJ_CNT6(A1)
		MOVE.W	D0,OBJ_CNT7(A1)

.aspin		MOVE.W	OBJ_CNT4(A1),D0
		MOVE.W	OBJ_CNT5(A1),D1
		ADD.W	D2,D0
		BPL.S	.achk
		MOVE.W	#0,D0
		ADDQ.W	#2,D1
		BRA.S	.aset
.achk		CMP.W	#8,D0
		BMI.S	.aset
		MOVE.W	#8,D0
.aset		ADD.W	D0,D1
		MOVE.W	D0,OBJ_CNT4(A1)
		MOVE.W	D1,OBJ_CNT5(A1)

		MOVE.B	OBJ_CNT3(A1),D0
		ADD.B	OBJ_CNT3+1(A1),D0
		BPL.S	.tchk
		MOVEQ	#0,D0
.tchk		CMP.B	#8,D0
		BMI.S	.tset
		MOVE.B	#8,D0
.tset		MOVE.B	D0,OBJ_CNT3(A1)	

		MOVE.W	OBJ_CNT7(A1),D0
		OR.B	OBJ_CNT3(A1),D0
		BNE.S	.skp
		BSR	TARGET_SET
.skp
		BTST	#O_DEAD,OBJ_FLAG0(A1)
		BEQ.S	.a_chk
		CLR.B	OBJ_FLAG1(A1)
		CLR.B	OBJ_FLAG2(A1)
		CLR.B	OBJ_FLAG3(A1)
		RTS

.a_chk		TST.B	OBJ_FLAG1(A1)
		BEQ.S	.b_chk
      		SUBQ.B	#1,OBJ_FLAG1(A1)
		BNE.S	.b_chk
		BSR	BLK_SET
		SUB.L	#108*$100,OBJ_XPOS(A2)
.b_chk		TST.B	OBJ_FLAG2(A1)
		BEQ.S	.c_chk
      		SUBQ.B	#1,OBJ_FLAG2(A1)
		BNE.S	.c_chk
		BSR	BLK_SET
.c_chk		TST.B	OBJ_FLAG3(A1)
		BEQ.S	.abc_done
      		SUBQ.B	#1,OBJ_FLAG3(A1)
		BNE.S	.abc_done
		BSR	BLK_SET
		ADD.L	#108*$100,OBJ_XPOS(A2)
.abc_done
		CMP.W	#$800,OBJ_ZPOS(A1)
		BPL.S	.end
		SUBQ.W	#1,OBJ_CNT0(A1)
		BPL.S	.end
		MOVE.L	OBJ_ADDR6(A1),A0
.lrep		MOVE.W	(A0)+,D0
		BPL.S	.lchk		
		LEA	DROP_DATA,A0
		BRA.S	.lrep
.lchk		MOVE.W	(A0)+,D1
	 	MOVE.B	#14,(A1,D1.W)
		TST.W	D0
		BEQ.S	.lrep
		CMP.W	#$7000,OBJ_DTOTAL(A1)
		BCS.S	.lset
		SUB.W	#6,D0
		CMP.W	#$A000,OBJ_DTOTAL(A1)
		BCS.S	.lset
		SUB.W	#6,D0
		CMP.W	#$D000,OBJ_DTOTAL(A1)
		BCS.S	.lset
		SUB.W	#6,D0
.lset		MOVE.W	D0,OBJ_CNT0(A1)
		MOVE.L	A0,OBJ_ADDR6(A1)

.end		RTS

; --------------------------------------------------------------------------

DROP_DATA	
		DC.W	30,OBJ_FLAG1
		DC.W	30,OBJ_FLAG2
		DC.W	30,OBJ_FLAG3
		
		DC.W	0,OBJ_FLAG1
		DC.W	30,OBJ_FLAG3

		DC.W	0,OBJ_FLAG1
		DC.W	0,OBJ_FLAG2
		DC.W	30,OBJ_FLAG3

		DC.W	0,OBJ_FLAG1
		DC.W	30,OBJ_FLAG2

		DC.W	0,OBJ_FLAG2
		DC.W	30,OBJ_FLAG3

		DC.W	-1

; --------------------------------------------------------------------------

ICE_PRINT     	JSR	O_CAR_PRINT_S
		MOVE.W	OBJ_FLASH0(A1),-(SP)
		CLR.W	OBJ_FLASH0(A1)
		BSR	PRN_POLE

		LEA	OBJ_ADDR4(A1),A4
		MOVE.B	OBJ_CNT3(A1),D1
		LSR.B	D1
		BEQ.S	.skp
		LEA	OBJ_ICE_TAR2A,A0
		LEA	OBJ_ICE_TAR2B,A2
		TST.W	OBJ_CNT2(A1)
		BEQ.S	.chkab
		LEA	OBJ_ICE_TAR1A,A0
		LEA	OBJ_ICE_TAR1B,A2
		BMI.S	.chkab
		LEA	OBJ_ICE_TAR3A,A0
		LEA	OBJ_ICE_TAR3B,A2
.chkab		CMP.B	#3,D1
		BPL.S	.do
		MOVE.L	A2,A0
.do		JSR	O_CPS_ENTC
.skp		

.prn_a		MOVE.B	OBJ_FLAG1(A1),D1
		AND.B	#2,D1
		BEQ.S	.prn_b
		LEA	OBJ_ADDR5(A1),A4
		LEA	OBJ_ICE_STRT1,A0
		JSR	O_CPS_ENTC
.prn_b		MOVE.B	OBJ_FLAG2(A1),D1
		AND.B	#2,D1
		BEQ.S	.prn_c
		LEA	OBJ_ADDR5(A1),A4
		LEA	OBJ_ICE_STRT2,A0
		JSR	O_CPS_ENTC
.prn_c		MOVE.B	OBJ_FLAG3(A1),D1
		AND.B	#2,D1
		BEQ.S	.abc_done
		LEA	OBJ_ADDR5(A1),A4
		LEA	OBJ_ICE_STRT3,A0
		JSR	O_CPS_ENTC

.abc_done	MOVE.W	(SP)+,OBJ_FLASH0(A1)
		JMP	SHOW_DAMAGE

PRN_POLE	JSR	O_PR_ALLOCX_XIT
		MOVE.L	OBJ_ADDR2(A1),A2
		LEA	OBJ_ICE_POLE,A0
		JSR	O_CALC_PATCHZ
		JSR	O_CALC_VIEW
		JSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR2(A1)

		MOVE.W	D5,D1
		MULS	RCYFRACT(A5),D1
		ADD.L	RCYPOS(A5),D1
		ADD.L	OBJ_YPOS(A1),D1
		SUB.L	#100*$100,D1
		DIVS	D0,D1
		ADD.W	#97,D1
		MOVE.W	RCYMAX(A5),-(SP)
		MOVE.W	D1,RCYMAX(A5)
		MOVE.L	OBJ_XPOS(A1),D6
		MOVE.W	OBJ_CNT7(A1),D7
		EXT.L	D7
		ASL.L	#4,D7
		ADD.L	OBJ_YPOS(A1),D7
		JSR	O_PR_SET0_XY
		MOVE.W	(SP)+,RCYMAX(A5)

		JSR	O_PR_ALLOCX_XIT
		
		SUBQ.W	#1,D0
		LEA	ICE_GR_TAB,A0
		TST.W	OBJ_CNT4(A1)
		BEQ.S	.skp
		LEA	16(A0),A0
.skp		MOVE.W	OBJ_CNT5(A1),D1
		LSR.W	D1
		AND.W	#%1100,D1
		MOVE.L	(A0,D1.W),A0

		MOVE.L	OBJ_ADDR3(A1),A2
		JSR	O_CALC_VIEW
		JSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR3(A1)

		JMP	O_PR_SET0_XY

; --------------------------------------------------------------------------

TARGET_SET	MOVE.L	A1,A2
		JSR	OBJECT_ALLOC
		BNE.S	.end
		LEA	TSET_DATA,A5
		JSR	O_OSET	
		MOVE.L	#TARGET_HIT,OBJ_ADDR7(A1)
.lp		MOVE.W	#2,D7
		JSR	RANDOM_RANGE_S
		CMP.W	OBJ_CNT2(A2),D7
		BEQ.S	.lp
		MOVE.W	D7,OBJ_CNT2(A2)
		MULS	#108*$100,D7
		ADD.L	OBJ_XPOS(A2),D7
		MOVE.L	D7,OBJ_XPOS(A1)
		MOVE.L	OBJ_YPOS(A2),OBJ_YPOS(A1)
		MOVE.W	OBJ_ZPOS(A2),D0
		SUB.W	#4,D0
		MOVE.W	D0,OBJ_ZPOS(A1)
		MOVE.W	#65,D0
		CMP.W	#$A000,OBJ_DTOTAL(A2)
		BCS.S	.set
		MOVE.W	#55,D0
		CMP.W	#$C000,OBJ_DTOTAL(A2)
		BCS.S	.set
		MOVE.W	#45,D0
.set		MOVE.W	D0,OBJ_CNT0(A1)	
		MOVE.L	A2,OBJ_ADDR6(A1)
		MOVE.L	A1,OBJ_ADDR7(A2)
		MOVE.B	#1,OBJ_CNT3+1(A2)
.end		EXG	A1,A2
		RTS

TARGET_HIT	JSR	DETECT_DEALL
		CLR.B	OBJ_FLAG0(A1)
		MOVE.L	OBJ_ADDR6(A1),A1
		CLR.L	OBJ_ADDR7(A1)
		MOVE.W	#-$1E0,OBJ_CNT6(A1)		
		MOVE.W	#-$1E0,OBJ_CNT7(A1)		
		MOVE.B	#-2,OBJ_CNT3+1(A1)

		MOVE.W	#SFX_TARGETHIT_A,D0
		JSR	CSFX_ONESHOT_V

		ADDQ.B	#6,OBJ_FLASH1(A1)
		ADD.W	#$1748,OBJ_DTOTAL(A1)
		BCC.S	.end
		MOVE.W	#$FFFF,OBJ_DTOTAL(A1)
.end		RTS

TARGET_MOVE	MOVE.L	OBJ_ADDR7(A1),D0
		BEQ.S	.end
		MOVE.L	D0,A2
		MOVE.W	OBJ_ZPOS(A1),D0
		SUB.W	#4,D0
		MOVE.W	D0,OBJ_ZPOS(A2)
.end		RTS

TARGET_CTRL	SUBQ.W	#1,OBJ_CNT0(A1)
		BPL.S	.end
		JSR	DETECT_DEALL
		CLR.B	OBJ_FLAG0(A1)
		MOVE.L	OBJ_ADDR6(A1),A1
		CLR.L	OBJ_ADDR7(A1)
		MOVE.B	#-1,OBJ_CNT3+1(A1)
.end		RTS

TSET_DATA	DC.W	ID_DETECTAREA	;OBJ_ID
		DC.L	TARGET_CTRL	;OBJ_MOVE_CTRL
		DC.L	SIGN_NEXT	;OBJ_PRN_CTRL
		DC.W	4		;OBJ_ZSIZE
		XYRATIO	178,370,100,200
		XYSIZE	6,20
		DC.W	0		;OBJ_MASS
		DC.L	O_T2M_HIT	;OBJ_HIT_GOTO

; --------------------------------------------------------------------------

BLK_SET		MOVE.L	A1,A2
		LEA	OBJ_BLK,A6
		JSR	O_OBJECT_SETUP
		EXG	A1,A2
		BNE.S	.end
		MOVE.L	OBJ_XPOS(A1),OBJ_XPOS(A2)
		MOVE.L	OBJ_YPOS(A1),D0
		SUB.L	#40*$100,D0
		MOVE.L	D0,OBJ_YPOS(A2)
		MOVE.W	OBJ_ZPOS(A1),D0
		SUB.W	OBJ_ZSIZE(A2),D0
		SUB.W	OBJ_ZSIZE(A2),D0
		MOVE.W	D0,OBJ_ZPOS(A2)
		MOVE.W	OBJ_SPD(A1),D0
		SUB.W	#8,D0
		MOVE.W	D0,OBJ_SPD(A2)
		MOVE.L	#-$400,OBJ_CNT0(A2)

		MOVE.W	#SFX_ICEDROP_A,D0
		JSR	CSFX_ONESHOT_V

.end		RTS

BLK_HIT		CMP.W	#ID_MAIN,OBJ_ID(A1)
		BEQ.S	.hit
		CMP.W	#ID_SHOT,OBJ_ID(A1)
		BEQ.S	.shot
		CMP.W	#ID_MISS,OBJ_ID(A1)
		BEQ.S	.miss
		JMP	OBJ_DET_NEXT
.shot		ADD.W	#2,OBJ_SHOT_AMT(A2)
		JMP	OBJ_DET_ZXKILL
.miss		ADD.W	#14,OBJ_SHOT_AMT(A2)
		ADD.B	#6,OBJ_FLASH1(A2)
		JMP	OBJ_DET_ZXKILL

.hit		MOVEM.L	D0-D7/A0-A6,-(SP)
		EXG	A2,A1
		ADDQ.B	#2,OBJ_FLASH1(A2)
		ADD.W	#6,OBJ_SHOT_AMT(A2)
		MOVE.W	OBJ_SPD(A2),D3
		ASR.W	D3
		MOVE.W	D3,D4
		ASR.W	D3
		ADD.W	D4,D3
		MOVE.W	D3,OBJ_SPD(A1)
		MOVE.L	#-20*$100,OBJ_ADDR7(A2)
		SUB.W	#$60,OBJ_SPD(A2)
		LEA	VIS_BLKHIT_CTRL,A2
		JSR	KILL_V1
		JSR	O_CLEAR_S
		MOVEM.L	(SP)+,D0-D7/A0-A6
		JMP	OBJ_DET_NEXT


BLK_MOVE	JSR	O_RANGE_CHK 
		BRA.L	.clr
		BRA.L	.clr

		MOVE.W	OBJ_SHOT_AMT(A1),D0
		BEQ.S	.move
		ADDQ.B	#2,OBJ_FLASH1(A1)
		CLR.W	OBJ_SHOT_AMT(A1)
		ADD.W	D0,OBJ_DTOTAL(A1)
		CMP.W	#5,OBJ_DTOTAL(A1)
		BMI.S	.move
		LEA	VIS_BLKHIT_CTRL,A2
		JSR	KILL_V1
		JSR	O_CLEAR_S
		JMP	SCADD_SMALLOBJ

.move		MOVEQ	#0,D6
		JSR	O_SPEED_UPDATE
		JSR	O_XIN_UPDATE
		MOVE.L	D0,D1
		ASR.L	#8,D1
		MOVE.W	OBJ_SPD(A1),D0
		ADD.W	D0,OBJ_ZPOS(A1)

		MOVE.L	OBJ_CNT0(A1),D1
		MOVE.L	OBJ_YPOS(A1),D0
		BNE.S	.air_time

		MOVE.L	D1,D2
		ADD.L	#570,D2
		BPL	.air_skp
		ASL.L	#8,D1
		DIVS	#400,D1
		EXT.L	D1

.air_time	ADD.L	#620,D1
		ADD.L	D1,D0
		BMI.S	.land_skp
		CMP.L	#$300,D1
		BMI.S	.land_done

;		CMP.L	#$500,D1
;		BMI.S	.sfx_skp
;		MOVE.W	#SFX_BMLAND_A,D0
;		JSR	CSFX_ONESHOT
.sfx_skp
		MOVEM.L	D1/A1,-(SP)
		MOVE.W	OBJ_ZPOS(A1),D0
		ADD.W	OBJ_ZSIZE(A1),D0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVE.L	OBJ_YPOS(A1),D2
		MOVE.W	OBJ_SPD(A1),D3
		MOVEM.L	D0-D3,-(SP)
		LEA	VIS_BLKSMOKE_L,A0
		ADD.L	#-20*$100,D1
		JSR	VIS_SET
		MOVEM.L	(SP)+,D0-D3
		LEA	VIS_BLKSMOKE_R,A0
		ADD.L	#20*$100,D1
		JSR	VIS_SET
		MOVEM.L	(SP)+,D1/A1

.land_done	MOVEQ	#0,D0
		NEG.L	D1
.land_skp	MOVE.L	D0,OBJ_YPOS(A1)
.air_skp	MOVE.L	D1,OBJ_CNT0(A1)
.end		RTS
.clr		JMP	O_CLEAR_S

VIS_BLKSMOKE_L	DC.W	$0000,$400,$0000
		DC.W	-$4,-$400,-$300
		DC.W	$0100,$0B00
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0X
		DC.L	VIS_SMOKE_GRS2
		DC.W	0			;SFX

VIS_BLKSMOKE_R	DC.W	$0000,$400,$0000
		DC.W	-$4,$400,-$300
		DC.W	$0100,$0B00
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0X
		DC.L	VIS_SMOKE_GRS2
		DC.W	0			;SFX

VIS_BLKHIT_CTRL	DC.W	1-1
		DC.L	VIS_BLK_HIT
		DC.W	10,10-1,1,1
		DC.L	VIS_ICEPIECE1

VIS_ICEPIECE1	DC.W	0,0,0
		DC.W	0,0,0
		DC.W	$0000,$0000
		DC.L	VIS_RTOBJ_MOVER
		DC.L	VIS_PRN_ROTATEX
		DC.L	VIS_ICEBIT1_GRS
		DC.W	0			;SFX
		DC.W	$2C0,$140		;Lie angles
		DC.W	$2C0+$C0,$2C0-$C0	;Lie range.
		DC.W	600,3			;Ydecay/Zdecay.
		DC.W	$40,$C00		;Spin/Xrand.
		DC.W	$E00			;Height range.
		DC.L	0;ICEHITGR_SFX

ICEHITGR_SFX	DC.W	4
		DC.W	SFX_ICEGND_A
		DC.W	SFX_ICEGND_B
		DC.W	SFX_ICEGND_C
		DC.W	SFX_ICEGND_D

VIS_ICEBIT1_GRS
		XYRATIO	100,90,100,90
		OBJCNTR	0,0,0,0

		OBJSCL	0,0,45,44,27,24,ICEBIT

VIS_BLK_HIT	DC.W	$0000,$0000,$0000
		DC.W	0,0,-$600
		DC.W	$100,$0600
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0X
		DC.L	VIS_BLKHIT_GRS
		DC.W	SFX_ICEBREAK_A	;SFX

VIS_BLKHIT_GRS
		XYRATIO	100,170,100,220
		OBJCNTR	0,0,30,22
		OBJSCL	-7,-4,0,0,44,27,ICBREK
		OBJSCL	-16,-14,44,0,65,38,ICBREK
		OBJSCL	-28,-22,0,39,91,46,ICBREK
		XYRATIO	100,190,100,240
		OBJCNTR	0,0,30,22
		OBJSCL	-28,-22,0,39,91,46,ICBREK
		OBJSCL	-28,-22,0,39,91,46,ICBREK
		XYRATIO	100,210,100,260
		OBJCNTR	0,0,30,22
		OBJSCL	-28,-22,0,39,91,46,ICBREK

OBJ_BLK
		XYRATIO	100,170,100,210
		OBJCNTR	0,0,30,22

		DC.L	OBJ_BLK_SET
		DC.L	OBJ_BLK_ANM0
		DC.L	OBJ_BLK_ANM0

OBJ_BLK_ANM0	DC.L	OBJ_BLK_XVIEW,OBJ_BLK_YVIEW

		OBJSCLM	2,-1,0,0,39,24,ICBLK3
		OBJSCLM	1,-1,0,0,36,24,ICBLK2
		OBJSCL	0,0,0,0,30,22,ICBLK1
		OBJSCL	1,-1,0,0,36,24,ICBLK2
		OBJSCL	2,-1,0,0,39,24,ICBLK3

OBJ_BLK_XVIEW	DC.W	2*$20,$20
		DC.L	$3000,$7800,-1
OBJ_BLK_YVIEW	DC.W	5*$20
		DC.W	-1

OBJ_BLK_SET	DC.W	ID_BOTTLE	;OBJ_ID
		DC.L	BLK_MOVE	;OBJ_MOVE_CTRL
		DC.L	O_CAR_PRINT_SNX	;OBJ_PRN_CTRL
		DC.W	$20		;OBJ_ZSIZE
		XYSIZE	30,22
		DC.W	$A0A0		;OBJ_MASS
		DC.L	BLK_HIT		;OBJ_HIT_GOTO
		DC.W	0		;OBJ_ZPOS
		DC.L	0		;OBJ_XPOS
		DC.L	0		;OBJ_YPOS
		DC.W	$A0		;OBJ_SPD_MAXA
		DC.W	$A0		;OBJ_SPD_MAXB
		DC.W	40		;OBJ_SPD_INCFA
		DC.W	40		;OBJ_SPD_INCFB
		DC.W	40		;OBJ_SPD_DECF
		DC.W	0		;OBJ_SPD_BRAKE
		DC.W	$0C0C		;OBJ_TRACTION
		
; --------------------------------------------------------------------------

OBJ_ICE
		XYRATIO	178,370,100,200
		OBJCNTR	72,3,178,106

		DC.L	OBJ_ICE_SET
		DC.L	OBJ_ICE_ANM0
		DC.L	OBJ_ICE_ANM1
		DC.L	OBJ_ICE_SKID
		DC.L	OBJ_ICE_DAM

OBJ_ICE_ANM0	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW

		OBJSCLM	72,3,0,0,178,106,ICETK2
		OBJSCL	72,2,0,0,178,110,ICETK1
		OBJSCL	72,3,0,0,178,106,ICETK2
		
OBJ_ICE_ANM1	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW

		OBJSCLM	72,3,0,112,178,106,ICETK2
		OBJSCL	72,2,0,112,178,110,ICETK1
		OBJSCL	72,3,0,112,178,106,ICETK2


OBJ_ICE_TAR1A	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW

		OBJSCLM	202,91,0,11,22,19,TARGET
		OBJSCL	97,88,23,12,21,19,TARGET
		OBJSCL	95,89,23,11,21,20,TARGET

OBJ_ICE_TAR1B	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW

		OBJSCLM	202,91,0,0,20,9,TARGET
		OBJSCL	97,88,21,1,22,8,TARGET
		OBJSCL	95,89,21,0,22,9,TARGET

OBJ_ICE_TAR2A	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW

		OBJSCLM	147,89,23,11,21,20,TARGET
		OBJSCL	150,88,0,11,22,19,TARGET
		OBJSCL	147,89,23,11,21,20,TARGET

OBJ_ICE_TAR2B	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW

		OBJSCLM	147,89,21,0,22,9,TARGET
		OBJSCL	150,88,0,0,20,9,TARGET
		OBJSCL	147,89,21,0,22,9,TARGET

OBJ_ICE_TAR3A	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW

		OBJSCLM	95,89,23,11,21,20,TARGET
		OBJSCLM	97,88,23,12,21,19,TARGET
		OBJSCL	202,91,0,11,22,19,TARGET

OBJ_ICE_TAR3B	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW

		OBJSCLM	95,89,21,0,22,9,TARGET
		OBJSCLM	97,88,21,1,22,8,TARGET
		OBJSCL	202,91,0,0,20,9,TARGET

OBJ_ICE_STRT1	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW
		
		OBJSCLM	198,68,0,0,29,18,ICSTRT
		OBJSCL	94,66,0,0,29,18,ICSTRT
		OBJSCL	93,66,0,0,29,18,ICSTRT

OBJ_ICE_STRT2	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW
		
		OBJSCLM	142,67,0,0,29,18,ICSTRT
		OBJSCL	147,66,0,0,29,18,ICSTRT
		OBJSCL	144,67,0,0,29,18,ICSTRT

OBJ_ICE_STRT3	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW
		
		OBJSCLM	90,67,0,0,29,18,ICSTRT
		OBJSCL	202,66,0,0,29,18,ICSTRT
		OBJSCL	199,68,0,0,29,18,ICSTRT


		XYRATIO	100,130,100,130
		OBJCNTR	0,60,13,40

OBJ_ICE_POLE	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW
		
		OBJSCL	-8,0,0,0,13,160,ICPOLE
		OBJSCL	0,-2,0,0,13,160,ICPOLE
		OBJSCL	8,0,0,0,13,160,ICPOLE

ICE_GR_TAB	DC.L	OBJ_ICE_PENG1
		DC.L	OBJ_ICE_PENG2
		DC.L	OBJ_ICE_PENG3
		DC.L	OBJ_ICE_PENG2
		DC.L	OBJ_ICE_PSPIN1
		DC.L	OBJ_ICE_PSPIN2
		DC.L	OBJ_ICE_PSPIN3
		DC.L	OBJ_ICE_PSPIN4

OBJ_ICE_PENG1	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW
		
		OBJSCLM	-9,-31,46,0,47,36,PENTMP
		OBJSCLM	-17,-33,46,0, 47,36,PENTMP
		OBJSCLM	-25,-31,46,0,47,36,PENTMP

OBJ_ICE_PENG2	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW
		
		OBJSCL	-24,-31,0,0,45,34,PENTMP
		OBJSCL	-16,-33,0,0,45,34,PENTMP
		OBJSCL	-8,-31,0,0,45,34,PENTMP

OBJ_ICE_PENG3	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW
		
		OBJSCL	-25,-31,46,0,47,36,PENTMP
		OBJSCL	-17,-33,46,0, 47,36,PENTMP
		OBJSCL	-9,-31,46,0,47,36,PENTMP

OBJ_ICE_PSPIN1	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW
		
		OBJSCL	-39,-29,0,0,75,34,PNSPIN
		OBJSCL	-31,-31,0,0,75,34,PNSPIN
		OBJSCL	-23,-29,0,0,75,34,PNSPIN

OBJ_ICE_PSPIN2	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW
		
		OBJSCL	-34,-32,76,0,85,36,PNSPIN
		OBJSCL	-26,-34,76,0,85,36,PNSPIN
		OBJSCL	-18,-32,76,0,85,36,PNSPIN

OBJ_ICE_PSPIN3	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW
		
		OBJSCL	-46,-35,0,37,80,39,PNSPIN
		OBJSCL	-38,-37,0,37,80,39,PNSPIN
		OBJSCL	-30,-35,0,37,80,39,PNSPIN

OBJ_ICE_PSPIN4	DC.L	OBJ_ICE_XVIEW,OBJ_ICE_YVIEW
		
		OBJSCL	-50,-32,81,37,86,37,PNSPIN
		OBJSCL	-42,-34,81,37,86,37,PNSPIN
		OBJSCL	-34,-32,81,37,86,37,PNSPIN


		XYRATIO	178,370,100,200
		OBJCNTR	72,3,178,106

OBJ_ICE_XVIEW	DC.W	1*$20,$20
		DC.L	$4000,-1
OBJ_ICE_YVIEW	DC.W	5*$20
		DC.W	-1

OBJ_ICE_SKID	DC.W	4
		XSIZE	84
		XSIZE	28
		XSIZE	-28
		XSIZE	-84

OBJ_ICE_SET	DC.W	ID_CAR_NONHIT	;OBJ_ID
		DC.L	ICE_MOVE	;OBJ_MOVE_CTRL
		DC.L	ICE_PRINT	;OBJ_PRN_CTRL
		DC.W	$50		;OBJ_ZSIZE
		XYSIZE	178,60
		DC.W	$7070		;OBJ_MASS
		DC.L	HROUTINE_CAR	;OBJ_HIT_GOTO
		DC.W	$80*40		;OBJ_ZPOS
		DC.L	$100*200	;OBJ_XPOS
		DC.L	$100*0		;OBJ_YPOS
		DC.W	$48		;OBJ_SPD_MAXA
		DC.W	$58		;OBJ_SPD_MAXB
		DC.W	20		;OBJ_SPD_INCFA
		DC.W	20		;OBJ_SPD_INCFB
		DC.W	10		;OBJ_SPD_DECF
		DC.W	5		;OBJ_SPD_BRAKE
		DC.W	$0101		;OBJ_TRACTION

OBJ_ICE_DAM	DC.W	0,0
		DC.W	1*$FFFF/7
		DC.L	OBJ_ICE_DPL,DAM_SET0
		DC.W	2*$FFFF/7
		DC.L	OBJ_ICE_DPL,DAM_SET0
		DC.W	3*$FFFF/7
		DC.L	OBJ_ICE_DPL,DAM_SET0
		DC.W	4*$FFFF/7
		DC.L	OBJ_ICE_DPL,DAM_SET0
		DC.W	5*$FFFF/7
		DC.L	OBJ_ICE_DPL,DAM_SET0
		DC.W	6*$FFFF/7
		DC.L	OBJ_ICE_DPL,DAM_SET0

		DC.W	$FFFF
		DC.L	OBJ_ICE_KILL,KILL_SET2

OBJ_ICE_KILL	DC.W	$0028,5-1
		DC.L	VIS_EXPLD_C1
		DC.L	VIS_EXPLD_C2
		DC.L	VIS_EXPLD_C3
		DC.L	VIS_EXPLD_C4
		DC.L	VIS_EXPLD_C5
		DC.W	40,40-1,1,6
		DC.L	VIS_DOOR
		DC.L	VIS_PIPE
		DC.L	VIS_BUMP
		DC.L	VIS_WHEL
		DC.L	VIS_PIECE1
		DC.L	VIS_PIECE2

OBJ_ICE_DPL	DC.W	6
		DC.L	OBJ_ICE_DP1
		DC.L	OBJ_ICE_DP2
		DC.L	OBJ_ICE_DP3
		DC.L	OBJ_ICE_DP4
		DC.L	OBJ_ICE_DP5
		DC.L	OBJ_ICE_DP6

OBJ_ICE_DP1	DC.L	VIS_FLAME1M,VIS_EXPCTRL_L
		DC.L	OBJ_IDAM_XVIEW,OBJ_IDAM_YVIEW
		DAMPTS	$11,96,31,$11,101,32,0,0,0
		
OBJ_ICE_DP2	DC.L	VIS_FLAME0M,VIS_EXPCTRL_B
		DC.L	OBJ_IDAM_XVIEW,OBJ_IDAM_YVIEW
		DAMPTS	$11,125,87,$11,122,87,0,0,0
		
OBJ_ICE_DP3	DC.L	VIS_FLAME0,VIS_EXPCTRL_B
		DC.L	OBJ_IDAM_XVIEW,OBJ_IDAM_YVIEW
		DAMPTS	$11,196,63,$11,195,63,0,0,0
		
OBJ_ICE_DP4	DC.L	VIS_FLAME1,VIS_EXPCTRL_R
		DC.L	OBJ_IDAM_XVIEW,OBJ_IDAM_YVIEW
		DAMPTS	$11,206,5,$11,211,4,0,0,0
		
OBJ_ICE_DP5	DC.L	VIS_FLAME1,VIS_EXPCTRL_B
		DC.L	OBJ_IDAM_XVIEW,OBJ_IDAM_YVIEW
		DAMPTS	$11,212,41,$11,216,41,0,0,0
		
OBJ_ICE_DP6	DC.L	VIS_FLAME1M,VIS_EXPCTRL_B
		DC.L	OBJ_IDAM_XVIEW,OBJ_IDAM_YVIEW
		DAMPTSM	$11,212,41,$11,216,41,0,0,0
		
OBJ_IDAM_XVIEW	DC.W	2*10,10
		DC.L	$4000,-1
OBJ_IDAM_YVIEW	DC.W	0
		DC.W	-1

; --------------------------------------------------------------------------

LOGIC_ICE

	LOGICEND
	EVEN

; --------------------------------------------------------------------------
