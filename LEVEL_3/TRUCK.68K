; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ BATMAN RETURNS CD-ROM						   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

; MID BOSS - LEVEL 3 - "Gastank truck"

; --------------------------------------------------------------------------

TRUCK_SETUP	LEA	OBJ_TRUCK,A6
		JSR	O_OBJECT_SETUP
		BNE.S	.end

		MOVE.L	#LOGIC_TRUCK,OBJ_LOGIC_DATA(A1)
		MOVE.W	#$90,OBJ_LMSPD(A1)	;WAS 70
		MOVE.L	#17*3303,OBJ_LXMAX(A1)
		MOVE.L	#15*3303,OBJ_LXMIN(A1)
		MOVE.W	#16*$80,OBJ_LZMIN(A1)
		MOVE.W	#18*$80,OBJ_LZMAX(A1)

		MOVE.W	#SFX_EPCVAN_A,OBJ_FX0_SFXNUM(A1)

		MOVEQ	#0,D0
.end		RTS

; --------------------------------------------------------------------------

TRUCK_CLEAR 	MOVE.L	OBJ_ADDR2(A1),A2
		JSR	O_DEALL_S
		JSR	O_CLEAR_S
		JSR	CLEAR_DAMAGE
		JSR	SFX3D_KILL
		JSR	SCADD_MIDBOSS
		JMP	ECL_KILL

; --------------------------------------------------------------------------

TRUCK_MOVE	BTST	#O_CLEAR,OBJ_FLAG0(A1)
		BNE.S	TRUCK_CLEAR
		
		JSR	LOGIC_CTRL
		JSR	SFX3D_SPEED
		JSR	DAMAGE_CTRL
		JSR	EDAMAGE_METER

		TST.W	OBJ_CNT0(A1)
		BEQ.S	.fchk
		SUB.W	#1,OBJ_CNT0(A1)
		BNE.S	.end
		BSR	TTANK_SET

		MOVE.W	#SFX_PCDROP_A,D0
		JMP	CSFX_ONESHOT_V

.fchk		
		BTST	#O_DEAD,OBJ_FLAG0(A1)
		BNE.S	.end

		MOVE.W	#SFX_TRUCKHORN_B,D0
		JSR	HORN_SFX

		CMP.W	#$800,OBJ_ZPOS(A1)
		BPL.S	.end
		TST.W	OBJ_CNT1(A1)
		BEQ.S	.ftchk
		SUBQ.W	#1,OBJ_CNT1(A1)
		BRA.S	.fskp
.ftchk		SUBQ.W	#1,OBJ_CNT2(A1)
		BPL.S	.end
		MOVEQ	#25,D0
		MOVEQ	#0,D1
		CMP.W	#$6000,OBJ_DTOTAL(A1)
		BCS.S	.fset
		MOVEQ	#18,D0
		MOVEQ	#1,D1
		CMP.W	#$A000,OBJ_DTOTAL(A1)
		BCS.S	.fset
		MOVEQ	#14,D0
		MOVEQ	#2,D1
		CMP.W	#$C000,OBJ_DTOTAL(A1)
		BCS.S	.fset
		MOVEQ	#8,D0
		MOVEQ	#3,D1
.fset		MOVE.W	D0,OBJ_CNT2(A1)
		MOVE.W	D1,OBJ_CNT1(A1)
.fskp		MOVE.W	#10,OBJ_CNT0(A1)

.end		RTS

; --------------------------------------------------------------------------

TRUCK_PRINT	JSR	O_CAR_PRINT_S
		SUBQ.W	#1,D0
		
		MOVE.W	OBJ_CNT0(A1),D1
		BEQ.S	.skp
		LEA	OBJ_TRUCK_TTA,A0
		AND.W	#2,D1
		BEQ.S	.tprn
		LEA	OBJ_TRUCK_TTB,A0
.tprn		LEA	OBJ_ADDR2(A1),A4
		MOVE.W	OBJ_FLASH0(A1),-(SP)
		CLR.W	OBJ_FLASH0(A1)
		JSR	O_CPS_ENTC
		MOVE.W	(SP)+,OBJ_FLASH0(A1)
.skp		
		JMP	SHOW_DAMAGE

; --------------------------------------------------------------------------

TTANK_SET	MOVE.L	A1,A2
		LEA	OBJ_TTANK,A6
		JSR	O_OBJECT_SETUP
		EXG	A1,A2
		BNE.S	.end
		MOVE.L	A1,OBJ_ADDR3(A2)
		MOVE.L	OBJ_XPOS(A1),OBJ_XPOS(A2)
		MOVE.L	OBJ_YPOS(A1),D0
		SUB.L	#34*$100,D0
		MOVE.L	D0,OBJ_YPOS(A2)
		MOVE.W	OBJ_ZPOS(A1),D0
		SUB.W	OBJ_ZSIZE(A2),D0
		SUB.W	OBJ_ZSIZE(A2),D0
		MOVE.W	D0,OBJ_ZPOS(A2)
		MOVE.W	OBJ_SPD(A1),D0
		SUB.W	#8,D0
		MOVE.W	D0,OBJ_SPD(A2)
		MOVE.L	#-$A00,OBJ_CNT0(A2)
.end		RTS

TTANK_HIT	CMP.W	#ID_MAIN,OBJ_ID(A1)
		BEQ.S	.hit
		CMP.W	#ID_SHOT,OBJ_ID(A1)
		BEQ.S	.shot
		CMP.W	#ID_MISS,OBJ_ID(A1)
		BEQ.S	.shot
		JMP	OBJ_DET_NEXT
.shot		ADD.W	#2,OBJ_SHOT_AMT(A2)
		JMP	OBJ_DET_ZXKILL
.miss		ADD.W	#100,OBJ_SHOT_AMT(A2)
		JMP	OBJ_DET_ZXKILL

.hit		MOVEM.L	D0-D7/A0-A6,-(SP)
		EXG	A2,A1
		ADD.W	#10,OBJ_SHOT_AMT(A2)
		MOVE.L	#-30*$100,OBJ_ADDR7(A2)
		SUB.L	#$400000,OBJ_SPD(A2)
		MOVE.L	A1,-(SP)
		LEA	VIS_EXPGR,A0
		MOVE.W	OBJ_ZPOS(A2),D0
		MOVE.L	OBJ_XPOS(A2),D1
		MOVE.L	OBJ_YPOS(A2),D2
		MOVEQ	#0,D3
		JSR	VIS_SET
		MOVE.L	(SP)+,A1
		MOVE.L	OBJ_ADDR2(A1),A2
		JSR	O_DEALL_S
		JSR	O_CLEAR_S

		MOVEM.L	(SP)+,D0-D7/A0-A6
		JMP	OBJ_DET_NEXT


TTANK_MOVE	JSR	O_RANGE_CHK 
		BRA.L	.clr
		BRA.L	.clr
		MOVE.L	OBJ_ADDR3(A1),A2
		BTST	#O_DEAD,OBJ_FLAG0(A2)
		BNE.S	.exp

		MOVE.W	OBJ_SHOT_AMT(A1),D0
		BEQ.S	.move
		ADDQ.B	#2,OBJ_FLASH1(A1)
		CLR.W	OBJ_SHOT_AMT(A1)
		ADD.W	D0,OBJ_DTOTAL(A1)
		CMP.W	#14,OBJ_DTOTAL(A1)
		BMI.S	.move
.exp		CLR.L	OBJ_YPOS(A1)
		LEA	VIS_TTHIT_CTRL,A2
		JSR	KILL_V1
		MOVE.L	OBJ_ADDR2(A1),A2
		JSR	O_DEALL_S
		JSR	O_CLEAR_S
		JMP	SCADD_SMALLOBJ

.move		MOVEQ	#0,D6
		JSR	O_SPEED_UPDATE
		JSR	O_XIN_UPDATE
		MOVE.L	D0,D1
		ASR.L	#8,D1
		MOVE.W	OBJ_SPD(A1),D0
		ADD.W	D0,OBJ_ZPOS(A1)

		MOVE.L	OBJ_CNT0(A1),D1
		MOVE.L	OBJ_YPOS(A1),D0
		BNE.S	.air_time

		MOVE.L	D1,D2
		ADD.L	#400,D2
		BPL	.air_skp
		ASL.L	#8,D1
		DIVS	#300,D1
		EXT.L	D1

.air_time	ADD.L	#700,D1
		ADD.L	D1,D0
		BMI.S	.land_skp
		CMP.L	#$300,D1
		BMI.S	.land_done

		CMP.L	#$500,D1
		BMI.S	.sfx_skp
		MOVE.W	#SFX_PCGND_A,D0
		JSR	CSFX_ONESHOT_V
.sfx_skp
		MOVEM.L	D1/A1,-(SP)
		MOVE.W	OBJ_ZPOS(A1),D0
		ADD.W	OBJ_ZSIZE(A1),D0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVE.L	OBJ_YPOS(A1),D2
		MOVE.W	OBJ_SPD(A1),D3
		MOVEM.L	D0-D3,-(SP)
		LEA	VIS_LAND_SPARKL,A0
		ADD.L	#-15*$100,D1
		JSR	VIS_SET
		MOVEM.L	(SP)+,D0-D3
		LEA	VIS_LAND_SPARKR,A0
		ADD.L	#15*$100,D1
		JSR	VIS_SET
		MOVEM.L	(SP)+,D1/A1

.land_done	MOVEQ	#0,D0
		NEG.L	D1
.land_skp	MOVE.L	D0,OBJ_YPOS(A1)
.air_skp	MOVE.L	D1,OBJ_CNT0(A1)
.end		RTS
.clr		MOVE.L	OBJ_ADDR2(A1),A2
		JSR	O_DEALL_S
		JMP	O_CLEAR_S

TTANK_PRINT	JSR	O_CAR_PRINT_S
		
		JSR	O_PR_ALLOCX_XIT
		LEA	OBJ_TTANK_SHAD,A0
		JSR	O_CALC_VIEW
		MOVE.L	OBJ_ADDR2(A1),A2
		JSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR2(A1)
		MOVE.L	OBJ_YPOS(A1),-(SP)
		CLR.L	OBJ_YPOS(A1)
		JSR	O_PR_SET0
		MOVE.L	(SP)+,OBJ_YPOS(A1)
		RTS

VIS_TTHIT_CTRL	DC.W	1-1
		DC.L	VIS_TTEXPGR
		DC.W	10,10-1,1,2
		DC.L	VIS_TTPIECE1
		DC.L	VIS_TTPIECE2

VIS_TTEXPGR
		DC.W	0,0,0
		DC.W	-$0,0,-$400
		DC.W	$0100,$0C00
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_TTEXPGR_GRS
		DC.W	SFX_EXPLDCAR_A	;SFX

VTTSIZE		EQU	120

VIS_TTEXPGR_GRS
		XYRATIO	VTTSIZE,300,VTTSIZE,140
		OBJCNTR	17,30,47,28
		OBJSCL	17,40,0,0,47,28,GREXP1
		
		XYRATIO	VTTSIZE,200,VTTSIZE,140
		OBJCNTR	17,30,47,28
		OBJSCL	8,24,0,0,64,48,GREXP2
		
		XYRATIO	VTTSIZE,150,VTTSIZE,150
		OBJCNTR	17,30,47,28
		OBJSCL	13,11,0,0,56,58,GREXP3
		
		XYRATIO	VTTSIZE,140,VTTSIZE,160
		OBJCNTR	17,30,47,28
		OBJSCL	10,9,0,0,61,63,GREXP4
		
		XYRATIO	VTTSIZE,130,VTTSIZE,170
		OBJCNTR	17,30,47,28
		OBJSCL	8,8,0,0,64,64,GREXP5
		OBJSCL	8,8,0,0,64,64,GREXP5

		XYRATIO	VTTSIZE,130,VTTSIZE,180
		OBJCNTR	17,30,47,28
		OBJSCL	10,8,0,0,61,59,GREXP6
		OBJSCL	10,8,0,0,61,59,GREXP6

		XYRATIO	VTTSIZE,130,VTTSIZE,190
		OBJCNTR	17,30,47,28
		OBJSCL	11,9,0,0,59,50,GREXP7
		OBJSCL	11,9,0,0,59,50,GREXP7

		XYRATIO	VTTSIZE,130,VTTSIZE,200
		OBJCNTR	17,30,47,28
		OBJSCL	10,9,0,0,61,46,GREXP8
		OBJSCL	10,9,0,0,61,46,GREXP8

VIS_TTPIECE1	DC.W	0,0,0
		DC.W	0,0,0
		DC.W	$0000,$0000
		DC.L	VIS_RTOBJ_MOVER
		DC.L	VIS_PRN_ROTATEX
		DC.L	VIS_PIECE1_GRS
		DC.W	0		;SFX
		DC.W	$2C0,$140		;Lie angles
		DC.W	$2C0+$C0,$2C0-$C0	;Lie range.
		DC.W	600,3			;Ydecay/Zdecay.
		DC.W	$40,$C00		;Spin/Xrand.
		DC.W	$E00			;Height range.
		DC.L	BITHITGR_SFX

VIS_TTPIECE2	DC.W	0,0,0
		DC.W	0,0,0
		DC.W	$0000,$0000
		DC.L	VIS_RTOBJ_MOVER
		DC.L	VIS_PRN_ROTATEX
		DC.L	VIS_PIECE2_GRS
		DC.W	0		;SFX
		DC.W	$2C0,$140		;Lie angles
		DC.W	$2C0+$C0,$2C0-$C0	;Lie range.
		DC.W	600,3			;Ydecay/Zdecay.
		DC.W	$40,$C00		;Spin/Xrand.
		DC.W	$E00			;Height range.
		DC.L	BITHITGR_SFX

OBJ_TTANK
		XYRATIO	100,190,100,190
		OBJCNTR	0,0,33,7

		DC.L	OBJ_TTANK_SET
		DC.L	OBJ_TTANK_ANM0
		DC.L	OBJ_TTANK_ANM0

OBJ_TTANK_ANM0	DC.L	OBJ_TTANK_XVIEW,OBJ_TTANK_YVIEW
		OBJSCL	0,-2,0,0,31,10,TTANKS
		OBJSCL	0,-1,32,0,32,9,TTANKS
		OBJSCL	0,0,65,0,33,7,TTANKS
		OBJSCL	1,-1,99,0,32,9,TTANKS
		OBJSCL	2,-2,132,0,31,10,TTANKS

OBJ_TTANK_SHAD	DC.L	OBJ_TTANK_XVIEW,OBJ_TTANK_YVIEW
		OBJSCL	0,-1,0,10,31,10,TTANKS
		OBJSCL	0,0,32,10,32,9,TTANKS
		OBJSCL	0,1,65,10,33,7,TTANKS
		OBJSCL	1,0,99,10,32,9,TTANKS
		OBJSCL	2,-1,132,10,31,10,TTANKS

OBJ_TTANK_XVIEW	DC.W	2*$20,$20
		DC.L	$3000,$7800,-1
OBJ_TTANK_YVIEW	DC.W	5*$20
		DC.W	-1

OBJ_TTANK_SKID	DC.W	0

OBJ_TTANK_SET	DC.W	ID_BOTTLE	;OBJ_ID
		DC.L	TTANK_MOVE	;OBJ_MOVE_CTRL
		DC.L	TTANK_PRINT	;OBJ_PRN_CTRL
		DC.W	$18		;OBJ_ZSIZE
		XYSIZE	33,20
		DC.W	0		;OBJ_MASS
		DC.L	TTANK_HIT	;OBJ_HIT_GOTO
		DC.W	0		;OBJ_ZPOS
		DC.L	0		;OBJ_XPOS
		DC.L	0		;OBJ_YPOS
		DC.W	$A0		;OBJ_SPD_MAXA
		DC.W	$A0		;OBJ_SPD_MAXB
		DC.W	40		;OBJ_SPD_INCFA
		DC.W	40		;OBJ_SPD_INCFB
		DC.W	40		;OBJ_SPD_DECF
		DC.W	0		;OBJ_SPD_BRAKE
		DC.W	0		;OBJ_TRACTION
		
; --------------------------------------------------------------------------

		XYRATIO	100,190,100,190
		OBJCNTR	0,16,33,7

OBJ_TRUCK_TTA	DC.L	OBJ_TTANK_XVIEW,OBJ_TTANK_YVIEW
		OBJSCL	12,-2,0,0,31,10,TTANKS
		OBJSCL	8,-1,32,0,32,9,TTANKS
		OBJSCL	0,0,65,0,33,7,TTANKS
		OBJSCL	-7,-1,99,0,32,9,TTANKS
		OBJSCL	-10,-2,132,0,31,10,TTANKS

		XYRATIO	100,200,100,200

OBJ_TRUCK_TTB	DC.L	OBJ_TTANK_XVIEW,OBJ_TTANK_YVIEW
		OBJSCL	12,-2,0,20,31,10,TTANKS
		OBJSCL	8,-1,32,20,32,9,TTANKS
		OBJSCL	0,0,65,20,33,7,TTANKS
		OBJSCL	-7,-1,99,20,32,9,TTANKS
		OBJSCL	-10,-2,132,20,31,10,TTANKS

OBJ_TRUCK
		XYRATIO	100,150,100,150
		OBJCNTR	108,46,69,92

		DC.L	OBJ_TRUCK_SET
		DC.L	OBJ_TRUCK_ANM0
		DC.L	OBJ_TRUCK_ANM1
		DC.L	OBJ_TRUCK_SKID
		DC.L	OBJ_TRUCK_DAM

OBJ_TRUCK_ANM0	DC.L	OBJ_TRUCK_XVIEW,OBJ_TRUCK_YVIEW

		OBJSCLM	96,38,0,0,106,117,TRUCK3
		OBJSCLM	100,38,0,0,89,116,TRUCK2
		OBJSCL	107,38,0,0,70,113,TRUCK1
		OBJSCL	100,38,0,0,89,116,TRUCK2
		OBJSCL	96,38,0,0,106,117,TRUCK3
       
		OBJSCLM	101,46,0,0,101,104,TRUCK6
		OBJSCLM	103,46,0,0,85,103,TRUCK5
		OBJSCL	108,46,0,0,69,100,TRUCK4
		OBJSCL	103,46,0,0,85,103,TRUCK5
		OBJSCL	101,46,0,0,101,104,TRUCK6
		
		OBJSCLM	103,41,0,0,98,103,TRUCK9
		OBJSCLM	104,42,0,0,84,101,TRUCK8
		OBJSCL	108,43,0,0,68,100,TRUCK7
		OBJSCL	104,42,0,0,84,101,TRUCK8
		OBJSCL	103,41,0,0,98,103,TRUCK9

OBJ_TRUCK_ANM1	DC.L	OBJ_TRUCK_XVIEW,OBJ_TRUCK_YVIEW

		OBJSCLM	96,38,112,0,106,117,TRUCK3
		OBJSCLM	100,38,96,0,89,116,TRUCK2
		OBJSCL	107,38,80,0,70,113,TRUCK1
		OBJSCL	100,38,96,0,89,116,TRUCK2
		OBJSCL	96,38,112,0,106,117,TRUCK3
       
		OBJSCLM	101,46,112,0,101,104,TRUCK6
		OBJSCLM	103,46,96,0,85,103,TRUCK5
		OBJSCL	108,46,80,0,69,100,TRUCK4
		OBJSCL	103,46,96,0,85,103,TRUCK5
		OBJSCL	101,46,112,0,101,104,TRUCK6
		
		OBJSCLM	103,41,112,0,98,103,TRUCK9
		OBJSCLM	104,42,96,0,84,101,TRUCK8
		OBJSCL	108,43,80,0,68,100,TRUCK7
		OBJSCL	104,42,96,0,84,101,TRUCK8
		OBJSCL	103,41,112,0,98,103,TRUCK9

OBJ_TRUCK_XVIEW	DC.W	7*$20,$20
		DC.L	$3000,$7800,-1
OBJ_TRUCK_YVIEW	DC.W	5*$20
		DC.W	$16,-1

OBJ_TRUCK_SKID	DC.W	2
		XSIZE	24
		XSIZE	-24

OBJ_TRUCK_SET	DC.W	ID_CAR		;OBJ_ID
		DC.L	TRUCK_MOVE	;OBJ_MOVE_CTRL
		DC.L	TRUCK_PRINT	;OBJ_PRN_CTRL
		DC.W	$50		;OBJ_ZSIZE
		XYSIZE	66,92
		DC.W	$7868		;OBJ_MASS
		DC.L	HROUTINE_CAR	;OBJ_HIT_GOTO
		DC.W	$80*40		;OBJ_ZPOS
		DC.L	$100*200	;OBJ_XPOS
		DC.L	$100*0		;OBJ_YPOS
		DC.W	$40		;OBJ_SPD_MAXA
		DC.W	$70		;OBJ_SPD_MAXB
		DC.W	20		;OBJ_SPD_INCFA
		DC.W	20		;OBJ_SPD_INCFB
		DC.W	20		;OBJ_SPD_DECF
		DC.W	5		;OBJ_SPD_BRAKE
		DC.W	$0608		;OBJ_TRACTION

OBJ_TRUCK_DAM	DC.W	($FFFF/$240)/20,$FFFF/200
	     
		DC.W	1*$FFFF/7
		DC.L	OBJ_TRUCK_DPL,DAM_SET0
		DC.W	2*$FFFF/7
		DC.L	OBJ_TRUCK_DPL,DAM_SET0
		DC.W	3*$FFFF/7
		DC.L	OBJ_TRUCK_DPL,DAM_SET0
		DC.W	4*$FFFF/7
		DC.L	OBJ_TRUCK_DPL,DAM_SET0
		DC.W	5*$FFFF/7
		DC.L	OBJ_TRUCK_DPL,DAM_SET0
		DC.W	6*$FFFF/7
		DC.L	OBJ_TRUCK_DPL,DAM_SET0

		DC.W	$FFFF
		DC.L	LOGIC_DEATH1,LOGIC_SET0
		DC.L	OBJ_TRUCK_KILL,KILL_SET0

OBJ_TRUCK_KILL	DC.W	$0110,5-1
		DC.L	VIS_EXPLD_C1
		DC.L	VIS_EXPLD_C2
		DC.L	VIS_EXPLD_C3
		DC.L	VIS_EXPLD_C4
		DC.L	VIS_EXPLD_C5
		DC.W	20,20-1,1,6
		DC.L	VIS_DOOR
		DC.L	VIS_PIPE
		DC.L	VIS_BUMP
		DC.L	VIS_WHEL
		DC.L	VIS_PIECE1
		DC.L	VIS_PIECE2

OBJ_TRUCK_DPL	DC.W	6
		DC.L	OBJ_TRUCK_DP1
		DC.L	OBJ_TRUCK_DP2
		DC.L	OBJ_TRUCK_DP3
		DC.L	OBJ_TRUCK_DP4
		DC.L	OBJ_TRUCK_DP5
		DC.L	OBJ_TRUCK_DP6

OBJ_TRUCK_DP1	DC.L	VIS_FLAME0,VIS_EXPCTRL_B
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTS	$11,114,122,$11,108,119,$11,105,118
		DAMPTS	$11,114,115,$11,109,114,$11,107,113
		DAMPTS	$11,115,111,$11,111,110,$11,108,111

OBJ_TRUCK_DP2	DC.L	VIS_FLAME1M,VIS_EXPCTRL_B
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTS	$11,172,135,$11,164,137,$11,157,138
		DAMPTS	$11,173,129,$11,164,131,$11,158,132
		DAMPTS	$11,172,125,$11,164,126,$11,158,126

OBJ_TRUCK_DP3	DC.L	VIS_FLAME2M,VIS_EXPCTRL_R
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTS	$00,168,76,$01,174,76,$01,176,76
		DAMPTS	$00,168,78,$01,175,78,$01,178,78
		DAMPTS	$00,167,81,$01,175,81,$01,177,80

OBJ_TRUCK_DP4	DC.L	VIS_FLAME3M,VIS_EXPCTRL_R
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTS	$00,162,89,$01,188,89,$01,200,91
		DAMPTS	$00,162,102,$01,187,102,$01,200,104
		DAMPTS	$00,162,114,$01,186,114,$01,199,114

OBJ_TRUCK_DP5	DC.L	VIS_FLAME2,VIS_EXPCTRL_L
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTSM	$00,168,76,$01,174,76,$01,176,76
		DAMPTSM	$00,168,78,$01,175,78,$01,178,78
		DAMPTSM	$00,167,81,$01,175,81,$01,177,80

OBJ_TRUCK_DP6	DC.L	VIS_FLAME3,VIS_EXPCTRL_L
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTSM	$00,162,89,$01,188,89,$01,200,91
		DAMPTSM	$00,162,102,$01,187,102,$01,200,104
		DAMPTSM	$00,162,114,$01,186,114,$01,199,114

OBJ_TDAM_XVIEW	DC.W	7*10,10
		DC.L	$3000,$7800,-1
OBJ_TDAM_YVIEW	DC.W	5*10
		DC.W	$16,-1

; --------------------------------------------------------------------------

LOGIC_TRUCK

	IFTRUE
    	BATRANGE 1,8
    		IFTRUE
    			GRIDRANGE 1,32,30,64
    			GRIDGOTOX 7
		ELSE_IFTRUE
			GRIDRANGE 1,11,8,29
			GRIDGOTOXY 7,16
		ELSE_IFTRUE
			GRIDRANGE 1,11,3,7
			GRIDGOTOXY 7,16
		ELSE_IFTRUE
			GRIDRANGE 1,11,1,2
			GRIDGOTOXY 7,6
		ELSE_IFTRUE
			GRIDRANGE 12,21,8,29
			GRIDGOTOXY 7,16
		ELSE_IFTRUE
			GRIDRANGE 12,21,3,7
			GRIDGOTOXY 7,10
		ELSE_IFTRUE
			GRIDRANGE 12,21,1,2
			GRIDGOTOXY 7,1
		ELSE_IFTRUE
			GRIDRANGE 22,32,8,29
			GRIDGOTOXY 15,16
		ELSE_IFTRUE
			GRIDRANGE 22,32,3,7
			GRIDGOTOXY 16,9 
	        ELSE_IFTRUE
			GRIDRANGE 22,32,1,2
			GRIDGOTOXY 11,1
		IFEND
    		
 	ELSE_IFTRUEx
    	BATRANGE 9,16
    		IFTRUE
    			GRIDRANGE 1,32,30,64
    			GRIDGOTOX 13
		ELSE_IFTRUE
			GRIDRANGE 1,8,8,29
			GRIDGOTOXY 13,16
	  	ELSE_IFTRUE
			GRIDRANGE 1,8,3,7
			GRIDGOTOXY 13,10
		ELSE_IFTRUE
			GRIDRANGE 1,8,1,2
			GRIDGOTOY 6
		ELSE_IFTRUE
			GRIDRANGE 9,16,8,29
			GRIDGOTOXY 13,16
		ELSE_IFTRUE
			GRIDRANGE 9,16,3,7
			GRIDGOTOXY 13,10
		ELSE_IFTRUE
			GRIDRANGE 9,16,1,2
			GRIDGOTOXY 13,1
		ELSE_IFTRUE
			GRIDRANGE 17,32,8,29
			GRIDGOTOXY 13,16
		ELSE_IFTRUE
			GRIDRANGE 17,32,3,7
			GRIDGOTOXY 13,10
	        ELSE_IFTRUE
			GRIDRANGE 17,32,1,2
			GRIDGOTOXY 13,1
		IFEND  
    	   
 	ELSE_IFTRUE
    	BATRANGE 17,24
    	      	IFTRUE
    			GRIDRANGE 1,32,30,64
    			GRIDGOTOX 21
		ELSE_IFTRUE
			GRIDRANGE 1,11,8,29
			GRIDGOTOXY 13,16
		ELSE_IFTRUE
			GRIDRANGE 1,11,3,7
			GRIDGOTOXY 13,10
	        ELSE_IFTRUE
			GRIDRANGE 1,11,1,2
			GRIDGOTOXY 13,1
		ELSE_IFTRUE
			GRIDRANGE 12,16,8,29
			GRIDGOTOXY 21,16
		ELSE_IFTRUE
			GRIDRANGE 12,16,3,7
			GRIDGOTOXY 21,9
		ELSE_IFTRUE
			GRIDRANGE 12,16,1,2
			GRIDGOTOXY 22,1
		ELSE_IFTRUE
			GRIDRANGE 17,32,8,29
			GRIDGOTOXY 22,16
		ELSE_IFTRUE
			GRIDRANGE 17,32,3,7
			GRIDGOTOXY 22,9
	        ELSE_IFTRUE
			GRIDRANGE 22,32,1,2
			GRIDGOTOY 6
		IFEND
 	
 	ELSE_IFTRUE
    	BATRANGE 25,32
    		IFTRUE
    			GRIDRANGE 1,32,30,64
    			GRIDGOTOX 26
		ELSE_IFTRUE
			GRIDRANGE 1,11,8,29
			GRIDGOTOXY 14,16
 		ELSE_IFTRUE
			GRIDRANGE 1,11,3,7
			GRIDGOTOXY 14,10
		ELSE_IFTRUE
			GRIDRANGE 1,11,1,2
			GRIDGOTOXY 14,1	
	       	ELSE_IFTRUE
			GRIDRANGE 12,21,8,29
			GRIDGOTOXY 25,16
	       	ELSE_IFTRUE
			GRIDRANGE 12,21,3,7
			GRIDGOTOXY 25,10
		ELSE_IFTRUE
			GRIDRANGE 12,21,1,2
			GRIDGOTOXY 24,1
		ELSE_IFTRUE
			GRIDRANGE 22,32,8,29
			GRIDGOTOXY 25,16
		ELSE_IFTRUE
			GRIDRANGE 22,32,3,7
			GRIDGOTOXY 25,9
	        ELSE_IFTRUE
			GRIDRANGE 22,32,1,2
			GRIDGOTOY 6
		     
		IFEND
	IFEND
	LOGICEND		       			

; --------------------------------------------------------------------------

		EVEN

; --------------------------------------------------------------------------

