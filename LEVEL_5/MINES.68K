; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ BATMAN RETURNS CD-ROM						   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

; ROAD OBJECT - LEVEL 5 - "MINES"

; --------------------------------------------------------------------------

MINE_SETUP	LEA	OBJ_MINE,A6
		BSR	O_MTR_SETUP
		MOVE.W	#-50,OBJ_CNT0(A1)
		MOVE.W	#10,D7
		BSR	RANDOM_RANGE_U
		MOVE.W	D7,OBJ_CNT2(A1)
		BRA	O_MTR_SETFIN

; --------------------------------------------------------------------------

MINE_HIT	CMP.W	#ID_MAIN,OBJ_ID(A1)
		BEQ.S	.car
		CMP.W	#ID_SHOT,OBJ_ID(A1)
		BNE.S	.miss_chk
		MOVEM.L	D0-D7/A0-A6,-(SP)
		EXG	A1,A2
		ADDQ.W	#2,OBJ_DTOTAL(A1)
		ADDQ.B	#2,OBJ_FLASH1(A1)
		CMP.W	#20,OBJ_DTOTAL(A1)
		BMI	.done

		MOVE.L	A1,-(SP)
		JSR	SCADD_SMALLOBJ
		MOVE.L	(SP)+,A1

		BRA.S	.blow_it
.miss_chk	CMP.W	#ID_MISS,OBJ_ID(A1)
		BNE	OBJ_DET_NEXT
		MOVEM.L	D0-D7/A0-A6,-(SP)
		EXG	A1,A2

.blow_it	MOVE.L	A1,-(SP)
		LEA	VIS_EXPGR,A0
		CLR.L	OBJ_YPOS(A1)
		BSR	VIS_SET_XYZS
		MOVE.L	(SP)+,A1
		MOVE.L	OBJ_ADDR6(A1),A0
		CLR.W	RDDATA3(A0)
		JSR	O_CLEAR_S
.done		MOVEM.L	(SP)+,D0-D7/A0-A6
		JMP	OBJ_DET_ZXKILL

.car		MOVEM.L	D0-D7/A0-A6,-(SP)
		EXG	A1,A2		
	
		ADDQ.W	#6,OBJ_SHOT_AMT(A2)	;Damage.

		ADDQ.B	#4,OBJ_FLASH1(A2)	;Rebound batski.
		MOVE.L	#-25*$100,OBJ_ADDR7(A2)
		MOVE.W	OBJ_SPD(A2),D7
		ASR.W	D7
		NEG.W	D7
		BMI.S	.neg
		ADD.W	#$10,D7
		BRA.S	.set
.neg		CMP.W	#-$40,D7
		BMI.S	.set
		MOVE.W	#-$40,D7
.set		MOVE.W	D7,OBJ_SPD(A2)
		
		BRA.S	.blow_it

; --------------------------------------------------------------------------

MINE_MOVE	SUBQ.W	#1,OBJ_CNT2(A1)
		BPL.S	.skp
		MOVE.W	#12,OBJ_CNT2(A1)
		MOVE.B	#4,OBJ_FLASH0(A1)
.skp
		MOVE.W	#2,D1
		MOVE.W	OBJ_CNT1(A1),D3
		MOVE.W	OBJ_CNT0(A1),D2
		BMI.S	.cangle_n
.cangle_p	SUB.W	D1,D3
		ADD.W	D3,D2
		BPL.S	.cset
     		BRA.S	.csfx
.cangle_n	ADD.W	D1,D3
		ADD.W	D3,D2
;		BMI.S	.cset

.csfx		;MOVE.W	#VCTRL+SFX_BOUYBELL_A,D0
		;JSR	CSFX_ONESHOT_V

.cset		MOVE.W	D2,OBJ_CNT0(A1)
		MOVE.W	D3,OBJ_CNT1(A1)

.end		RTS

; --------------------------------------------------------------------------

MINE_PRINT	MOVE.W	D0,D1
		ADD.W	#$58,D1
		CMP.W	#$180,D1
		BMI.S	.alloc
		ADD.W	#$200,D1
.alloc		BSR	O_PR_A_XIT
		BSR	O_CALC_PATCHZ
		MOVE.L	OBJ_ADDR0(A1),A2
		MOVE.L	OBJGR_ANM0(A2),A0
		BSR	O_FLASH_CHECK_S
		MOVE.L	OBJ_ADDR1(A1),A2
		BSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		MOVE.W	OBJ_CNT0(A1),D2
		AND.W	#$3FE,D2
		BRA	O_PR_ROTATE

; --------------------------------------------------------------------------

VIS_EXPGR
		DC.W	0,0,0
		DC.W	0,0,-$400
		DC.W	$0100,$0C00
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0
		DC.L	VIS_EXPGR_GRS
		DC.W	SFX_EXPLDCAR_A	;SFX

VIS_EXPGR_GRS
		XYRATIO	100,300,100,140
		OBJCNTR	17,40,47,28
		OBJSCL	17,40,0,0,47,28,GREXP1
		
		XYRATIO	100,200,100,140
		OBJCNTR	17,40,47,28
		OBJSCL	8,24,0,0,64,48,GREXP2
		
		XYRATIO	100,150,100,150
		OBJCNTR	17,40,47,28
		OBJSCL	13,11,0,0,56,58,GREXP3
		
		XYRATIO	100,140,100,160
		OBJCNTR	17,40,47,28
		OBJSCL	10,9,0,0,61,63,GREXP4
		
		XYRATIO	100,130,100,170
		OBJCNTR	17,40,47,28
		OBJSCL	8,8,0,0,64,64,GREXP5
		OBJSCL	8,8,0,0,64,64,GREXP5

		XYRATIO	100,130,100,180
		OBJCNTR	17,40,47,28
		OBJSCL	10,8,0,0,61,59,GREXP6
		OBJSCL	10,8,0,0,61,59,GREXP6

		XYRATIO	100,130,100,190
		OBJCNTR	17,40,47,28
		OBJSCL	11,9,0,0,59,50,GREXP7
		OBJSCL	11,9,0,0,59,50,GREXP7

		XYRATIO	100,130,100,200
		OBJCNTR	17,40,47,28
		OBJSCL	10,9,0,0,61,46,GREXP8
		OBJSCL	10,9,0,0,61,46,GREXP8

; --------------------------------------------------------------------------

		XYRATIO	100,250,100,250
		OBJCNTR	0,0,0,0
OBJ_MINE
		DC.L	OBJ_MINE_SET
		DC.L	OBJ_MINE_ANM0

OBJ_MINE_ANM0	OBJSCL	0,-24,39,39,46,46,MINEAA

OBJ_MINE_SET	DC.W	ID_ROADOBJ	;OBJ_ID
		DC.L	MINE_MOVE	;OBJ_MOVE_CTRL
		DC.L	MINE_PRINT	;OBJ_PRN_CTRL
		DC.W	$20		;OBJ_ZSIZE
		XYSIZE	32,46
		DC.W	0		;OBJ_MASS
		DC.L	MINE_HIT	;OBJ_HIT_GOTO
		DC.W	0		;OBJ_ZPOS
		DC.L	0		;OBJ_XPOS
		DC.L	55*$100		;OBJ_YPOS

; --------------------------------------------------------------------------

