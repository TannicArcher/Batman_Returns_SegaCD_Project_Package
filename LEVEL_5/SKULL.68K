; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ BATMAN RETURNS CD-ROM						   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

; MID BOSS - LEVEL 5 - "Giant Skull"

; --------------------------------------------------------------------------

SKULL_SETUP	LEA	OBJ_SKULL_A,A6
		JSR	O_OBJECT_SETUP	 
		BNE.S	.end

		MOVE.L	A1,A2
		JSR	OBJECT_ALLOC
		BNE	.error
		LEA	SKULL_DET_DATA,A5
		JSR	O_OSET
		MOVE.L	A2,OBJ_ADDR6(A1)
		MOVE.L	A1,OBJ_ADDR6(A2)

		MOVE.L	#LOGIC_SKULL,OBJ_LOGIC_DATA(A2)
		MOVE.W	#$65,OBJ_LMSPD(A2)
		MOVE.W	#SFX_ESKULL_A,OBJ_FX0_SFXNUM(A2)
		MOVE.W	#-$400,OBJ_CNT1(A2)
		MOVE.W	#120,OBJ_CNT6(A1)

		MOVEQ	#0,D0
.end		RTS

.error		MOVE.L	A2,A1
		JSR	O_CLEAR_S
		MOVEQ	#1,D0
		RTS

; --------------------------------------------------------------------------

SKULL_CLEAR	JSR	O_CLEAR_S
		MOVE.L	OBJ_ADDR2(A1),A2
		JSR	O_DEALL_S
		JSR	CLEAR_DAMAGE
		JSR	SFX3D_KILL
		MOVE.L	OBJ_ADDR6(A1),A1
		JSR	DETECT_DEALL
		CLR.B	OBJ_FLAG0(A1)
		JSR	SCADD_MIDBOSS
		JMP	ECL_KILL

; --------------------------------------------------------------------------

SKULL_MOVE	BTST	#O_CLEAR,OBJ_FLAG0(A1)
		BNE.S	SKULL_CLEAR
	
		CLR.L	OBJ_XINERTIA(A1)

		JSR	LOGIC_CTRL
		JSR	SFX3D_SPEED

		MOVE.L	OBJ_ADDR6(A1),A6	;Hit update.
		MOVE.W	OBJ_SHOT_AMT(A6),D0
		ADD.W	D0,OBJ_SHOT_AMT(A1)
		MOVE.B	OBJ_FLASH1(A6),D0
		ADD.B	D0,OBJ_FLASH1(A1)

		JSR	DAMAGE_CTRL
		JSR	EDAMAGE_METER

		MOVE.L	OBJ_ADDR6(A1),A6	;Move detect area.
		MOVE.L	OBJ_XPOS(A1),OBJ_XPOS(A6)
		MOVE.W	OBJ_ZPOS(A1),D0
		SUB.W	#8,D0
		MOVE.W	D0,OBJ_ZPOS(A6)
		MOVE.L	OBJ_SPD(A1),OBJ_SPD(A6)
		CLR.W	OBJ_SHOT_AMT(A6)
		CLR.B	OBJ_FLASH1(A6)

		BSR	SKULL_MOUTH

		MOVE.L	OBJ_ADDR5(A1),D1
		BEQ.S	.fchk
		MOVE.L	D1,A2
		ADDQ.L	#2,A2
		CMP.W	#-1,(A2)
		BEQ.S	.fset
		CMP.W	#-2,(A2)
		BNE.S	.fnext
		CLR.L	OBJ_ADDR5(A1)
		BRA.S	.fchk
.fset		MOVE.W	#8,OBJ_CNT5(A1)
		MOVE.L	OBJECT_DATA+OBJ_XPOS,OBJ_ADDR4(A1)
		ADDQ.L	#2,A2
		
		MOVE.W	#SFX_LAZERFIRE_A,D0
		JSR	CSFX_ONESHOT_V

.fnext		MOVE.L	A2,OBJ_ADDR5(A1)

.fchk		TST.W	OBJ_CNT5(A1)
		BEQ.S	.misc
		BTST	#O_DEAD,OBJ_FLAG0(A1)
		BNE.S	.end
		SUBQ.W	#1,OBJ_CNT5(A1)
		BPL	SKULL_BEAM	
	
.misc		SUBQ.W	#1,OBJ_CNT6(A1)
		BPL.S	.end
		CLR.W	OBJ_CNT6(A1)
		CMP.W	#$B00,OBJ_ZPOS(A1)
		BPL.S	.end
		
		MOVE.W	#12+15,OBJ_CNT6(A1)
		BCHG	#0,OBJ_FLAG1(A1)
		BEQ.S	.fr

.fl		BTST	#2,OBJ_FLAG1(A1)
		BNE.S	.end
		MOVE.L	#SKULL_FTAB_L,OBJ_ADDR5(A1)		
		MOVE.L	#-48*$100,OBJ_ADDR3(A1)
		RTS

.fr		BTST	#1,OBJ_FLAG1(A1)
		BNE.S	.end
		MOVE.L	#SKULL_FTAB_R,OBJ_ADDR5(A1)		
		MOVE.L	#48*$100,OBJ_ADDR3(A1)
.end		RTS

; --------------------------------------------------------------------------

SKULL_FTAB_L	
		DC.W	$20,$20,$00
		DC.W	$20,$20,$00
		DC.W	$20,$00
		DC.W	$20,$20
		DC.W	$40,$20
		DC.W	-1
		DC.W	$40
		DC.W	$40
		DC.W	$40
		DC.W	$40
		DC.W	-2

SKULL_FTAB_R	
		DC.W	$60,$60,$00
		DC.W	$60,$60,$00
		DC.W	$60,$00
		DC.W	$60,$60
		DC.W	$80,$60
		DC.W	-1
		DC.W	$80
		DC.W	$80
		DC.W	$80
		DC.W	$80
		DC.W	-2

; --------------------------------------------------------------------------

SKULL_MOUTH	MOVE.L	OBJ_ADDR7(A1),D1
		MOVE.W	OBJ_CNT0(A1),D0		;Ymove.
		MOVE.W	OBJ_CNT1(A1),D2		;Ystep.
		BPL.S	.forward

.backward	ADD.W	D2,D0
		EXT.L	D0
		ADD.L	D0,D1
		BPL	.set
		MOVEQ	#0,D1
		MOVE.W	D0,D3
		ASR.W	#2,D0
		SUB.W	D3,D0
		CMP.W	#$200,D0
		BPL.S	.spkchk
		SUBQ.W	#1,OBJ_CNT3(A1)
		BPL	.set
		
		MOVE.W	#SFX_SKULLOPEN_A,D0
		JSR	CSFX_ONESHOT_V
		
		MOVE.W	#20,OBJ_CNT4(A1)
		MOVEQ	#0,D2
		MOVE.W	#$400,D0
		LEA	VIS_SKSPLASHD,A0
		BRA	.splash

.spkchk		CMP.W	#$C00,D0
		BMI	.set
		CMP.W	#$80,OBJ_ZPOS(A1)
		BMI.S	.set
		MOVE.W	#5,D7
.spark_lp	MOVEM.L	D0-D2/D7/A1,-(SP)
		LEA	VIS_SKSPARK,A0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVE.L	#-$2000,D2
		JSR	VIS_SET_ZS
		MOVEM.L	(SP)+,D0-D2/D7/A1
		DBRA	D7,.spark_lp
		BRA.S	.set

.forward	ADD.W	D2,D0
		EXT.L	D0
		ADD.L	D0,D1
		CMP.L	#50*$100,D1
		BLT.S	.set
		MOVE.L	#50*$100,D1
		
		SUBQ.W	#1,OBJ_CNT4(A1)
		BPL.S	.set

		MOVE.W	#SFX_SKULLCLOS_A,D0
		JSR	CSFX_ONESHOT_V

		MOVE.W	#40,OBJ_CNT3(A1)
		MOVE.W	#-$400,D2

		LEA	VIS_SKSPLASHU,A0

.splash		MOVEM.L	D0-D2/A1,-(SP)
		MOVE.W	OBJ_ZPOS(A1),D0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVEQ	#0,D2
		JSR	VIS_SET_S
		MOVEM.L	(SP)+,D0-D2/A1

.set		MOVE.W	D0,OBJ_CNT0(A1)
		MOVE.W	D2,OBJ_CNT1(A1)
		MOVE.L	D1,OBJ_ADDR7(A1)
		RTS

VSK_PRN0NC	MOVE.W	D0,D1
		ADD.W	#$100,D1
		JSR	O_PR_A_XIT
		JSR	VIS_PCALC
		MOVE.W	D0,D1
		ADD.W	OBJ_CNT7(A1),D1
		JSR	O_PR_SET0_RS
		MOVE.L	#$01000000,(A6)
		MOVE.L	#$00C00000,20(A6)
		RTS

; --------------------------------------------------------------------------

SKULL_PRINT	JSR	O_CALC_PATCHZ
		JSR	O_PR_ALLOCX
		BRA.L	.misc
		BRA.L	.misc
		BRA.L	.misc
		MOVE.L	OBJ_ADDR0(A1),A0
		MOVE.L	OBJGR_ANM0(A0),A0
		MOVE.L	OBJ_ADDR5(A1),D1
		BEQ.S	.skp
		MOVE.L	D1,A2
		ADD.W	(A2),A0
.skp		JSR	O_FLASH_CHECK_S
		MOVE.L	OBJ_ADDR1(A1),A2
		JSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		JSR	O_PR_SET0

		SUBQ.W	#1,D0
		JSR	O_PR_ALLOCX
		BRA.L	.misc
		BRA.L	.misc
		BRA.L	.misc
		MOVE.L	OBJ_ADDR0(A1),A0
		MOVE.L	OBJGR_ANM1(A0),A0
		JSR	O_FCS_CHK
		MOVE.L	OBJ_ADDR2(A1),A2
		JSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR2(A1)
		MOVE.L	OBJ_XPOS(A1),D6
		MOVE.L	OBJ_ADDR7(A1),D7
		JSR	O_PR_SET0_XY
		MULS	RCYFRACT(A5),D5
		ADD.L	RCYPOS(A5),D5
		DIVS	D0,D5
		ADD.W	#97,D5
		CMP.W	20(A6),D5
		BPL.S	.misc
		MOVE.W	D5,20(A6)
.misc		
		BRA	SHOW_DAMAGE

; --------------------------------------------------------------------------

SKULL_DET_HIT	CMP.W	#ID_SHOT,OBJ_ID(A1)
		BEQ	.hit
		CMP.W	#ID_MISS,OBJ_ID(A1)
		BNE.S	.next
.hit		MOVE.L	A0,-(SP)
		MOVE.L	OBJ_ADDR6(A2),A0
		CMP.L	#25*$100,OBJ_ADDR7(A0)
		BMI.S	.closed
		MOVE.L	(SP)+,A0
		JMP	OBJ_HIT_NORM
.closed		MOVE.L	(SP)+,A0
.next		JMP	OBJ_DET_NEXT

SKULL_DET_DATA	DC.W	ID_CAR		;OBJ_ID
		DC.L	SIGN_NEXT	;OBJ_MOVE_CTRL
		DC.L	SIGN_NEXT	;OBJ_PRN_CTRL
		DC.W	2		;OBJ_ZSIZE
		XYRATIO	100,350,100,350
		XYSIZE	27,18
		DC.W	$0000		;OBJ_MASS
		DC.L	SKULL_DET_HIT	;OBJ_HIT_GOTO
		DC.W	$80*40		;OBJ_ZPOS
		DC.L	$100*200	;OBJ_XPOS
		DC.L	$100*0		;OBJ_YPOS

; --------------------------------------------------------------------------

SKULL_BEAM	MOVE.L	OBJ_XPOS(A1),-(SP)
		MOVE.L	OBJ_ADDR3(A1),D0
		ADD.L	D0,OBJ_XPOS(A1)
		LEA	BEAM_SET_DATA,A4
		
		MOVE.L	OBJECT_DATA+OBJ_SPD,D0
		MOVE.W	OBJECT_DATA+OBJ_ZPOS,D1
		MOVE.L	OBJ_ADDR4(A1),D6
		MOVEQ	#0,D7						
		JSR	O_LINE_TO_POS
		
		MOVE.L	(SP)+,OBJ_XPOS(A1)
		RTS

BEAM_PRINT	MOVE.L	OBJ_ADDR0(A1),A0
		MOVE.L	OBJGR_ANM0(A0),A0
		MOVE.W	OBJ_CNT0(A1),D1
		ADD.W	#$20,D1
		CMP.W	#19*$20,D1
		BMI.S	.skp
		MOVE.W	#19*$20,D1
.skp		MOVE.W	D1,OBJ_CNT0(A1)
		AND.W	#$FFE0,D1
		LEA	(A0,D1.W),A0
		JMP	O_T2M_PRINT

BEAM_MOVE	BTST	#O_CLEAR,OBJ_FLAG0(A1)
		BNE	.clear
		JSR	O_RANGE_CHK 	
		BRA.L	.clear
		BRA.L	.clear
		MOVE.L	OBJ_XPOS(A1),D0
		SUB.L	#200*$100,D0
		BPL.S	.pxchk
		NEG.L	D0
.pxchk		CMP.L	#600*$100,D0
		BPL	.clear

		MOVE.W	OBJ_SPD(A1),D0
		MOVE.W	OBJ_ADDR4+2(A1),D1
		LEA	SIGN_DET_ZXKILL,A5	;Sign hit adjust routine.
		JSR	OBJDET_ZXY
		ADD.W	D0,OBJ_ZPOS(A1)
		ADD.L	D1,OBJ_XPOS(A1)

		MOVE.W	DET_HIT_FLAG,D0
		BNE.S	.hit
.cont		MOVE.L	OBJ_ADDR5(A1),D0
		ADD.L	D0,OBJ_SPD(A1)
		MOVE.L	OBJ_ADDR2(A1),D0
		ADD.L	D0,OBJ_YPOS(A1)
		BMI.S	.end
		CMP.L	#50*$100,OBJ_YPOS(A1)
		BPL.S	.clear
.end		RTS

.clear		JMP	O_CLEAR_S

.hit		CMP.W	#OBJ_HIT,D0
		BCS.S	BEAMHIT_OBJECT
		MOVE.L	DET_HIT_DATA1,A2
		CMP.W	#ID_MAIN,OBJ_ID(A2)
		BNE.S	.cont

BEAMHIT_OBJECT	BSET	#O_CLEAR,OBJ_FLAG0(A1)
		ADDQ.W	#1,OBJ_SHOT_AMT(A2)
		
		MOVE.W	#SFX_LAZERHIT_A,D0
		JMP	CSFX_ONESHOT_V

; --------------------------------------------------------------------------

BEAM_SET_DATA	DC.L	OBJ_BEAM	;Object setup.
		DC.L	SIGN_NEXT	;Ground hit routine.
		DC.L	BEAMHIT_OBJECT	;Object hit routine.
		DC.L	-150*$100	;YPOS offset.
		DC.L	155*$100	;Yheight.
		DC.W	0,8		;Up&down steps.
		DC.L	$40000		;Zlimit.
		DC.L	300*$100	;Xlimit.
		
OBJ_BEAM	DC.L	OBJ_BEAM_SET
		DC.L	OBJ_BEAM_ANM0
		DC.L	OBJ_BEAM_ANM0
		DC.L	0

OBJ_BEAM_ANM0	
		XYRATIO	100,80,100,80
RINC		=	10

		REPT	2
		OBJCNTR	0,0,31,15
		OBJSCL	0,0,0,0,31,31,SKBEAM
		XYRATIO	100,(XRATIO_TO/$100+RINC),100,(XRATIO_TO/$100+RINC)
		ENDR

		REPT	3
		OBJCNTR	0,0,31,15
		OBJSCL	0,0,32,0,31,31,SKBEAM
		XYRATIO	100,(XRATIO_TO/$100+RINC),100,(XRATIO_TO/$100+RINC)
		ENDR

		REPT	5
		OBJCNTR	0,0,31,15
		OBJSCL	0,0,64,0,31,31,SKBEAM
		XYRATIO	100,(XRATIO_TO/$100+RINC),100,(XRATIO_TO/$100+RINC)
		ENDR

		REPT	10
		OBJCNTR	0,0,31,15
		OBJSCL	0,0,96,0,31,31,SKBEAM
		XYRATIO	100,(XRATIO_TO/$100+RINC),100,(XRATIO_TO/$100+RINC)
		ENDR

OBJ_BEAM_SET	DC.W	ID_BOTTLE	;OBJ_ID
		DC.L	BEAM_MOVE	;OBJ_MOVE_CTRL
		DC.L	BEAM_PRINT	;OBJ_PRN_CTRL
		DC.W	$10		;OBJ_ZSIZE
		XYSIZE	28,28
		DC.W	0		;OBJ_MASS
		DC.L	O_T2M_HIT	;OBJ_HIT_GOTO
		
; --------------------------------------------------------------------------

VIS_SKSPARK
		DC.W	0,$4800,$400
		DC.W	-$08,0,0
		DC.W	$0080,$0500
		DC.L	VIS_MOVE0
		DC.L	VIS_PRN0W
		DC.L	VIS_SPARK0_GRS
		DC.W	0			;SFX

VIS_SKSPLASHU	DC.W	$0000,$0000,$0000
		DC.W	0,0,0
		DC.W	$0100,$0600
		DC.L	VIS_MOVE0
		DC.L	VSK_PRN0NC
		DC.L	VIS_SKSPLASH_GRS
		DC.W	0			;SFX

VIS_SKSPLASHD	DC.W	$0000,$0000,$0000
		DC.W	0,0,0
		DC.W	$0080,$0600
		DC.L	VIS_MOVE0
		DC.L	VSK_PRN0NC
		DC.L	VIS_SKSPLASH_GRS
		DC.W	0			;SFX

VIS_SKSPLASH_GRS

		XYRATIO	100,140,100,80
		OBJCNTR	106,62,76,25

		OBJSCL	106,62,0,0,76,51,WSPLS1
		OBJSCL	97,53,0,0,93,64,WSPLS2
		OBJSCL	83,46,0,0,117,76,WSPLS3
		OBJSCL	68,37,0,0,146,93,WSPLS4
		OBJSCL	62,33,0,0,158,103,WSPLS5
		OBJSCL	49,30,0,0,175,113,WSPLS6

; --------------------------------------------------------------------------

		XYRATIO	100,350,100,350
		OBJCNTR	0,0,78,84

OBJ_SKULL_A	DC.L	OBJ_SKULL_SET
		DC.L	OBJ_SKULL_BOD_A
		DC.L	OBJ_SKULL_JAW_A
		DC.L	OBJ_SKULL_SKID
		DC.L	OBJ_SKULL_DAM
OBJ_SKULL_BOD_A	OBJSCL	0,0,0,0,78,82,SKULLA
		OBJSCL	0,0,0,96,78,82,SKULLA
		OBJSCL	0,0,80,0,78,82,SKULLA
		OBJSCL	0,0,80,96,78,82,SKULLA
		OBJSCL	0,0,160,0,78,82,SKULLA
OBJ_SKULL_JAW_A	OBJSCL	22,63,160,96,33,19,SKULLA

OBJ_SKULL_B	DC.L	OBJ_SKULL_SET
		DC.L	OBJ_SKULL_BOD_B
		DC.L	OBJ_SKULL_JAW_B
		DC.L	OBJ_SKULL_SKID
		DC.L	OBJ_SKULL_DAM
OBJ_SKULL_BOD_B	OBJSCL	0,0,0,0,78,82,SKULLB
		OBJSCL	0,0,0,96,78,82,SKULLB
		OBJSCL	0,0,80,0,78,82,SKULLB
		OBJSCL	0,0,80,96,78,82,SKULLB
		OBJSCL	0,0,160,0,78,82,SKULLB
OBJ_SKULL_JAW_B	OBJSCL	22,63,160,96,33,19,SKULLB

OBJ_SKULL_C	DC.L	OBJ_SKULL_SET
		DC.L	OBJ_SKULL_BOD_C
		DC.L	OBJ_SKULL_JAW_C
		DC.L	OBJ_SKULL_SKID
		DC.L	OBJ_SKULL_DAM
OBJ_SKULL_BOD_C	OBJSCL	0,0,0,0,78,82,SKULLC
		OBJSCL	0,0,0,96,78,82,SKULLC
		OBJSCL	0,0,80,0,78,82,SKULLC
		OBJSCL	0,0,80,96,78,82,SKULLC
		OBJSCL	0,0,160,0,78,82,SKULLC
OBJ_SKULL_JAW_C	OBJSCL	22,63,160,96,33,19,SKULLC

OBJ_SKULL_D	DC.L	OBJ_SKULL_SET
		DC.L	OBJ_SKULL_BOD_D
		DC.L	OBJ_SKULL_JAW_D
		DC.L	OBJ_SKULL_SKID
		DC.L	OBJ_SKULL_DAM
OBJ_SKULL_BOD_D	OBJSCL	0,0,0,0,78,82,SKULLD
		OBJSCL	0,0,0,96,78,82,SKULLD
		OBJSCL	0,0,80,0,78,82,SKULLD
		OBJSCL	0,0,80,96,78,82,SKULLD
		OBJSCL	0,0,160,0,78,82,SKULLD
OBJ_SKULL_JAW_D	OBJSCL	22,63,160,96,33,19,SKULLD

OBJ_SKULL_E	DC.L	OBJ_SKULL_SET
		DC.L	OBJ_SKULL_BOD_E
		DC.L	OBJ_SKULL_JAW_E
		DC.L	OBJ_SKULL_SKID
		DC.L	OBJ_SKULL_DAM
OBJ_SKULL_BOD_E	OBJSCL	0,0,0,0,78,82,SKULLE
		OBJSCL	0,0,0,96,78,82,SKULLE
		OBJSCL	0,0,80,0,78,82,SKULLE
		OBJSCL	0,0,80,96,78,82,SKULLE
		OBJSCL	0,0,160,0,78,82,SKULLE
OBJ_SKULL_JAW_E	OBJSCL	22,63,160,96,33,19,SKULLE

OBJ_SKULL_SKID	DC.W	2
		XSIZE	24
		XSIZE	-24

OBJ_SKULL_SET	DC.W	ID_CAR_NONHIT	;OBJ_ID
		DC.L	SKULL_MOVE	;OBJ_MOVE_CTRL
		DC.L	SKULL_PRINT	;OBJ_PRN_CTRL
		DC.W	$60		;OBJ_ZSIZE
		XYSIZE	76,70
		DC.W	$A0A0		;OBJ_MASS
		DC.L	HROUTINE_CAR	;OBJ_HIT_GOTO
		DC.W	$80*42		;OBJ_ZPOS
		DC.L	$100*200	;OBJ_XPOS
		DC.L	$100*0		;OBJ_YPOS
		DC.W	$50		;OBJ_SPD_MAXA
		DC.W	$90		;OBJ_SPD_MAXB
		DC.W	10		;OBJ_SPD_INCFA
		DC.W	10		;OBJ_SPD_INCFB
		DC.W	10		;OBJ_SPD_DECF
		DC.W	5		;OBJ_SPD_BRAKE
		DC.W	$0406		;OBJ_TRACTION

SKULL_DAM_B	MOVE.L	#OBJ_SKULL_B,OBJ_ADDR0(A1)
		BRA	DAM_SET1
SKULL_DAM_C	MOVE.L	#OBJ_SKULL_C,OBJ_ADDR0(A1)
		BRA	DAM_SET1
SKULL_DAM_D	MOVE.L	#OBJ_SKULL_D,OBJ_ADDR0(A1)
		BRA	DAM_SET1
SKULL_DAM_E	BSET	#2,OBJ_FLAG1(A1)
		BRA	DAM_SET1
SKULL_DAM_F	MOVE.L	#OBJ_SKULL_E,OBJ_ADDR0(A1)
		BRA	DAM_SET1
SKULL_DAM_G	BSET	#1,OBJ_FLAG1(A1)
		BRA	DAM_SET1

OBJ_SKULL_DAM	DC.W	0,$FFFF/100
		
		DC.W	1*$FFFF/8
		DC.L	OBJ_SKULL_DP1,SKULL_DAM_B
		DC.W	2*$FFFF/8
		DC.L	OBJ_SKULL_DP2,SKULL_DAM_C
		DC.W	3*$FFFF/8
		DC.L	OBJ_SKULL_DP3,SKULL_DAM_D
		DC.W	4*$FFFF/8
		DC.L	OBJ_SKULL_DP5,DAM_SET1
		DC.W	5*$FFFF/8
		DC.L	OBJ_SKULL_DP6,SKULL_DAM_E
		DC.W	6*$FFFF/8
		DC.L	OBJ_SKULL_DP4,SKULL_DAM_F
		DC.W	7*$FFFF/8
		DC.L	OBJ_SKULL_DP7,SKULL_DAM_G
		
		DC.W	$FFFF
		DC.L	OBJ_SKULL_KILL,KILL_SET2A

OBJ_SKULL_KILL	DC.W	$0020,5-1
		DC.L	VIS_EXPLD_C1
		DC.L	VIS_EXPLD_C2
		DC.L	VIS_EXPLD_C3
		DC.L	VIS_EXPLD_C4
		DC.L	VIS_EXPLD_C5
		DC.W	30,30-1,1,6
		DC.L	VIS_DOOR
		DC.L	VIS_PIPE
		DC.L	VIS_BUMP
		DC.L	VIS_WHEL
		DC.L	VIS_PIECE1
		DC.L	VIS_PIECE2

OBJ_SKULL_DP1	DC.L	VIS_FLAMEXLM,VIS_EXPCTRL_SK0
		DC.L	OBJ_SKDAM_XVIEW,OBJ_SKDAM_YVIEW
		DAMPTS	$00,16,54,0,0,0,0,0,0

OBJ_SKULL_DP2	DC.L	VIS_FLAMEXL,VIS_EXPCTRL_SK0
		DC.L	OBJ_SKDAM_XVIEW,OBJ_SKDAM_YVIEW
		DAMPTS	$00,63,11,0,0,0,0,0,0

OBJ_SKULL_DP3	DC.L	VIS_FLAMEXL,VIS_EXPCTRL_SK0
		DC.L	OBJ_SKDAM_XVIEW,OBJ_SKDAM_YVIEW
		DAMPTS	$00,70,70,0,0,0,0,0,0

OBJ_SKULL_DP4	DC.L	VIS_FLAMEXXL,VIS_EXPCTRL_SK1
		DC.L	OBJ_SKDAM_XVIEW,OBJ_SKDAM_YVIEW
		DAMPTS	$11,32,18,0,0,0,0,0,0

OBJ_SKULL_DP5	DC.L	VIS_FLAME1M,VIS_EXPCTRL_SK0
		DC.L	OBJ_SKDAM_XVIEW,OBJ_SKDAM_YVIEW
		DAMPTS	$11,46,68,0,0,0,0,0,0

OBJ_SKULL_DP6	DC.L	VIS_FLAME0M,VIS_EXPCTRL_SK0
		DC.L	OBJ_SKDAM_XVIEW,OBJ_SKDAM_YVIEW
		DAMPTS	$11,26,40,0,0,0,0,0,0

OBJ_SKULL_DP7	DC.L	VIS_FLAME0,VIS_EXPCTRL_SK0
		DC.L	OBJ_SKDAM_XVIEW,OBJ_SKDAM_YVIEW
		DAMPTS	$11,53,40,0,0,0,0,0,0

VIS_EXPCTRL_SK0	DC.L	VIS_EXPLD_C
		DC.W	8,8-1,0,6
		DC.L	VIS_PIECE6_B
		DC.L	VIS_PIPE
		DC.L	VIS_PIECE4_B
		DC.L	VIS_BUMP
		DC.L	VIS_PIECE2_B
		DC.L	VIS_PIECE1_B

VIS_EXPCTRL_SK1	DC.L	VIS_EXPLD_A
		DC.W	8,8-1,0,6
		DC.L	VIS_PIECE6_B
		DC.L	VIS_PIPE
		DC.L	VIS_PIECE4_B
		DC.L	VIS_BUMP
		DC.L	VIS_PIECE2_B
		DC.L	VIS_PIECE1_B

OBJ_SKDAM_XVIEW	DC.W	2*10,0
		DC.L	-1
OBJ_SKDAM_YVIEW	DC.W	0,-1

; --------------------------------------------------------------------------

LOGIC_SKULL

	LOGICEND

; --------------------------------------------------------------------------

		EVEN

; --------------------------------------------------------------------------
