; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ BATMAN RETURNS CD-ROM						   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

BANIM_SET_A	CLR.W	SRAM_DATA+WRAM_BGR_DLD
		MOVE.L	BANIM_OBJDATA,D0
		BNE.S	.clear
		RTS
.clear		CLR.L	BANIM_OBJDATA
		MOVE.L	D0,A1
		CLR.B	OBJ_FLAG0(A1)
		MOVE.L	OBJ_ADDR1(A1),A2
		BSR.S	.deall
		MOVE.L	OBJ_ADDR3(A1),A2
.deall		MOVEM.L	A0-A6/D0-D7,-(SP)
		MOVE.L	8+SCL_GR(A2),A1
		BRA	O_DEALL


BANIM_SET_B	MOVE.W	#4,SRAM_DATA+WRAM_BGR_DLD
		TST.L	BANIM_OBJDATA
		BNE.S	.end
		BSR	OBJECT_ALLOC
		BNE.S	.end
		MOVE.L	#SIGN_NEXT,OBJ_MOVE_CTRL(A1)
		MOVE.L	#BANIM_PRINT,OBJ_PRN_CTRL(A1)
		MOVE.L	#BGR_ROCKET,OBJ_ADDR0(A1)
		MOVE.L	#BGR_WHEEL,OBJ_ADDR2(A1)
		MOVE.L	A1,BANIM_OBJDATA

		MOVE.W	#SFX_CIRCUS_A,D0
		JSR	CSFX_ONESHOT_V

.end		RTS

BANIM_PRINT	CMP.W	#1,BG_TYPE	;Check if inside tunnel.
		BEQ	BAT_SETUP

		LEA	OBJ_ADDR0(A1),A4
	  	MOVE.W	OBJ_CNT0(A1),D2
		ADD.W	#-24,D2
		AND.W	#$3FE,D2
	  	MOVE.W	D2,OBJ_CNT0(A1)
		BSR	BACKANIM_PRINT
		LEA	OBJ_ADDR2(A1),A4
	  	MOVE.W	OBJ_CNT1(A1),D2
		ADD.W	#8,D2
		AND.W	#$3FE,D2
	  	MOVE.W	D2,OBJ_CNT1(A1)
		BRA	BACKANIM_PRINT
		
; --------------------------------------------------------------------------

BGR_ROCKET	XYPOS	189,-36
		SCALER	38,36,43,43,CBACKG

BGR_WHEEL	XYPOS	272,-29
		SCALER	104,40,47,47,CBACKG

; --------------------------------------------------------------------------

BAT_SETUP	JSR	RANDOM_NO
		AND.W	#$3E,D7
		BEQ.S	.set
		RTS
		
.set		MOVE.W	ZMOVE,D7
		BPL.S	.skp
		NEG.W	D7
.skp		LSR.W	#5,D7
		JSR	RANDOM_RANGE_U
.loop		MOVE.W	D7,-(SP)
		LEA	VIS_BATS,A0
		MOVE.W	#$F00,D0
		MOVE.L	#200*$100,D1
		MOVE.L	#-80*$100,D2
		MOVEQ	#0,D3
		JSR	VIS_SET
		BNE.S	.next
		MOVE.W	#$300,D7
		JSR	RANDOM_RANGE_U
		MOVE.W	D7,OBJ_CNT0(A1)
.next		MOVE.W	(SP)+,D7
		DBRA	D7,.loop
		RTS

BAT_MOVE	MOVE.W	ZMOVE,D1
		ASR.W	#2,D1
		ADD.W	D1,OBJ_ZPOS(A1)

		CMP.W	#$600,OBJ_ZPOS(A1)
		BPL.S	.vis_move1
		MOVE.W	#100,D7
		JSR	RANDOM_RANGE_U
		CMP.W	#95,D7
		BMI	.vis_move1
		MOVE.W	#SFX_BATNOISE_A,D0
		JSR	CSFX_ONESHOT_V
.vis_move1	JMP	VIS_MOVE1

VIS_BATS	DC.W	$80,$7FFF,$3000
		DC.W	-$28,0,0
		DC.W	$0080,$03FF
		DC.L	BAT_MOVE
		DC.L	VIS_PRN0
		DC.L	VIS_BAT_GRS
		DC.W	0

VIS_BAT_GRS	XYRATIO	100,200,100,200
		OBJCNTR	173,72,49,27

		OBJSCL	173,72,0,0,49,27,TUNBAT
		OBJSCL	167,82,49,0,61,17,TUNBAT
		OBJSCL	165,81,110,0,65,21,TUNBAT
		OBJSCL	171,82,175,0,53,22,TUNBAT

; --------------------------------------------------------------------------

BACKANIM_PRINT	MOVE.L	(A4)+,A0
		MOVE.L	(A4),A2

		MOVE.W	#$1800,D0
		MOVE.W	D0,D1
		BSR	O_PR_ALLOC_ENT
		BRA.L	.end
		BRA.L	.end
		BRA.L	.end
		PEA	.ret
		MOVEM.L	A0-A6/D0-D7,-(SP)
		MOVE.L	8+SCL_GR(A0),D6
		MOVE.L	8+SCL_GR(A2),D7
		BRA	O_ALLOC_DEALL
.ret		MOVE.L	A0,(A4)
		BNE.S	.cont
		MOVE.L	#SIGN_NEXT,$20(A6)		
		RTS

.cont		MOVEM.L	(A0)+,D4/D5
		SUB.L	PX_XPOS,D4
		ASR.L	#8,D4
		AND.W	#$1FF,D4
		SUB.W	#$80,D4
		ASR.L	#8,D5
		ADD.W	PX_YPOS,D5

		MOVE.L	SCL_HEIGHT(A0),D6
		SWAP	D6
		ROL.L	#4,D6
		SUB.W	D6,D5
		ROL.L	D6
		SWAP	D6
		MOVE.L	SCL_WIDTH(A0),D7
		SWAP	D7
		ROL.L	#4,D7
		SUB.W	D7,D4
		ROL.L	D7
		MOVE.W	D7,D6
		MOVE.L	D6,D3

		LEA	SIN_LOOKUP,A4
		MOVE.W	(A4,D2.W),D1
		SWAP	D1
		LEA	$100(A4),A4
		MOVE.W	(A4,D2.W),D1
		
		LEA	ROAD_CORD_DATA+(33*RCSTEP),A5
		MOVE.L	RCYMAX(A5),D7
		MOVE.L	RCXMAX(A5),D0	;Clip co-ords.
		CMP.W	#2,BG_TYPE
		BNE.S	.set
		MOVE.L	#$01000000,D0
		SWAP	D7
		CMP.W	BG_SIZE,D7
		BMI.S	.skp
		MOVE.W	BG_SIZE,D7
		BPL.S	.skp
		MOVEQ	#0,D7
.skp		SWAP	D7
		CLR.W	D7
.set		LEA	SCALE_ROTATE,A4
		MOVE.W	D4,(A6)+	
		MOVE.W	D5,(A6)+
		MOVEM.L	D0/D1/D2/D3/D6/D7/A0/A4,(A6)
.end		RTS

; --------------------------------------------------------------------------

