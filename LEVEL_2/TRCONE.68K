; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ BATMAN RETURNS CD-ROM						   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

; ROAD OBJECT - LEVEL 2 - "Traffic cone"

; --------------------------------------------------------------------------

CONE_SETUP	LEA	OBJ_CONE,A6
		BSR	O_MTR_SETUP
	    	BRA	O_MTR_SETFIN

; --------------------------------------------------------------------------

CONE_HIT	CMP.W	#IDRANGE_CAR,OBJ_ID(A1)
		BPL	OBJ_DET_NEXT
		MOVEM.L	D0-D7/A0-A6,-(SP)
		EXG	A2,A1
		MOVE.W	DET_MOVE_Z,D0
		BPL.S	.pos
		MOVE.W	#-$20,D3
		BRA.S	CONE_SPIN
.pos		ADD.W	OBJ_ZSIZE(A2),D0
		MOVEQ	#0,D3	       
       
CONE_SPIN	MOVE.L	OBJ_ADDR6(A1),A0
		CLR.W	RDDATA3(A0)
		BSR	O_CLEAR_S
		
		ADD.W	OBJ_ZPOS(A2),D0
		MOVE.L	OBJ_XPOS(A1),D1
		MOVE.L	OBJ_YPOS(A1),D2
		ADD.W	OBJ_SPD(A2),D3
		ASR.W	D3
		ADD.W	OBJ_SPD(A2),D3
		LEA	VIS_CONE,A0
		JSR	VIS_RTOBJ_SET
		MOVE.W	OBJ_XFORCE(A2),D0
		ADD.W	D0,OBJ_CNT4(A1)

		CMP.W	#ID_MAIN,OBJ_ID(A2)
		BNE.S	.done
		JSR	SCADD_ROADOBJ

.done		MOVEM.L	(SP)+,D0-D7/A0-A6
		JMP	OBJ_DET_NEXT

; --------------------------------------------------------------------------

OBJ_CONE
		XYRATIO	100,100,100,100
		OBJCNTR	0,0,25,30

		DC.L	OBJ_CONE_SET
		DC.L	OBJ_CONE_ANM0
		DC.L	OBJ_CONE_ANM0
		DC.L	0

OBJ_CONE_ANM0	DC.L	OBJ_CONE_XVIEW,OBJ_CONE_YVIEW

		OBJSCL	0,0,32,32,25,30,TRCONE
		
OBJ_CONE_XVIEW	DC.W	0*$20,$20
		DC.L	-1
OBJ_CONE_YVIEW	DC.W	5*$20
		DC.W	-1

OBJ_CONE_SET	DC.W	ID_ROADOBJ	;OBJ_ID
		DC.L	SIGN_NEXT	;OBJ_MOVE_CTRL
		DC.L	O_CAR_PRINT_S	;OBJ_PRN_CTRL
		DC.W	$18		;OBJ_ZSIZE
		XYSIZE	25,30
		DC.W	0		;OBJ_MASS
		DC.L	CONE_HIT	;OBJ_HIT_GOTO

VIS_CONE	DC.W	0,0,0
		DC.W	0,0,0
		DC.W	$0000,$0000
		DC.L	VIS_RTOBJ_MOVER
		DC.L	VIS_PRN_ROTATEX
		DC.L	VIS_CONE_GRS
		DC.W	SFX_CONEHIT_A	;SFX
		DC.W	$2C0,$140		;Lie angles
		DC.W	$2C0+$C0,$2C0-$C0	;Lie range.
		DC.W	500,6			;Ydecay/Xdecay.
		DC.W	$70,$800		;Spin/Xrand.
		DC.W	0			;Height range.
		DC.L	CHITGR_SFX

CHITGR_SFX	DC.W	4
		DC.W	SFX_CONEGND_A
		DC.W	SFX_CONEGND_B
		DC.W	SFX_CONEGND_C
		DC.W	SFX_CONEGND_D

		OBJCNTR	0,0,25,23
VIS_CONE_GRS	OBJSCL	0,0,45,51,30,30,TRCONE

; --------------------------------------------------------------------------
