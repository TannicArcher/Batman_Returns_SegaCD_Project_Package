; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ BATMAN RETURNS CD-ROM						   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

; MID BOSS - LEVEL 2 - "Fire trail tanker"

; --------------------------------------------------------------------------

TANK_SETUP	LEA	OBJ_TANK,A6
		JSR	O_OBJECT_SETUP 
		BNE.S	.end

		MOVE.L	#LOGIC_TANK,OBJ_LOGIC_DATA(A1)
		MOVE.W	#$60,OBJ_LMSPD(A1)

		MOVE.W	#SFX_ETANKER_A,OBJ_FX0_SFXNUM(A1)

		MOVEQ	#0,D0
.end		RTS
	 
; --------------------------------------------------------------------------

TANK_CLEAR	JSR	O_CLEAR_S
		JSR	CLEAR_DAMAGE
		JSR	SFX3D_KILL
		JSR	SCADD_MIDBOSS
		JMP	ECL_KILL

; --------------------------------------------------------------------------

TANK_MOVE	BTST	#O_CLEAR,OBJ_FLAG0(A1)
		BNE.S	TANK_CLEAR
	
		JSR	LOGIC_CTRL
		JSR	SFX3D_SPEED	
		JSR	DAMAGE_CTRL
		JSR	EDAMAGE_METER
		
		BTST.B	#O_DEAD,OBJ_FLAG0(A1)
		BNE.S	.end

		MOVE.W	#SFX_TRUCKHORN_C,D0
		JSR	HORN_SFX

		CMP.W	#$100,OBJ_ZPOS(A1)
		BMI.S	.fclr
		CMP.W	#$D00,OBJ_ZPOS(A1)
		BPL.S	.fclr

		TST.W	OBJ_CNT0(A1)
		BNE.S	.fdrop

		SUBQ.W	#1,OBJ_CNT1(A1)
		BPL.S	.end
		MOVE.W	#10,D7
		JSR	RANDOM_RANGE_U
		ADD.W	#16,D7
		MOVE.W	D7,OBJ_CNT0(A1)
		MOVE.W	#5,D7
		JSR	RANDOM_RANGE_U
		ADD.W	#10,D7
		MOVE.W	D7,OBJ_CNT1(A1)
		
		MOVE.W	#SFX_FTFLAME_A,D0
		JMP	CSFX_ONESHOT_V

.fclr		CLR.W	OBJ_CNT0(A1)
.end		RTS

.fdrop		SUBQ.W	#1,OBJ_CNT0(A1)
		BRA	FLAME_DROP

; --------------------------------------------------------------------------

TANK_PRINT	JSR	O_CAR_PRINT_S
		JMP	SHOW_DAMAGE

; --------------------------------------------------------------------------

OBJ_TANK
		XYRATIO	100,160,100,160
		OBJCNTR	115,48,59,72

		DC.L	OBJ_TANK_SET
		DC.L	OBJ_TANK_ANM0
		DC.L	OBJ_TANK_ANM1
		DC.L	OBJ_TANK_SKID
		DC.L	OBJ_TANK_DAM

OBJ_TANK_ANM0	DC.L	OBJ_TANK_XVIEW,OBJ_TANK_YVIEW

		OBJSCLM	107,48,0,0,103,79,TANKR6
		OBJSCLM	110,48,0,0,83,78,TANKR5
		OBJSCL	115,48,0,0,61,74,TANKR4
		OBJSCL	110,48,0,0,83,78,TANKR5
		OBJSCL	107,48,0,0,103,79,TANKR6

OBJ_TANK_ANM1	DC.L	OBJ_TANK_XVIEW,OBJ_TANK_YVIEW

		OBJSCLM	107,48,112,0,103,79,TANKR6
		OBJSCLM	110,48,96,0,83,78,TANKR5
		OBJSCL	115,48,64,0,61,74,TANKR4
		OBJSCL	110,48,96,0,83,78,TANKR5
		OBJSCL	107,48,112,0,103,79,TANKR6

OBJ_TANK_XVIEW	DC.W	2*$20,$20
		DC.L	$3000,$8000,-1
OBJ_TANK_YVIEW	DC.W	0,-1

OBJ_TANK_SKID	DC.W	2
		XSIZE	24
		XSIZE	-24

OBJ_TANK_SET	DC.W	ID_CAR		;OBJ_ID
		DC.L	TANK_MOVE	;OBJ_MOVE_CTRL
		DC.L	TANK_PRINT	;OBJ_PRN_CTRL
		DC.W	$C0		;OBJ_ZSIZE
		XYSIZE	58,55
		DC.W	$7060		;OBJ_MASS
		DC.L	HROUTINE_CAR	;OBJ_HIT_GOTO
		DC.W	$80*40		;OBJ_ZPOS
		DC.L	$100*200	;OBJ_XPOS
		DC.L	$100*0		;OBJ_YPOS
		DC.W	$60		;OBJ_SPD_MAXA
		DC.W	$A0		;OBJ_SPD_MAXB
		DC.W	15		;OBJ_SPD_INCFA
		DC.W	15		;OBJ_SPD_INCFB
		DC.W	15		;OBJ_SPD_DECF
		DC.W	5		;OBJ_SPD_BRAKE
		DC.W	$0606		;OBJ_TRACTION

OBJ_TANK_DAM	DC.W	($FFFF/$280)/10,$FFFF/150
		
		DC.W	1*$FFFF/7
		DC.L	OBJ_TANK_DPL,DAM_SET0
		DC.W	2*$FFFF/7
		DC.L	OBJ_TANK_DPL,DAM_SET0
		DC.W	3*$FFFF/7
		DC.L	OBJ_TANK_DPL,DAM_SET0
		DC.W	4*$FFFF/7
		DC.L	OBJ_TANK_DPL,DAM_SET0
		DC.W	5*$FFFF/7
		DC.L	OBJ_TANK_DPL,DAM_SET0
		DC.W	6*$FFFF/7
		DC.L	OBJ_TANK_DPL,DAM_SET0
		
		DC.W	$FFFF
		DC.L	LOGIC_DEATH1,LOGIC_SET0
		DC.L	OBJ_TANK_KILL,KILL_SET0

OBJ_TANK_KILL	DC.W	$0020,5-1
		DC.L	VIS_EXPLD_C1
		DC.L	VIS_EXPLD_C2
		DC.L	VIS_EXPLD_C3
		DC.L	VIS_EXPLD_C4
		DC.L	VIS_EXPLD_C5
		DC.W	30,30-1,1,6
		DC.L	VIS_DOOR
		DC.L	VIS_PIPE
		DC.L	VIS_BUMP
		DC.L	VIS_WHEL
		DC.L	VIS_PIECE1
		DC.L	VIS_PIECE2

OBJ_TANK_DPL	DC.W	8
		DC.L	OBJ_TANK_DP1
		DC.L	OBJ_TANK_DP2
		DC.L	OBJ_TANK_DP3
		DC.L	OBJ_TANK_DP4
		DC.L	OBJ_TANK_DP5
		DC.L	OBJ_TANK_DP6
		DC.L	OBJ_TANK_DP7
		DC.L	OBJ_TANK_DP8

OBJ_TANK_DP1	DC.L	VIS_FLAME0,VIS_EXPCTRL_L
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTS	$11,123,79,$11,114,78,$11,110,78

OBJ_TANK_DP2	DC.L	VIS_FLAME0M,VIS_EXPCTRL_B
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTS	$11,156,113,$11,151,114,$11,147,113

OBJ_TANK_DP3	DC.L	VIS_FLAME2M,VIS_EXPCTRL_R
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTS	$00,168,87,$01,174,87,$01,178,89

OBJ_TANK_DP4	DC.L	VIS_FLAME3M,VIS_EXPCTRL_R
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTS	$00,164,64,$01,183,64,$01,194,64

OBJ_TANK_DP5	DC.L	VIS_FLAME3M,VIS_EXPCTRL_R
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTS	$00,163,79,$01,191,79,$01,208,79

OBJ_TANK_DP6	DC.L	VIS_FLAME2,VIS_EXPCTRL_L
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTSM	$00,168,87,$01,174,87,$01,178,89

OBJ_TANK_DP7	DC.L	VIS_FLAME3,VIS_EXPCTRL_L
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTSM	$00,164,64,$01,183,64,$01,194,64

OBJ_TANK_DP8	DC.L	VIS_FLAME3,VIS_EXPCTRL_L
		DC.L	OBJ_TDAM_XVIEW,OBJ_TDAM_YVIEW
		DAMPTSM	$00,163,79,$01,191,79,$01,208,79

OBJ_TDAM_XVIEW	DC.W	2*10,10
		DC.L	$3000,$8000,-1
OBJ_TDAM_YVIEW	DC.W	0,-1

; --------------------------------------------------------------------------

FLAME_SETUP	MOVE.L	A1,A2
		LEA	OBJ_FLAME,A6
		JSR	O_OBJECT_SETUP 
		EXG	A1,A2
		BNE.S	.end

		JSR	RANDOM_NO
		MOVE.W	D7,OBJ_CNT0(A2)
		MOVE.L	#FLM_HIT,OBJ_ADDR7(A2)

		MOVE.L	OBJ_XPOS(A1),OBJ_XPOS(A2)
		MOVE.L	OBJ_YPOS(A1),OBJ_YPOS(A2)
		MOVE.W	OBJ_ZPOS(A1),OBJ_ZPOS(A2)

.end		RTS

FLM_CLEAR	JMP	O_CLEAR_S

FLM_PRINT	JSR	O_PR_ALLOCX
		BRA.L	FLM_CLEAR
		BRA.L	FLM_CLEAR
		BRA.L	FLM_CLEAR

		LEA	OBJ_FLAME_ANM0,A0
		ADD.W	#$20,OBJ_CNT0(A1)
		MOVE.W	OBJ_CNT0(A1),D1
		AND.W	#$E0,D1
		ADD.W	D1,A0

		JSR	O_CALC_PATCHZ
		MOVE.L	OBJ_ADDR1(A1),A2
		JSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		JMP	O_PR_SET0

FLM_HIT		ADDQ.W	#1,OBJ_SHOT_AMT(A2)	
		JSR	O_CLEAR_S

		MOVE.W	#SFX_FTFLMHIT_A,D0
		JMP	CSFX_ONESHOT_V

OBJ_FLAME	DC.L	OBJ_FLAME_SET
		DC.L	OBJ_FLAME_ANM0

OBJ_FLAME_ANM0	
		XYRATIO	100,200,100,200
		OBJCNTR	139,104,13,24

		OBJSCL	139,104,0,0,13,24,TNKFIR
		OBJSCL	140,104,14,0,13,24,TNKFIR
		
		XYRATIO	100,200,100,300
		OBJCNTR	139,104,13,24

		OBJSCL	139,105,28,0,14,23,TNKFIR
		OBJSCL	139,103,43,0,13,25,TNKFIR

		XYRATIO	100,200,100,400
		OBJCNTR	139,104,13,24

		OBJSCL	139,104,0,0,13,24,TNKFIR
		OBJSCL	140,104,14,0,13,24,TNKFIR
		
		XYRATIO	100,200,100,300
		OBJCNTR	139,104,13,24

		OBJSCL	139,105,28,0,14,23,TNKFIR
		OBJSCL	139,103,43,0,13,25,TNKFIR


OBJ_FLAME_SET	DC.W	ID_BOTTLE	;OBJ_ID
		DC.L	SIGN_NEXT	;OBJ_MOVE_CTRL
		DC.L	FLM_PRINT	;OBJ_PRN_CTRL
		DC.W	$30		;OBJ_ZSIZE
		XYSIZE	13,24
		DC.W	0		;OBJ_MASS
		DC.L	O_T2M_HIT	;OBJ_HIT_GOTO

; --------------------------------------------------------------------------

FLAME_DROP	JSR	O_CALC_PATCH
		LEA	OBJ_DFLAME_POS,A0
		JSR	O_CALC_VIEW
		MOVE.L	A0,-(SP)
		MOVEM.L	(A0),D0/D1
		ADD.L	D0,OBJ_XPOS(A1)
		ADD.L	D1,OBJ_YPOS(A1)

		MOVE.L	OBJ_XPOS(A1),D6

		LEA	DFLM_SET_DATA,A4
		
		MOVE.L	OBJ_SPD(A1),D0
		MOVE.W	OBJ_ZPOS(A1),D1
		SUB.W	#$80,D1
		MOVEQ	#0,D7

		JSR	O_THROW_TO_POS
		BNE.S	.end
		
		MOVE.L	#4*$100,D7
		JSR	RANDOM_RANGE_S
		ADD.L	D7,OBJ_XPOS(A2)
		MOVE.L	#4*$100,D7
		JSR	RANDOM_RANGE_S
		ADD.L	D7,OBJ_YPOS(A2)

.end		MOVE.L	(SP)+,A0
		MOVEM.L	(A0),D0/D1
		SUB.L	D0,OBJ_XPOS(A1)
		SUB.L	D1,OBJ_YPOS(A1)
		RTS

FHIT_GROUND	BSR	FLAME_SETUP
		JMP	O_CLEAR_S

FHIT_OBJECT	ADDQ.W	#4,OBJ_SHOT_AMT(A2)
		JSR	O_CLEAR_S

		MOVE.W	#SFX_FTFLMHIT_A,D0
		JMP	CSFX_ONESHOT_V

DFLM_PRINT	MOVE.L	OBJ_ADDR0(A1),A0
		MOVE.L	OBJGR_ANM0(A0),A0
		ADD.W	#$20,OBJ_CNT0(A1)
		MOVE.W	OBJ_CNT0(A1),D1
		AND.W	#$1E0,D1
		LEA	(A0,D1.W),A0

		MOVE.L	OBJ_ADDR1(A1),A2
		MOVE.W	D0,D1
		JSR	OBJ_PR_ALLOCATE
		BRA.L	.deall
		BRA.L	.deall
		BRA.L	.end

		JSR	O_CALC_PATCHZ
		JSR	O_ALLOC_DEALL_S
		MOVE.L	A0,OBJ_ADDR1(A1)
		JMP	O_PR_SET0

.deall		JSR	O_DEALL_S
		CLR.L	OBJ_ADDR1(A1)
.end		RTS

; --------------------------------------------------------------------------

DFLM_SET_DATA	DC.L	OBJ_DFLAME	;Object setup.
		DC.L	FHIT_GROUND	;Ground hit routine.
		DC.L	FHIT_OBJECT	;Object hit routine.
		DC.L	0*$100		;YPOS offset.
;		DC.L	30*$100		;Yheight.
;		DC.W	3,8		;Up&down steps.
		DC.L	40*$100		;Yheight.
		DC.W	2,4		;Up&down steps.
		DC.L	0		;Zlimit.
		DC.L	0		;Xlimit.

OBJ_DFLAME	DC.L	OBJ_DFLAME_SET
		DC.L	OBJ_DFLAME_ANM0

OBJ_DFLAME_ANM0	

;		XYRATIO	100,40,100,40
;RINC		=	10
;		REPT	18
;		OBJCNTR	0,0,13,24
;		OBJSCL	0,0,0,0,13,24,TNKFIR
;		XYRATIO	100,(XRATIO_TO/$100+RINC),100,(XRATIO_TO/$100+RINC)
;		ENDR

		XYRATIO	100,25,100,25
RINC		=	8
		REPT	10
		OBJCNTR	0,0,26,15
		OBJSCL	0,0,0,0,26,29,FIRTST
		XYRATIO	100,(XRATIO_TO/$100+RINC),100,(XRATIO_TO/$100+RINC)
		ENDR


		XYRATIO	100,120,100,120

OBJ_DFLAME_SET	DC.W	ID_BOTTLE	;OBJ_ID
		DC.L	O_T2M_MOVE	;OBJ_MOVE_CTRL
		DC.L	DFLM_PRINT	;OBJ_PRN_CTRL
		DC.W	$30		;OBJ_ZSIZE
		XYSIZE	26,15
		DC.W	0		;OBJ_MASS
		DC.L	O_T2M_HIT	;OBJ_HIT_GOTO

		XYRATIO	100,160,100,160
		OBJCNTR	115,48,59,72

OBJ_DFLAME_POS	DC.L	OBJ_DFLM_XVIEW,OBJ_DFLM_YVIEW
		
		OBJPOSM	123,102
		OBJPOSM	131,102
		OBJPOS	144,102
		OBJPOS	131,102
		OBJPOS	123,102

OBJ_DFLM_XVIEW	DC.W	2*8,8
		DC.L	$3000,$8000,-1
OBJ_DFLM_YVIEW	DC.W	0,-1

; --------------------------------------------------------------------------

LOGIC_TANK
	IFTRUE
	BATRANGE 1,3
 		IFTRUE
			GRIDRANGE 1,32,1,64
			GRIDGOTOX 15
		IFEND
     	
	ELSE_IFTRUEx
    	BATRANGE 4,8
    		IFTRUE
    			GRIDRANGE 1,32,30,64
    			GRIDGOTOX 7
		ELSE_IFTRUE
			GRIDRANGE 1,11,8,29
			GRIDGOTOXY 7,16
		ELSE_IFTRUE
			GRIDRANGE 1,11,3,7
			GRIDGOTOXY 7,16
		ELSE_IFTRUE
			GRIDRANGE 1,11,1,2
			GRIDGOTOXY 7,6
		ELSE_IFTRUE
			GRIDRANGE 12,21,8,29
			GRIDGOTOXY 7,16
		ELSE_IFTRUE
			GRIDRANGE 12,21,3,7
			GRIDGOTOXY 7,10
		ELSE_IFTRUE
			GRIDRANGE 12,21,1,2
			GRIDGOTOXY 7,1
		ELSE_IFTRUE
			GRIDRANGE 22,32,8,29
			GRIDGOTOXY 15,16
		ELSE_IFTRUE
			GRIDRANGE 22,32,3,7
			GRIDGOTOXY 16,9 
	        ELSE_IFTRUE
			GRIDRANGE 22,32,1,2
			GRIDGOTOXY 11,1
		IFEND
    		
 	ELSE_IFTRUEx
    	BATRANGE 9,13
    		IFTRUE
    			GRIDRANGE 1,32,30,64
    			GRIDGOTOX 13
		ELSE_IFTRUE
			GRIDRANGE 1,8,8,29
			GRIDGOTOXY 11,16
	  	ELSE_IFTRUE
			GRIDRANGE 1,8,3,7
			GRIDGOTOXY 11,10
		ELSE_IFTRUE
			GRIDRANGE 1,8,1,2
			GRIDGOTOY 6
		ELSE_IFTRUE
			GRIDRANGE 9,16,8,29
			GRIDGOTOXY 11,16
		ELSE_IFTRUE
			GRIDRANGE 9,16,3,7
			GRIDGOTOXY 11,10
		ELSE_IFTRUE
			GRIDRANGE 9,16,1,2
			GRIDGOTOXY 11,1
		ELSE_IFTRUE
			GRIDRANGE 17,32,8,29
			GRIDGOTOXY 11,16
		ELSE_IFTRUE
			GRIDRANGE 17,32,3,7
			GRIDGOTOXY 11,10
	        ELSE_IFTRUE
			GRIDRANGE 17,32,1,2
			GRIDGOTOXY 11,1
		IFEND  

	ELSE_IFTRUEx
    	BATRANGE 14,18
    		IFTRUE
    			GRIDRANGE 1,32,30,64
    			GRIDGOTOX 13
		ELSE_IFTRUE
			GRIDRANGE 1,8,8,29
			GRIDGOTOXY 16,16
	  	ELSE_IFTRUE
			GRIDRANGE 1,8,3,7
			GRIDGOTOXY 16,10
		ELSE_IFTRUE
			GRIDRANGE 1,8,1,2
			GRIDGOTOY 6
		ELSE_IFTRUE
			GRIDRANGE 9,16,8,29
			GRIDGOTOXY 16,16
		ELSE_IFTRUE
			GRIDRANGE 9,16,3,7
			GRIDGOTOXY 16,10
		ELSE_IFTRUE
			GRIDRANGE 9,16,1,2
			GRIDGOTOXY 16,1
		ELSE_IFTRUE
			GRIDRANGE 17,32,8,29
			GRIDGOTOXY 16,16
		ELSE_IFTRUE
			GRIDRANGE 17,32,3,7
			GRIDGOTOXY 16,10
	        ELSE_IFTRUE
			GRIDRANGE 17,32,1,2
			GRIDGOTOXY 16,1
		IFEND  
    	   
 	ELSE_IFTRUEx
    	BATRANGE 18,22
    	      	IFTRUE
    			GRIDRANGE 1,32,30,64
    			GRIDGOTOX 21
		ELSE_IFTRUE
			GRIDRANGE 1,11,8,29
			GRIDGOTOXY 13,16
		ELSE_IFTRUE
			GRIDRANGE 1,11,3,7
			GRIDGOTOXY 13,10
	        ELSE_IFTRUE
			GRIDRANGE 1,11,1,2
			GRIDGOTOXY 13,1
		ELSE_IFTRUE
			GRIDRANGE 12,16,8,29
			GRIDGOTOXY 21,16
		ELSE_IFTRUE
			GRIDRANGE 12,16,3,7
			GRIDGOTOXY 20,9
		ELSE_IFTRUE
			GRIDRANGE 12,16,1,2
			GRIDGOTOXY 20,1
		ELSE_IFTRUE
			GRIDRANGE 17,32,8,29
			GRIDGOTOXY 20,16
		ELSE_IFTRUE
			GRIDRANGE 17,32,3,7
			GRIDGOTOXY 20,9
	        ELSE_IFTRUE
			GRIDRANGE 22,32,1,2
			GRIDGOTOY 6
		IFEND
 	
 	ELSE_IFTRUE
    	BATRANGE 23,28
    		IFTRUE
    			GRIDRANGE 1,32,30,64
    			GRIDGOTOX 26
		ELSE_IFTRUE
			GRIDRANGE 1,11,8,29
			GRIDGOTOXY 14,16
 		ELSE_IFTRUE
			GRIDRANGE 1,11,3,7
			GRIDGOTOXY 14,10
		ELSE_IFTRUE
			GRIDRANGE 1,11,1,2
			GRIDGOTOXY 14,1	
	       	ELSE_IFTRUE
			GRIDRANGE 12,21,8,29
			GRIDGOTOXY 25,16
	       	ELSE_IFTRUE
			GRIDRANGE 12,21,3,7
			GRIDGOTOXY 25,10
		ELSE_IFTRUE
			GRIDRANGE 12,21,1,2
			GRIDGOTOXY 24,1
		ELSE_IFTRUE
			GRIDRANGE 22,32,8,29
			GRIDGOTOXY 25,16
		ELSE_IFTRUE
			GRIDRANGE 22,32,3,7
			GRIDGOTOXY 25,9
	        ELSE_IFTRUE
			GRIDRANGE 22,32,1,2
			GRIDGOTOY 6
		     
		IFEND
 	ELSE_IFTRUE
    	BATRANGE 29,32
    		IFTRUE
    			GRIDRANGE 1,32,1,64
    			GRIDGOTOX 16
		IFEND
	IFEND
	LOGICEND		       			

; --------------------------------------------------------------------------

		EVEN

; --------------------------------------------------------------------------
